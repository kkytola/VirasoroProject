/-
Copyright (c) 2024 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.Algebra.Lie.Abelian
import VirasoroProject.ToMathlib.Algebra.Lie.Basic

/-!
# Lie algebra cohomology in degree two (for central extensions)

(*WARNING*: This file needs cleaning up. It was not the main goal and it was the first time I
tried to use multilinear maps in Lean.)

This file defines Lie algebra 2-cocycles and 2-coboundaries and constructs the Lie algebra
cohomology in degree two, with coefficients in a vector space (an Abelian Lie algebra).

## Main definitions

* `LieOneCocycle`: The set CÂ¹(ğ“–,ğ“) of 1-cocycles of a Lie algebra ğ“– with coefficients in a
  vector space ğ“.
* `LieTwoCocycle`: The set CÂ²(ğ“–,ğ“) of 2-cocycles of a Lie algebra ğ“– with coefficients in a
  vector space ğ“.
* `LieTwoCoboundary`: The subspace BÂ²(ğ“–,ğ“) âŠ† CÂ²(ğ“–,ğ“) of 2-coboundaries.
* `LieTwoCohomology`: The 2-cohomology HÂ²(ğ“–,ğ“) := CÂ²(ğ“–,ğ“) â§¸ BÂ²(ğ“–,ğ“) of a Lie algebra ğ“– with
  coefficients in a vector space ğ“.

## Main statements

* `LieTwoCocycle.toLieTwoCohomologyEquiv`: If ğ“– is abelian, then the canonical projection
  from 2-cocycles to 2-cohomologies is a linear isomorphism.

## Implementation notes

This file needs some clean-up! (But it works for the purposes of concrete calculations of
central extensions etc.)

A reasonable thing to do would be to define Lie algebra cohomology in general degrees. But for
concrete applications, the special case of degree two probably deserves its own API. Once a
general definition is made, the API for the degree 2 case (especially central extensions)
could be refactored. (But even in the current generality, clean-up is needed here.)

## Tags

Lie algebra, cohomology

-/

namespace VirasoroProject

universe u
variable (ğ•œ : Type*) [CommRing ğ•œ]
variable (ğ“– ğ“ : Type u) [LieRing ğ“–] [AddCommGroup ğ“] [LieAlgebra ğ•œ ğ“–] [Module ğ•œ ğ“]

section LieOneCocycle

/-! ### Lie algebra 1-cocycles -/

/-- Lie algebra 1-cocycles. -/
@[ext] structure LieOneCocycle where
  /-- The underlying linear map of a Lie algebra 1-cocycle. -/
  toLinearMap : ğ“– â†’â‚—[ğ•œ] ğ“

instance : Zero (LieOneCocycle ğ•œ ğ“– ğ“) where
  zero := { toLinearMap := 0 }

instance : Add (LieOneCocycle ğ•œ ğ“– ğ“) where
  add Î² Î²' := { toLinearMap := Î².toLinearMap + Î²'.toLinearMap }

instance : SMul ğ•œ (LieOneCocycle ğ•œ ğ“– ğ“) where
  smul c Î² := { toLinearMap := c â€¢ Î².toLinearMap }

namespace LieOneCocycle

@[simp]
lemma toLinearMap_zero : (0 : LieOneCocycle ğ•œ ğ“– ğ“).toLinearMap = 0 := rfl

@[simp]
lemma toLinearMap_add (Î² Î²' : LieOneCocycle ğ•œ ğ“– ğ“) :
    (Î² + Î²').toLinearMap = Î².toLinearMap + Î²'.toLinearMap := rfl

@[simp]
lemma toLinearMap_smul (c : ğ•œ) (Î² : LieOneCocycle ğ•œ ğ“– ğ“) :
    (c â€¢ Î²).toLinearMap = c â€¢ Î².toLinearMap := rfl

instance : AddCommMonoid (LieOneCocycle ğ•œ ğ“– ğ“) where
  add_assoc Î² Î²' Î²'' := by
    ext1
    simp only [LieOneCocycle.toLinearMap_add]
    exact add_assoc Î².toLinearMap Î²'.toLinearMap Î²''.toLinearMap
  zero_add Î² := by ext1 ; simp
  add_zero Î² := by ext1 ; simp
  add_comm Î² Î²' := by
    ext1
    simp only [LieOneCocycle.toLinearMap_add]
    exact AddCommMagma.add_comm Î².toLinearMap Î²'.toLinearMap
  nsmul n Î² := { toLinearMap := n â€¢ Î².toLinearMap }
  nsmul_zero Î² := by ext1 ; simp only [zero_smul] ; rfl
  nsmul_succ n Î² := by
    ext1
    simpa only [LieOneCocycle.toLinearMap_add] using succ_nsmul Î².toLinearMap n

instance : Module ğ•œ (LieOneCocycle ğ•œ ğ“– ğ“) where
  one_smul Î² := by ext1 ; simp
  mul_smul c c' Î² := by ext1 ; simp ; exact mul_smul c c' Î².toLinearMap
  smul_zero Î² := by ext1 ; simp
  smul_add c Î² Î²' := by ext1 ; simp
  add_smul c c' Î² := by ext1 ; simp ; exact Module.add_smul c c' Î².toLinearMap
  zero_smul Î² := by ext1 ; simp

instance : AddCommGroup (LieOneCocycle ğ•œ ğ“– ğ“) where
  zero_add Î² := AddZeroClass.zero_add Î²
  add_zero Î² := AddZeroClass.add_zero Î²
  nsmul_zero Î² := zero_nsmul Î²
  nsmul_succ n Î² := succ_nsmul Î² n
  neg Î² := (-1 : ğ•œ) â€¢ Î²
  sub Î²â‚ Î²â‚‚ := Î²â‚ + (-1 : ğ•œ) â€¢ Î²â‚‚
  zsmul k Î² := (k : ğ•œ) â€¢ Î²
  zsmul_zero' Î² := by simp only [Int.cast_zero, zero_smul]
  zsmul_succ' k Î² := by simp [add_smul]
  zsmul_neg' k Î² := by simp [add_smul, smul_smul, add_comm]
  neg_add_cancel Î² := by
    nth_rewrite 2 [â† one_smul ğ•œ Î²]
    simp only [â† add_smul, neg_add_cancel, zero_smul]
  add_comm Î²â‚ Î²â‚‚ := AddCommMagma.add_comm Î²â‚ Î²â‚‚

variable {ğ•œ ğ“– ğ“}

instance : FunLike (LieOneCocycle ğ•œ ğ“– ğ“) ğ“– ğ“ where
  coe := fun Î² X â†¦ Î².toLinearMap X
  coe_injective' := fun Î² Î²' h â†¦ by ext1 ; exact LinearMap.ext_iff.mpr (congrFun h)

instance : LinearMapClass (LieOneCocycle ğ•œ ğ“– ğ“) ğ•œ ğ“– ğ“ where
  map_add Î² X Y := Î².toLinearMap.map_add X Y
  map_smulâ‚›â‚— Î² c X := LinearMap.CompatibleSMul.map_smul Î².toLinearMap c X

end LieOneCocycle -- namespace

end LieOneCocycle -- section

section LieTwoCocycle

/-! ### Lie algebra 2-cocycles -/

/-- Lie algebra 2-cocycles. -/
@[ext] structure LieTwoCocycle where
  /-- The underlying bilinear map of a Lie algebra 2-cocycle. -/
  toBilin : ğ“– â†’â‚—[ğ•œ] ğ“– â†’â‚—[ğ•œ] ğ“
  self' : âˆ€ X, toBilin X X = 0
  leibniz' : âˆ€ X Y Z, toBilin X â…Y, Zâ† = toBilin â…X, Yâ† Z + toBilin Y â…X, Zâ†

namespace LieTwoCocycle

instance : FunLike (LieTwoCocycle ğ•œ ğ“– ğ“) ğ“– (ğ“– â†’â‚—[ğ•œ] ğ“) where
  coe := fun Î³ X â†¦ LieTwoCocycle.toBilin Î³ X
  coe_injective' := by
    intro Î³ Î³' h
    ext
    exact congrFun (congrArg DFunLike.coe (congrFun h _)) _

instance : LinearMapClass (LieTwoCocycle ğ•œ ğ“– ğ“) ğ•œ ğ“– (ğ“– â†’â‚—[ğ•œ] ğ“) where
  map_add Î³ X Y := (LieTwoCocycle.toBilin Î³).map_add X Y
  map_smulâ‚›â‚— Î³ c X := (LieTwoCocycle.toBilin Î³).map_smul c X

variable {ğ•œ ğ“– ğ“}
variable (Î³ : LieTwoCocycle ğ•œ ğ“– ğ“)

@[simp]
lemma self {X : ğ“–} : Î³ X X = 0 := Î³.self' X

lemma leibniz {X Y Z : ğ“–} : Î³ X â…Y, Zâ† = Î³ â…X, Yâ† Z + Î³ Y â…X, Zâ† := Î³.leibniz' X Y Z

lemma apply_add (Xâ‚ Xâ‚‚ Y : ğ“–) : Î³ (Xâ‚ + Xâ‚‚) Y = Î³ Xâ‚ Y + Î³ Xâ‚‚ Y := by simp

lemma apply_addâ‚‚ (X Yâ‚ Yâ‚‚ : ğ“–) : Î³ X (Yâ‚ + Yâ‚‚) = Î³ X Yâ‚ + Î³ X Yâ‚‚ := by simp

lemma apply_smul (c : ğ•œ) (X Y : ğ“–) : Î³ (c â€¢ X) Y = c â€¢ Î³ X Y := by simp

lemma apply_smulâ‚‚ (c : ğ•œ) (X Y : ğ“–) : Î³ X (c â€¢ Y) = c â€¢ Î³ X Y := by simp

lemma skew (X Y : ğ“–) : -(Î³ Y X) = Î³ X Y := by
  have aux : Î³ (X + Y) X + Î³ (X + Y) Y = 0 := by
    rw [â† LieTwoCocycle.apply_addâ‚‚]
    exact LieTwoCocycle.self Î³
  simpa [neg_eq_iff_add_eq_zero] using aux

instance : Zero (LieTwoCocycle ğ•œ ğ“– ğ“) where
  zero := { toBilin := 0 , self' := by simp , leibniz' := by simp }

instance : Add (LieTwoCocycle ğ•œ ğ“– ğ“) where
  add Î³ Î³' :=
    { toBilin := Î³.toBilin + Î³'.toBilin
      self' := fun X â†¦ by simp [Î³.self', Î³'.self']
      leibniz' := fun X Y Z â†¦ by
        simp only [LinearMap.add_apply, Î³.leibniz' X Y Z, Î³'.leibniz' X Y Z]
        simp only [add_assoc, add_right_inj]
        simp only [â† add_assoc, add_left_inj]
        rw [add_comm] }

instance : SMul ğ•œ (LieTwoCocycle ğ•œ ğ“– ğ“) where
  smul c Î³ :=
    { toBilin := c â€¢ Î³.toBilin
      self' := fun X â†¦ by simp [Î³.self']
      leibniz' := fun X Y Z â†¦ by simp only [LinearMap.smul_apply, Î³.leibniz' X Y Z, smul_add] }

@[simp]
lemma toBilin_zero : (0 : LieTwoCocycle ğ•œ ğ“– ğ“).toBilin = 0 := rfl

@[simp]
lemma toBilin_add (Î³ Î³' : LieTwoCocycle ğ•œ ğ“– ğ“) :
    (Î³ + Î³').toBilin = Î³.toBilin + Î³'.toBilin := rfl

@[simp]
lemma toBilin_smul (c : ğ•œ) (Î³ : LieTwoCocycle ğ•œ ğ“– ğ“) :
    (c â€¢ Î³).toBilin = c â€¢ Î³.toBilin := rfl

instance : AddCommMonoid (LieTwoCocycle ğ•œ ğ“– ğ“) where
  add_assoc Î³ Î³' Î³'' := by
    ext1
    simp only [LieTwoCocycle.toBilin_add]
    exact add_assoc _ _ _
  zero_add Î³ := by ext1 ; simp only [LieTwoCocycle.toBilin_add, add_left_eq_self] ; rfl
  add_zero Î³ := by ext1 ; simp only [LieTwoCocycle.toBilin_add, add_right_eq_self] ; rfl
  add_comm Î³ Î³' := by ext1 ; simp only [LieTwoCocycle.toBilin_add] ; exact AddCommMagma.add_comm _ _
  nsmul n Î³ :=
    { toBilin := n â€¢ Î³.toBilin
      self' := fun X â†¦ by simp only [LinearMap.smul_apply, Î³.self', smul_zero]
      leibniz' := fun X Y Z â†¦ by simp only [LinearMap.smul_apply, Î³.leibniz' X Y Z, smul_add] }
  nsmul_zero Î³ := by ext1 ; simp only [zero_smul] ; rfl
  nsmul_succ n Î³ := by
    ext1
    simpa only [LieTwoCocycle.toBilin_add] using succ_nsmul Î³.toBilin n

instance : Module ğ•œ (LieTwoCocycle ğ•œ ğ“– ğ“) where
  one_smul Î³ := by ext1 ; simp
  mul_smul c c' Î³ := by ext1 ; simp ; exact mul_smul c c' Î³.toBilin
  smul_zero Î³ := by ext1 ; simp
  smul_add c Î³ Î³' := by ext1 ; simp
  add_smul c c' Î³ := by ext1 ; simp ; exact Module.add_smul c c' Î³.toBilin
  zero_smul Î³ := by ext1 ; simp

instance [AddCommGroup ğ“–] [Module ğ•œ ğ“–] [LieAlgebra ğ•œ ğ“–] [AddCommGroup ğ“] [Module ğ•œ ğ“] :
    AddCommGroup (LieTwoCocycle ğ•œ ğ“– ğ“) where
  zero_add Î³ := AddZeroClass.zero_add Î³
  add_zero Î³ := AddZeroClass.add_zero Î³
  nsmul_zero Î³ := zero_nsmul Î³
  nsmul_succ n Î³ := succ_nsmul Î³ n
  neg Î³ := (-1 : ğ•œ) â€¢ Î³
  sub Î³â‚ Î³â‚‚ := Î³â‚ + (-1 : ğ•œ) â€¢ Î³â‚‚
  zsmul k Î³ := (k : ğ•œ) â€¢ Î³
  zsmul_zero' Î³ := by simp only [Int.cast_zero, zero_smul]
  zsmul_succ' k Î³ := by simp [add_smul]
  zsmul_neg' k Î³ := by simp [add_smul, smul_smul, add_comm]
  neg_add_cancel Î³ := by
    nth_rewrite 2 [â† one_smul ğ•œ Î³]
    simp only [â† add_smul, neg_add_cancel, zero_smul]
  add_comm Î³â‚ Î³â‚‚ := AddCommMagma.add_comm Î³â‚ Î³â‚‚

lemma add_apply (Î³â‚ Î³â‚‚ : LieTwoCocycle ğ•œ ğ“– ğ“) (X Y : ğ“–) :
    (Î³â‚ + Î³â‚‚) X Y = Î³â‚ X Y + Î³â‚‚ X Y := rfl

lemma smul_apply (c : ğ•œ) (Î³ : LieTwoCocycle ğ•œ ğ“– ğ“) (X Y : ğ“–) :
    (c â€¢ Î³) X Y = c â€¢ Î³ X Y := rfl

lemma sub_apply (Î³â‚ Î³â‚‚ : LieTwoCocycle ğ•œ ğ“– ğ“) (X Y : ğ“–) :
    (Î³â‚ - Î³â‚‚) X Y = Î³â‚ X Y - Î³â‚‚ X Y := by
  simp only [sub_eq_add_neg, LieTwoCocycle.add_apply]
  rw [show -Î³â‚‚ = (-1 : ğ•œ) â€¢ Î³â‚‚ from rfl, LieTwoCocycle.smul_apply]
  simp

@[simp] lemma zero_apply (X Y : ğ“–) : (0 : LieTwoCocycle ğ•œ ğ“– ğ“) X Y = 0 := rfl

@[simp] lemma zero_apply' (X : ğ“–) : (0 : LieTwoCocycle ğ•œ ğ“– ğ“) X = 0 := rfl

end LieTwoCocycle -- namespace

end LieTwoCocycle -- section

section LieTwoCoboundary

/-! ### Lie algebra 2-coboundaries -/

variable {ğ•œ ğ“– ğ“}

/-- A Lie algebra 1-cocycle determines a bilinear map via the differential. -/
private def LieOneCocycle.bdry' (Î² : LieOneCocycle ğ•œ ğ“– ğ“) : ğ“– â†’â‚—[ğ•œ] ğ“– â†’â‚—[ğ•œ] ğ“ where
  toFun := fun X â†¦ Î² âˆ˜â‚— LieAlgebra.bracketHom ğ•œ ğ“– X
  map_add' Xâ‚ Xâ‚‚ := by simp_all only [map_add] ; ext ; simp_all
  map_smul' c X := by simp_all only [LinearMapClass.map_smul, RingHom.id_apply] ; ext ; simp_all

/-- A Lie algebra 1-cocycle linearly determines a bilinear map via the differential. -/
private def LieOneCocycle.bdryHom' : LieOneCocycle ğ•œ ğ“– ğ“ â†’â‚—[ğ•œ] ğ“– â†’â‚—[ğ•œ] ğ“– â†’â‚—[ğ•œ] ğ“ where
  toFun := fun Î² â†¦ LieOneCocycle.bdry' Î²
  map_add' Î²â‚ Î²â‚‚ := by dsimp ; ext X Y; rfl
  map_smul' c Z := by dsimp ; ext X Y; rfl

/-- The `âˆ‚` of a Lie algebra 1-cocycle as a Lie algebra 2-cocycle. -/
def LieOneCocycle.bdry (Î² : LieOneCocycle ğ•œ ğ“– ğ“) : LieTwoCocycle ğ•œ ğ“– ğ“ where
  toBilin := LieOneCocycle.bdryHom' Î²
  self' X := by simp [LieOneCocycle.bdryHom', LieOneCocycle.bdry']
  leibniz' X Y Z := by simp [LieOneCocycle.bdryHom', LieOneCocycle.bdry']

variable (ğ•œ ğ“– ğ“)

/-- The `âˆ‚` as a linear map from Lie algebra 1-cocycles to Lie algebra 2-cocycles. -/
def lieOneCocycle_bdryHom : LieOneCocycle ğ•œ ğ“– ğ“ â†’â‚—[ğ•œ] LieTwoCocycle ğ•œ ğ“– ğ“ where
  toFun Î² := Î².bdry
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

@[simp] lemma LieOneCocycle.neg_bdry (Î² : LieOneCocycle ğ•œ ğ“– ğ“) : (-Î²).bdry = -Î².bdry := by
  change lieOneCocycle_bdryHom ğ•œ ğ“– ğ“ (-Î²) = -lieOneCocycle_bdryHom ğ•œ ğ“– ğ“ Î²
  simp

lemma LieOneCocycle.bdry_apply (Î² : LieOneCocycle ğ•œ ğ“– ğ“) (X Y : ğ“–) :
    Î².bdry X Y = Î² (â…X, Yâ†) := rfl

/-- Lie algebra 2-coboundaries as a vector space. -/
abbrev LieTwoCoboundary := LinearMap.range (lieOneCocycle_bdryHom ğ•œ ğ“– ğ“)

end LieTwoCoboundary -- section

section LieTwoCohomology

/-! ### Lie algebra 2-cohomology -/

/-- The 2-cohomology `HÂ²(ğ“–,ğ“)` of a Lie algebra `ğ“–` with coefficients in `ğ“`. -/
def LieTwoCohomology := LieTwoCocycle ğ•œ ğ“– ğ“ â§¸ LieTwoCoboundary ğ•œ ğ“– ğ“

namespace LieTwoCohomology

/-- The 2-cohomology `HÂ²(ğ“–,ğ“)` is an additive commutative group. -/
instance : AddCommGroup (LieTwoCohomology ğ•œ ğ“– ğ“) := by
  change AddCommGroup (LieTwoCocycle ğ•œ ğ“– ğ“ â§¸ LieTwoCoboundary ğ•œ ğ“– ğ“)
  exact Submodule.Quotient.addCommGroup (LieTwoCoboundary ğ•œ ğ“– ğ“)

/-- The 2-cohomology `HÂ²(ğ“–,ğ“)` is a module over the scalar ring `ğ•œ`. -/
instance : Module ğ•œ (LieTwoCohomology ğ•œ ğ“– ğ“) := by
  change Module ğ•œ (LieTwoCocycle ğ•œ ğ“– ğ“ â§¸ LieTwoCoboundary ğ•œ ğ“– ğ“)
  exact Submodule.Quotient.module' _

end LieTwoCohomology -- namespace

namespace LieTwoCocycle

/-- The linear map from 2-cocycles to 2-cohomologies of a Lie algebra `ğ“–` with coefficients
in `ğ“`. -/
def toLieTwoCohomology : LieTwoCocycle ğ•œ ğ“– ğ“ â†’â‚—[ğ•œ] LieTwoCohomology ğ•œ ğ“– ğ“ :=
  (LieTwoCoboundary ğ•œ ğ“– ğ“).mkQ

lemma range_toLieTwoCohomology_eq_top :
    LinearMap.range (toLieTwoCohomology ğ•œ ğ“– ğ“) = âŠ¤ :=
  Submodule.range_mkQ ..

variable {ğ•œ ğ“– ğ“}

/-- The projection to 2-cohomologies from 2-cocycles of a Lie algebra `ğ“–` with coefficients
in `ğ“`. (This definition is to enable dot notation, while the linear map version doesn't.) -/
def cohomologyClass (Î³ : LieTwoCocycle ğ•œ ğ“– ğ“) : LieTwoCohomology ğ•œ ğ“– ğ“ :=
  LieTwoCocycle.toLieTwoCohomology _ _ _ Î³

/-- Adding a coboundary does not change the cohomology class. -/
lemma cohomologyClass_add_bdry (Î³ : LieTwoCocycle ğ•œ ğ“– ğ“) (Î² : LieOneCocycle ğ•œ ğ“– ğ“) :
    (Î³ + Î².bdry).cohomologyClass = Î³.cohomologyClass := by
  simp only [cohomologyClass, map_add, add_right_eq_self]
  apply (Submodule.Quotient.mk_eq_zero _).mpr <| LinearMap.mem_range.mpr âŸ¨Î², rflâŸ©

/-- A cocycle representing a trivial cohomology class is a coboundary. -/
lemma exists_eq_bdry (hÎ³ : Î³.cohomologyClass = 0) :
    âˆƒ Î² : LieOneCocycle ğ•œ ğ“– ğ“, Î³ = Î².bdry := by
  simp_rw [@Eq.comm (LieTwoCocycle ğ•œ ğ“– ğ“) Î³ _]
  simpa using (Submodule.Quotient.eq _).mp <|
    show Î³.cohomologyClass = LieTwoCocycle.cohomologyClass 0 by rw [hÎ³] ; rfl

end LieTwoCocycle -- namespace

end LieTwoCohomology -- section

section IsLieAbelian

variable [IsLieAbelian ğ“–]

variable {ğ•œ ğ“– ğ“}

/-- For abelian Lie algebras, a 2-coboundary is necessarily zero. -/
lemma LieOneCocycle.bdry_apply_eq_zero_of_isLieAbelian (Î² : LieOneCocycle ğ•œ ğ“– ğ“) (X Y : ğ“–) :
    Î².bdry X Y = 0 := by
  simp [LieOneCocycle.bdry_apply]

variable (ğ•œ ğ“– ğ“)

/-- For abelian Lie algebras, the space of 2-coboundaries is the zero vector space. -/
lemma LieTwoCoboundary.eq_bot_of_isLieAbelian :
    LieTwoCoboundary ğ•œ ğ“– ğ“ = âŠ¥ := by
  refine LinearMap.range_eq_bot.mpr ?_
  ext Î² X Y
  exact Î².bdry_apply_eq_zero_of_isLieAbelian X Y

/-- For abelian Lie algebras, the map from 2-cocycles to their cohomology classes has
trivial kernel. -/
lemma LieTwoCocycle.ker_toLieTwoCohomology_eq_bot_of_isLieAbelian :
    LinearMap.ker (LieTwoCocycle.toLieTwoCohomology ğ•œ ğ“– ğ“) = âŠ¥ := by
  rw [LieTwoCocycle.toLieTwoCohomology, Submodule.ker_mkQ]
  exact LieTwoCoboundary.eq_bot_of_isLieAbelian ğ•œ ğ“– ğ“

/-- For abelian Lie algebras, the map from 2-cocycles to their cohomology classes is a linear
equivalence. -/
noncomputable def LieTwoCocycle.toLieTwoCohomologyEquiv :
    LieTwoCocycle ğ•œ ğ“– ğ“ â‰ƒâ‚—[ğ•œ] LieTwoCohomology ğ•œ ğ“– ğ“ :=
  LinearEquiv.ofBijective (LieTwoCocycle.toLieTwoCohomology ğ•œ ğ“– ğ“)
    âŸ¨LinearMap.ker_eq_bot.mp <| ker_toLieTwoCohomology_eq_bot_of_isLieAbelian ..,
     LinearMap.range_eq_top.mp <| range_toLieTwoCohomology_eq_top ..âŸ©

lemma LieTwoCocycle.toLieTwoCohomologyEquiv_toLinearMap :
    (toLieTwoCohomologyEquiv ğ•œ ğ“– ğ“).toLinearMap = toLieTwoCohomology ğ•œ ğ“– ğ“ := rfl

end IsLieAbelian --section

end VirasoroProject -- namespace
