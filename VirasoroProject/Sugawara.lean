/-
Copyright (c) 2025 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import VirasoroProject.VirasoroAlgebra
import VirasoroProject.ToMathlib.Topology.Algebra.Module.LinearMap.Defs
import Mathlib

/-!
# The bosonic Sugawara construction
-/

namespace VirasoroProject



section Sugawara_boson

open Filter

variable {ğ•œ : Type*} [Field ğ•œ]
variable {V : Type*} [AddCommGroup V] [Module ğ•œ V]

/-- Commutator `[A,B] := AB-BA` of two linear operators `A`, `B`. -/
def _root_.LinearMap.commutator (A B : V â†’â‚—[ğ•œ] V) : V â†’â‚—[ğ•œ] V := A * B - B * A

/-- `[A,B] = -[B,A]` -/
lemma _root_.LinearMap.commutator_comm (A B : V â†’â‚—[ğ•œ] V) :
    A.commutator B = - B.commutator A := by
  simp [LinearMap.commutator]

variable (V) in
/-- Commutator `[â¬,â¬]` as a bilinear map on the space of linear maps. -/
noncomputable def _root_.LinearMap.commutatorBilin :
    (V â†’â‚—[ğ•œ] V) â†’â‚—[ğ•œ] (V â†’â‚—[ğ•œ] V) â†’â‚—[ğ•œ] (V â†’â‚—[ğ•œ] V) where
  toFun A :=
    { toFun := fun B â†¦ A.commutator B
      map_add' Bâ‚ Bâ‚‚ := by
        simp [LinearMap.commutator, mul_add, add_mul, sub_eq_add_neg]
        ac_rfl
      map_smul' c B := by simp [LinearMap.commutator, smul_sub] }
  map_add' Aâ‚ Aâ‚‚ := by
    ext1 B
    simp [LinearMap.commutator, add_mul, mul_add, sub_eq_add_neg]
    ac_rfl
  map_smul' c A := by
    ext1 B
    simp [LinearMap.commutator, smul_sub]

@[simp] lemma _root_.LinearMap.commutatorBilin_applyâ‚‚ (A B : V â†’â‚—[ğ•œ] V) :
    LinearMap.commutatorBilin V A B = A.commutator B := rfl

lemma _root_.LinearMap.mul_eq_mul_add_commutator (A B : V â†’â‚—[ğ•œ] V) :
    A * B = B * A + A.commutator B := by
  simp [LinearMap.commutator]

/-- `[AB,C] = A[B,C] + [A,C]B` -/
lemma _root_.LinearMap.commutator_pair (A B C : V â†’â‚—[ğ•œ] V) :
    (A * B).commutator C = A * B.commutator C + A.commutator C * B := by
  simp [LinearMap.commutator, sub_mul, mul_sub, â† mul_assoc]

/-- `[A,BC] = B[A,C] + [A,B]C` -/
lemma _root_.LinearMap.commutator_pair' (A B C : V â†’â‚—[ğ•œ] V) :
    A.commutator (B * C) = B * A.commutator C + A.commutator B * C := by
  simp [LinearMap.commutator, sub_mul, mul_sub, â† mul_assoc]

variable (heiOper : â„¤ â†’ (V â†’â‚—[ğ•œ] V))
variable (heiTrunc : âˆ€ v, atTop.Eventually (fun l â†¦ (heiOper l) v = 0))
variable (heiComm : âˆ€ k l,
  (heiOper k).commutator (heiOper l) = if k + l = 0 then (k : ğ•œ) â€¢ 1 else 0)

section normal_ordered_pair

/-- Normal ordered pair of two operators:
`pairNO k l` equals `(heiOper l) âˆ˜ (heiOper k)` if `l â‰¤ k`,
and `(heiOper k) âˆ˜ (heiOper l)` otherwise. -/
def pairNO (k l : â„¤) : (V â†’â‚—[ğ•œ] V) :=
  if l â‰¤ k then ((heiOper l) âˆ˜â‚— (heiOper k)) else ((heiOper k) âˆ˜â‚— (heiOper l))

/-- Alternative normal ordered pair of two operators:
`pairNO' k l` equals `(heiOper l) âˆ˜ (heiOper k)` if `k â‰¥ 0`,
and `(heiOper k) âˆ˜ (heiOper l)` otherwise. -/
def pairNO' (k l : â„¤) : (V â†’â‚—[ğ•œ] V) :=
  if 0 â‰¤ k then ((heiOper l) âˆ˜â‚— (heiOper k)) else ((heiOper k) âˆ˜â‚— (heiOper l))

lemma pairNO_apply_eq_zero (A : â„¤ â†’ (V â†’â‚—[ğ•œ] V)) {v : V} {N : â„¤}
    (A_trunc : âˆ€ n â‰¥ N, A n v = 0) {k l : â„¤} (h : N â‰¤ max k l) :
    (pairNO A k l) v = 0 := by
  cases' le_sup_iff.mp h with k_large l_large
  Â· by_cases hlk : l â‰¤ k
    Â· simp [pairNO, hlk, A_trunc k k_large]
    Â· simp [pairNO, hlk, A_trunc l (by linarith)]
  Â· by_cases hlk : l â‰¤ k
    Â· simp [pairNO, hlk, A_trunc k (by linarith)]
    Â· simp [pairNO, hlk, A_trunc l l_large]

include heiComm

/-- `heiOper k` and `heiOper l` commute unless `k = l`. -/
lemma heiComm_of_add_ne_zero {k l : â„¤} (hkl : k + l â‰  0) :
    (heiOper k) âˆ˜â‚— (heiOper l) = (heiOper l) âˆ˜â‚— (heiOper k) := by
  simpa [hkl, sub_eq_zero, LinearMap.commutator] using heiComm k l

variable {heiOper}

/-- The two definitions of normal ordered pairs coincide. -/
lemma heiOper_pairNO_eq_pairNO' (k l : â„¤) :
    pairNO heiOper k l = pairNO' heiOper k l := by
  unfold pairNO pairNO'
  by_cases hk : 0 â‰¤ k
  Â· simp only [hk, â†“reduceIte, ite_eq_left_iff, not_le]
    intro hkl
    apply heiComm_of_add_ne_zero _ heiComm
    exact ne_of_gt (by linarith)
  Â· simp only [hk, â†“reduceIte, ite_eq_right_iff]
    intro hlk
    apply heiComm_of_add_ne_zero _ heiComm
    exact ne_of_lt (by linarith)

include heiTrunc in
omit heiComm in
lemma finite_support_smul_pairNO_heiOper_apply {ğ•‚ : Type*} [SMulZeroClass ğ•‚ V]
    (n m : â„¤) (a : â„¤ â†’ ğ•‚) (v : V) :
    (Function.support fun k â†¦ a k â€¢ ((pairNO heiOper (m - k) (n + k)) v)).Finite := by
  obtain âŸ¨N, hNâŸ© := eventually_atTop.mp <| heiTrunc v
  apply (Set.finite_Ioo (m - N) (N - n)).subset
  simp only [Function.support_subset_iff, ne_eq, Set.mem_Icc, tsub_le_iff_right]
  intro k hk
  by_contra con
  apply hk
  rw [pairNO_apply_eq_zero heiOper hN ?_, smul_zero]
  by_cases h : N â‰¤ n + k
  Â· exact le_sup_of_le_right h
  Â· apply le_sup_of_le_left
    simp only [Set.mem_Ioo, not_and, not_lt, tsub_le_iff_right] at con
    by_contra con'
    linarith [con (by linarith)]

include heiTrunc in
omit heiComm in
lemma finite_support_pairNO_heiOper_apply (n m : â„¤) (v : V) :
    (Function.support fun k â†¦ ((pairNO heiOper (m - k) (n + k)) v)).Finite := by
  apply (finite_support_smul_pairNO_heiOper_apply heiTrunc n m  (fun _ â†¦ 1) v).subset
  intro k hk
  simp only [Function.mem_support, ne_eq, one_smul] at hk âŠ¢
  intro con
  simp [hk] at con

include heiTrunc in
lemma finite_support_smul_pairNO'_heiOper_apply {ğ•‚ : Type*} [SMulZeroClass ğ•‚ V]
    (n m : â„¤) (a : â„¤ â†’ ğ•‚) (v : V) :
    (Function.support fun k â†¦ a k â€¢ ((pairNO' heiOper (m - k) (n + k)) v)).Finite := by
  apply (finite_support_smul_pairNO_heiOper_apply heiTrunc n m a v).subset
  intro j hj
  convert hj using 2
  simp_rw [heiOper_pairNO_eq_pairNO' heiComm]

include heiTrunc in
omit heiComm in
lemma finite_support_smul_pairNO_heiOper_applyâ‚€ {ğ•‚ : Type*} [SMulZeroClass ğ•‚ V]
    (s : â„¤) (a : â„¤ â†’ ğ•‚) (v : V) :
    (Function.support fun k â†¦ (a k â€¢ (pairNO heiOper (s - k) k) v)).Finite := by
  simpa using finite_support_smul_pairNO_heiOper_apply heiTrunc 0 s a v

include heiTrunc in
omit heiComm in
lemma finite_support_pairNO_heiOper_applyâ‚€ (s : â„¤) (v : V) :
    (Function.support fun k â†¦ ((pairNO heiOper (s - k) k) v)).Finite := by
  simpa using finite_support_pairNO_heiOper_apply heiTrunc 0 s v

include heiTrunc in
lemma finite_support_pairNO'_heiOper_apply (n m : â„¤) (v : V) :
    (Function.support fun k â†¦ ((pairNO' heiOper (m - k) (n + k)) v)).Finite := by
  apply (finite_support_pairNO_heiOper_apply heiTrunc n m v).subset
  intro j hj
  convert hj using 2
  simp_rw [heiOper_pairNO_eq_pairNO' heiComm]

include heiTrunc in
lemma finite_support_smul_pairNO'_heiOper_applyâ‚€ {ğ•‚ : Type*} [SMulZeroClass ğ•‚ V]
    (s : â„¤) (a : â„¤ â†’ ğ•‚) (v : V) :
    (Function.support fun k â†¦ (a k â€¢ (pairNO' heiOper (s - k) k) v)).Finite := by
  simpa using finite_support_smul_pairNO'_heiOper_apply heiTrunc heiComm 0 s a v

include heiTrunc in
lemma finite_support_pairNO'_heiOper_applyâ‚€ (s : â„¤) (v : V) :
    (Function.support fun k â†¦ ((pairNO' heiOper (s - k) k) v)).Finite := by
  simpa using finite_support_pairNO'_heiOper_apply heiTrunc heiComm 0 s v

variable (heiOper)

omit heiComm

/-- `pairNO k l` is symmetric in `k` and `l`. -/
lemma heiOper_pairNO_symm (k l : â„¤) :
    pairNO heiOper k l = pairNO heiOper l k := by
  unfold pairNO ; grind

include heiComm

/-- `pairNO' k l` is symmetric in `k` and `l`. -/
lemma heiOper_pairNO'_symm (k l : â„¤) :
    pairNO' heiOper k l = pairNO' heiOper l k := by
  simpa [â† heiOper_pairNO_eq_pairNO' heiComm] using heiOper_pairNO_symm heiOper k l

omit heiComm
include heiTrunc

lemma heiPairNO_trunc_atTop_sub (n : â„¤) (v : V) :
    atTop.Eventually (fun k â†¦ pairNO heiOper (n-k) k v = 0) := by
  obtain âŸ¨N, hNâŸ© := eventually_atTop.mp (heiTrunc v)
  filter_upwards [Ici_mem_atTop N] with l hl
  rw [Set.mem_Ici] at hl
  by_cases hln : l â‰¤ n-l
  Â· simp [pairNO, hln, hN _ (show N â‰¤ n-l by linarith)]
  Â· simp [pairNO, hln, hN _ (show N â‰¤ l by linarith)]

lemma heiPairNO_trunc_atBot_sub (n : â„¤) (v : V) :
    atBot.Eventually (fun k â†¦ pairNO heiOper (n-k) k v = 0) := by
  obtain âŸ¨N, hNâŸ© := eventually_atTop.mp (heiTrunc v)
  filter_upwards [Iic_mem_atBot (n-N)] with l hl
  rw [Set.mem_Iic] at hl
  by_cases hln : l â‰¤ n-l
  Â· simp [pairNO, hln, hN _ (show N â‰¤ n-l by linarith)]
  Â· simp [pairNO, hln, hN _ (show N â‰¤ l by linarith)]

lemma heiPairNO_trunc_cofinite_sub (n : â„¤) (v : V) :
    cofinite.Eventually (fun k â†¦ pairNO heiOper (n-k) k v = 0) := by
  obtain âŸ¨T, hTâŸ© := eventually_atTop.mp (heiPairNO_trunc_atTop_sub heiOper heiTrunc n v)
  obtain âŸ¨B, hBâŸ© := eventually_atBot.mp (heiPairNO_trunc_atBot_sub heiOper heiTrunc n v)
  have filt : (Set.Ioo B T)á¶œ âˆˆ cofinite :=
    Set.Finite.compl_mem_cofinite (show (Set.Ioo B T).Finite from Set.finite_Ioo B T)
  filter_upwards [filt] with k hkBT
  simp only [Set.mem_compl_iff, Set.mem_Ioo, not_and, not_lt] at hkBT
  by_cases hk : k â‰¤ B
  Â· exact hB k hk
  Â· exact hT k (hkBT (Int.lt_of_not_ge hk))

open Topology

noncomputable def sugawaraGenAux (n : â„¤) (v : V) : V :=
  (2 : ğ•œ)â»Â¹ â€¢ âˆ‘á¶  k, pairNO heiOper (n-k) k v

omit heiTrunc in
lemma sugawaraGenAux_def (n : â„¤) (v : V) :
    sugawaraGenAux heiOper n v = (2 : ğ•œ)â»Â¹ â€¢ âˆ‘á¶  k, pairNO heiOper (n-k) k v :=
  rfl

omit heiTrunc in
lemma sugawaraGenAux_comp_apply (A : V â†’â‚—[ğ•œ] V) (n : â„¤) (v : V) :
    (sugawaraGenAux heiOper n (A v))
      = (2 : ğ•œ)â»Â¹ â€¢ âˆ‘á¶  k, pairNO heiOper (n-k) k (A v) := by
  rw [sugawaraGenAux_def heiOper n (A v)]

variable {heiOper} in
lemma comp_sugawaraGenAux_apply (A : V â†’â‚—[ğ•œ] V) (n : â„¤) (v : V) :
    A (sugawaraGenAux heiOper n v) = (2 : ğ•œ)â»Â¹ â€¢ âˆ‘á¶  k, A (pairNO heiOper (n-k) k v) := by
  rw [sugawaraGenAux_def heiOper n v, map_smul, A.map_finsum]
  exact finite_support_pairNO_heiOper_applyâ‚€ heiTrunc n v

omit heiTrunc

-- NOTE: Should be in Mathlib?
def continuousConstSMul_of_discreteTopology (ğ•œ X : Type*) [TopologicalSpace X]
    [DiscreteTopology X] [AddCommMonoid X] [SMul ğ•œ X] :
    ContinuousConstSMul ğ•œ X :=
  âŸ¨fun c â†¦ by continuityâŸ©

variable {heiOper}

include heiTrunc

lemma sugawaraGenAux_add (n : â„¤) (v w : V) :
    sugawaraGenAux heiOper n (v + w) = sugawaraGenAux heiOper n v + sugawaraGenAux heiOper n w := by
  simp only [sugawaraGenAux_def, map_add, â† smul_add]
  congr 1
  rw [finsum_add_distrib]
  Â· exact finite_support_pairNO_heiOper_applyâ‚€ heiTrunc n v
  Â· exact finite_support_pairNO_heiOper_applyâ‚€ heiTrunc n w

variable (heiOper) in
omit heiTrunc in
lemma sugawaraGenAux_smul (n : â„¤) (c : ğ•œ) (v : V) :
    sugawaraGenAux heiOper n (c â€¢ v) = c â€¢ sugawaraGenAux heiOper n v := by
  simp [sugawaraGenAux_def, map_smul, smul_finsum, smul_comm c]

noncomputable def sugawaraGen (n : â„¤) : V â†’â‚—[ğ•œ] V where
  toFun := sugawaraGenAux heiOper n
  map_add' v w := sugawaraGenAux_add heiTrunc n v w
  map_smul' c v := sugawaraGenAux_smul heiOper n c v

lemma sugawaraGen_apply (n : â„¤) (v : V) :
    sugawaraGen heiTrunc n v = (2 : ğ•œ)â»Â¹ â€¢ âˆ‘á¶  k, pairNO heiOper (n-k) k v :=
  rfl

lemma sugawaraGen_apply_eq_tsum_shift (n s : â„¤) (v : V) :
    sugawaraGen heiTrunc n v
      = (2 : ğ•œ)â»Â¹ â€¢ âˆ‘á¶  k, pairNO heiOper (n - (k + s)) (k + s) v := by
  rw [sugawaraGen_apply]
  congr 1
  let Ïƒ : â„¤ â‰ƒ â„¤ := âŸ¨fun n â†¦ n + s, fun n â†¦ n - s, fun n â†¦ by simp, fun n â†¦ by simpâŸ©
  rw [â† finsum_comp_equiv Ïƒ]
  rfl

lemma commutator_sugawaraGen_apply_eq_tsum_commutator_apply (n : â„¤) (A : V â†’â‚—[ğ•œ] V) (v : V) :
    (sugawaraGen heiTrunc n).commutator A v =
      (2 : ğ•œ)â»Â¹ â€¢ âˆ‘á¶  k, ((pairNO heiOper (n - k) k).commutator A) v := by
  simp only [LinearMap.commutator, LinearMap.sub_apply, Module.End.mul_apply]
  simp_rw [sub_eq_add_neg]
  rw [finsum_add_distrib]
  Â· rw [smul_add]
    congr
    convert comp_sugawaraGenAux_apply heiTrunc (-A) n v using 1
  Â· exact finite_support_pairNO_heiOper_applyâ‚€ heiTrunc n (A v)
  Â· apply (finite_support_pairNO_heiOper_applyâ‚€ heiTrunc n v).subset
    refine Function.support_subset_iff'.mpr ?_
    simp only [Function.mem_support, ne_eq, not_not, neg_eq_zero, â† sub_eq_add_neg]
    intro k hk
    simp [hk]

lemma sugawaraGen_commutator_apply_eq_tsum_commutator_apply (n : â„¤) (A : V â†’â‚—[ğ•œ] V) (v : V) :
    A.commutator (sugawaraGen heiTrunc n) v =
      (2 : ğ•œ)â»Â¹ â€¢ âˆ‘á¶  k, A.commutator (pairNO heiOper (n-k) k) v := by
  rw [LinearMap.commutator_comm, LinearMap.neg_apply]
  rw [commutator_sugawaraGen_apply_eq_tsum_commutator_apply, â† smul_neg, â† finsum_neg_distrib]
  congr 2
  funext j
  rw [LinearMap.commutator_comm, LinearMap.neg_apply, neg_neg]

omit heiTrunc

include heiComm

/-- `[(heiOper l) âˆ˜ (heiOper k), (heiOper m)] = -m * (Î´[k+m=0] + Î´[l+m=0]) â€¢ heiOper (k + l + m)` -/
lemma commutator_heiPair_heiGen (l k m : â„¤) :
    ((heiOper l) * (heiOper k)).commutator (heiOper m)
      = ((-m : ğ•œ) * ((if k + m = 0 then 1 else 0)
               + (if l + m = 0 then 1 else 0))) â€¢ heiOper (k + l + m) := by
  simp [LinearMap.commutator_pair, heiComm]
  by_cases hkm : k + m = 0
  Â· simp [show k = -m by linarith]
    by_cases hlm : l + m = 0
    Â· simp [show l = -m by linarith, mul_add, add_smul]
    Â· simp [hlm]
  Â· simp [hkm]
    by_cases hlm : l + m = 0
    Â· simp [show l = -m by linarith]
    Â· simp [hlm]

/-- `[:(heiOper l)(heiOper k):, (heiOper m)] = -m * (Î´[k+m=0] + Î´[l+m=0]) â€¢ heiOper (k + l + m)` -/
lemma commutator_heiPairNO_heiGen (l k m : â„¤) :
    (pairNO heiOper l k).commutator (heiOper m)
      = ((-m : ğ•œ) * ((if k + m = 0 then 1 else 0)
               + (if l + m = 0 then 1 else 0))) â€¢ heiOper (k + l + m) := by
  by_cases hlk : k â‰¤ l
  Â· simp [pairNO, hlk]
    have key := @commutator_heiPair_heiGen ğ•œ _ V _ _ heiOper heiComm k l m
    simp only [neg_mul, neg_smul, Module.End.mul_eq_comp] at key âŠ¢
    rw [key, neg_inj, show k + l + m = l + k + m by ring, add_comm]
  Â· simp [pairNO, hlk]
    have key := @commutator_heiPair_heiGen ğ•œ _ V _ _ heiOper heiComm l k m
    simp only [Module.End.mul_eq_comp, neg_mul, neg_smul] at key âŠ¢
    rw [key]

include heiTrunc

/-- `[L(n), J(m)] = -m â€¢ J(n+m)` -/
lemma commutator_sugawaraGen_heiOper [CharZero ğ•œ] (n m : â„¤) :
    (sugawaraGen heiTrunc n).commutator (heiOper m) = -m â€¢ heiOper (n + m) := by
  ext v
  rw [commutator_sugawaraGen_apply_eq_tsum_commutator_apply]
  simp_rw [commutator_heiPairNO_heiGen heiComm]
  simp only [neg_mul, add_sub_cancel, neg_smul, LinearMap.neg_apply, LinearMap.smul_apply,
             zsmul_eq_mul, Module.End.mul_apply, Module.End.intCast_apply]
  simp only [mul_add, mul_ite, mul_one, mul_zero, add_smul, ite_smul, zero_smul]
  rw [finsum_neg_distrib, finsum_add_distrib]
  Â· rw [finsum_eq_single _ (-m)]
    Â· simp only [neg_add_cancel, â†“reduceIte]
      rw [finsum_eq_single _ (n + m)]
      Â· simp only [sub_add_cancel_left, neg_add_cancel, â†“reduceIte]
        simp only [â† two_smul (R := ğ•œ), â† smul_assoc, smul_eq_mul, smul_neg, â† mul_assoc, neg_inj]
        simp [inv_mul_cancelâ‚€ (Gâ‚€ := ğ•œ) two_ne_zero, one_mul]
        norm_cast
      Â· intro j hjnm
        simp [show n - j + m â‰  0 by intro con ; apply hjnm ; linarith]
    Â· intro j hjm
      simp [show j + m â‰  0 by intro con ; apply hjm ; linarith]
  Â· apply (show Set.Finite {-m} from Set.finite_singleton (-m)).subset
    simp only [Set.subset_singleton_iff, Function.mem_support, ne_eq, ite_eq_right_iff,
               smul_eq_zero, Classical.not_imp, not_or, and_imp]
    intro j hjm _ _
    linarith
  Â· apply (show Set.Finite {n + m} from Set.finite_singleton (n + m)).subset
    simp only [Set.subset_singleton_iff, Function.mem_support, ne_eq, ite_eq_right_iff,
               smul_eq_zero, Classical.not_imp, not_or, and_imp]
    intro j hjm _ _
    linarith

/-- `[L(n), J(m-k)J(k)] = -k â€¢ J(m-k)J(n+k) - (m-k) â€¢ J(n+m-k)J(k)` -/
lemma commutator_sugawaraGen_heiOperPair [CharZero ğ•œ] (n m k : â„¤) :
    (sugawaraGen heiTrunc n).commutator (heiOper (m-k) * heiOper k)
      = -k â€¢ (heiOper (m-k) * heiOper (n+k)) - (m-k) â€¢ (heiOper (n+m-k) * heiOper k) := by
  rw [LinearMap.commutator_pair']
  rw [commutator_sugawaraGen_heiOper _ heiComm, commutator_sugawaraGen_heiOper _ heiComm]
  simp only [neg_smul, zsmul_eq_mul, mul_neg, neg_sub, Int.cast_sub, sub_eq_add_neg]
  congr 2
  Â· simp only [â† mul_assoc]
    congr 1
    exact (Int.cast_comm k _).symm
  Â· simp [show n + (m + -k) = n + m + -k by ring, â† mul_assoc]

/-- `[L(n), :J(m-k)J(k):] = -k â€¢ :J(m-k)J(n+k): - (m-k) â€¢ :J(n+m-k)J(k): + extra terms â€¢ 1` -/
lemma commutator_sugawaraGen_heiPairNO' [CharZero ğ•œ] (n m k : â„¤) :
    (sugawaraGen heiTrunc n).commutator (pairNO' heiOper k (m-k))
      = -k â€¢ (pairNO' heiOper (n+k) (m-k)
        + if 0 â‰¤ k âˆ§ k < -n âˆ§ n + m = 0 then -(n + k) â€¢ 1 else 0
        + if k < 0 âˆ§ -n â‰¤ k âˆ§ n + m = 0 then (n + k) â€¢ 1 else 0)
        - (m-k) â€¢ (pairNO' heiOper k (n+m-k)) := by
  by_cases hk : 0 â‰¤ k
  Â· by_cases hnk : 0 â‰¤ n + k
    Â· have hk' : Â¬ k < 0 := by linarith
      have hnk' : Â¬ k < -n := by linarith
      simp only [pairNO', hk, hk', hnk, hnk', â†“reduceIte, and_false, true_and, false_and, add_zero,
                 neg_smul, zsmul_eq_mul, Int.cast_sub, â† Module.End.mul_eq_comp]
      simp [commutator_sugawaraGen_heiOperPair heiTrunc heiComm]
    Â· have hnk' : k < -n := by linarith
      simp only [pairNO', hk, â†“reduceIte, hnk, hnk', and_true, add_zero, neg_smul, zsmul_eq_mul,
                 Int.cast_sub, â† Module.End.mul_eq_comp]
      rw [commutator_sugawaraGen_heiOperPair heiTrunc heiComm]
      have aux := (heiComm (n+k) (m-k)) â–¸
                  LinearMap.mul_eq_mul_add_commutator (heiOper (n+k)) (heiOper (m-k))
      simp only [aux, show n + k + (m - k) = n + m by ring, neg_smul, zsmul_eq_mul, Int.cast_sub,
                 Int.cast_add, true_and, Int.cast_ite, Int.cast_zero, sub_left_inj, neg_inj]
      simp only [mul_one, neg_add_rev, mul_ite, mul_zero, mul_add (k : V â†’â‚—[ğ•œ] V),
                 add_assoc, left_eq_add]
      by_cases hnm : n + m = 0
      Â· simp only [hnm, â†“reduceIte, Algebra.mul_smul_comm, mul_one, mul_neg]
        simp_rw [add_smul, â† smul_eq_mul, â† add_assoc]
        have (j : â„¤) : Int.cast (R := V â†’â‚—[ğ•œ] V) j = Int.cast (R := ğ•œ) j â€¢ 1 := by norm_cast
        simp [this]
      Â· simp [hnm]
  Â· have obs := commutator_sugawaraGen_heiOperPair heiTrunc heiComm n m (m-k)
    simp only [sub_sub_cancel, neg_sub, zsmul_eq_mul, Int.cast_sub,
               show n + m - (m - k) = n + k by ring] at obs
    by_cases hnk : 0 â‰¤ n + k
    Â· have hk' : k < 0 := by linarith
      have hnk' : -n â‰¤ k := by linarith
      simp only [pairNO', hk, hk', hnk, hnk', â†“reduceIte, and_false, true_and, false_and, add_zero,
                 neg_smul, zsmul_eq_mul, Int.cast_sub, â† Module.End.mul_eq_comp]
      simp only [obs, add_sub, Int.cast_add, mul_one, neg_add_rev, zero_add]
      have aux := (heiComm (m-k) (n+k)) â–¸
                  LinearMap.mul_eq_mul_add_commutator (heiOper (m-k)) (heiOper (n+k))
      rw [aux, show m - k + (n + k) = n + m by ring]
      rw [sub_eq_add_neg _ ((k : V â†’â‚—[ğ•œ] V) * _), sub_eq_add_neg _ ((m - k : V â†’â‚—[ğ•œ] V) * _)]
      rw [add_comm _ (-_)]
      simp only [Int.cast_add, mul_one, neg_add_rev, zero_add, neg_add, â† neg_mul]
      simp only [mul_add (_ : V â†’â‚—[ğ•œ] V), add_assoc, add_right_inj, add_sub]
      by_cases hnm : n + m = 0
      Â· simp only [hnm, zero_sub, â†“reduceIte, Int.cast_sub, Algebra.mul_smul_comm, mul_one,
                   smul_neg, neg_mul, neg_sub]
        rw [show n = -m by linarith]
        simp only [right_eq_add, sub_eq_add_neg, add_smul, mul_add, â† add_assoc,
                   neg_add, mul_neg, neg_smul, neg_neg]
        have (j : â„¤) : Int.cast (R := V â†’â‚—[ğ•œ] V) j = Int.cast (R := ğ•œ) j â€¢ 1 := by norm_cast
        simp [this]
      Â· simp [hnm]
    Â· have hk' : k < 0 := by linarith
      have hnk' : Â¬ -n â‰¤ k := by linarith
      simp only [pairNO', hk, hk', hnk, hnk', â†“reduceIte, and_false, true_and, false_and, add_zero,
                 neg_smul, zsmul_eq_mul, Int.cast_sub, â† Module.End.mul_eq_comp]
      rw [obs]
      rw [sub_eq_add_neg _ ((k : V â†’â‚—[ğ•œ] V) * _), sub_eq_add_neg _ ((m - k : V â†’â‚—[ğ•œ] V) * _)]
      rw [add_comm _ (-_)]
      simp only [Int.cast_add, mul_one, neg_add_rev, zero_add, neg_add, â† neg_mul]
      simp only [mul_add (_ : V â†’â‚—[ğ•œ] V), add_assoc, add_right_inj, add_sub]
      congr 1
      simp

/-- `[L(n), :J(m-k)J(k):] v = -k â€¢ :J(m-k)J(n+k): v - (m-k) â€¢ :J(n+m-k)J(k): v + extra terms â€¢ v` -/
lemma commutator_sugawaraGen_heiPairNO'_apply [CharZero ğ•œ] (n m k : â„¤) (v : V) :
    (sugawaraGen heiTrunc n).commutator (pairNO' heiOper k (m-k)) v
      = -k â€¢ ((pairNO' heiOper (n+k) (m-k) v)
        + if 0 â‰¤ k âˆ§ k < -n âˆ§ n + m = 0 then -(n + k) â€¢ v else 0
        + if k < 0 âˆ§ -n â‰¤ k âˆ§ n + m = 0 then (n + k) â€¢ v else 0)
        - (m-k) â€¢ (pairNO' heiOper k (n+m-k) v) := by
  have key := LinearMap.congr_fun (commutator_sugawaraGen_heiPairNO' heiTrunc heiComm n m k) v
  simp only [LinearMap.sub_apply, LinearMap.add_apply] at key
  rw [key]
  simp_rw [smul_add, sub_eq_add_neg, neg_add, LinearMap.add_apply, LinearMap.smul_apply, add_assoc]
  rw [add_right_inj]
  simp only [â† add_assoc]
  rw [add_left_inj]
  split_ifs <;> simp [add_smul]

end normal_ordered_pair -- section



section central_charge_calculation

open Finset

/-- A discrete integral of a function on `â„•`. -/
def nPrimitive {R : Type*} [AddCommMonoid R] (f : â„• â†’ R) (n : â„•) : R := match n with
  | 0 => (0 : R)
  | n + 1 => nPrimitive f n + f n

@[simp] lemma nPrimitive_zero {R : Type*} [AddCommMonoid R] (f : â„• â†’ R) :
    nPrimitive f 0 = 0 :=
  rfl

@[simp] lemma nPrimitive_succ {R : Type*} [AddCommMonoid R] (f : â„• â†’ R) (n : â„•) :
    nPrimitive f (n + 1) = nPrimitive f n + f n :=
  rfl

lemma nPrimitive_eq_sum {R : Type*} [AddCommMonoid R] (f : â„• â†’ R) (n : â„•) :
    nPrimitive f n = âˆ‘ j âˆˆ range n, f j := by
  induction' n with n ih
  Â· simp
  Â· simp [nPrimitive_succ, sum_range_succ, ih]

/-- A discrete integral of a function on `â„¤`. -/
def zPrimitive {R : Type*} [AddCommGroup R] (f : â„¤ â†’ R) (n : â„¤) : R :=
  if 0 â‰¤ n then âˆ‘ j âˆˆ range (Int.toNat n), f j else -(âˆ‘ j âˆˆ range (Int.natAbs n), f (-j-1))

@[simp] lemma zPrimitive_zero {R : Type*} [AddCommGroup R] (f : â„¤ â†’ R) :
    zPrimitive f 0 = 0 :=
  rfl

@[simp] lemma zPrimitive_apply_of_nonneg {R : Type*} [AddCommGroup R] (f : â„¤ â†’ R)
    {n : â„¤} (hn : 0 â‰¤ n) :
    zPrimitive f n = âˆ‘ j âˆˆ range (Int.toNat n), f j := by
  simp [zPrimitive, hn]

@[simp] lemma zPrimitive_apply_of_nonpos {R : Type*} [AddCommGroup R] (f : â„¤ â†’ R)
    {n : â„¤} (hn : n â‰¤ 0) :
    zPrimitive f n = -(âˆ‘ j âˆˆ range (Int.natAbs n), f (-j-1)) := by
  by_cases hn' : n = 0
  Â· simp [hn']
  Â· simp [zPrimitive, lt_of_le_of_ne hn hn']

@[simp] lemma zPrimitive_succ {R : Type*} [AddCommGroup R] (f : â„¤ â†’ R) (n : â„¤) :
    zPrimitive f (n + 1) = zPrimitive f n + f n := by
  by_cases hn : 0 â‰¤ n
  Â· simp [zPrimitive, hn, Int.le_add_one hn, Int.toNat_add hn zero_le_one, sum_range_succ]
  Â· simp only [not_le] at hn
    have n_natAbs : n.natAbs = (n+1).natAbs + 1 := by
      simpa using Int.natAbs_add_of_nonpos (b := -1) hn (Int.toNat_eq_zero.mp rfl)
    simp only [zPrimitive_apply_of_nonpos _ hn, zPrimitive_apply_of_nonpos _ hn.le, n_natAbs,
               sum_range_succ, Int.natCast_natAbs, neg_add_rev]
    simp only [add_comm (-(f _)), add_assoc, left_eq_add]
    simp [show -|n + 1| - 1 = n by rw [abs_of_nonpos hn, neg_neg] ; ring]

lemma eq_zPrimitive_of_eq_zero_of_forall_eq_add {R : Type*} [AddCommGroup R] {f F : â„¤ â†’ R}
    (h0 : F 0 = 0) (h1 : âˆ€ n, F (n + 1) = F n + f n) :
    F = zPrimitive f := by
  have obsP : âˆ€ (n : â„•), F n = zPrimitive f n := by
    intro n
    induction' n with n ih
    Â· simp [h0]
    Â· have keyF := h1 n
      have keyP := zPrimitive_succ f n
      norm_cast at *
      rw [keyF, ih, â† keyP]
  have obsM : âˆ€ (n : â„•), F (-n) = zPrimitive f (-n) := by
    intro n
    induction' n with n ih
    Â· simp [h0]
    Â· have keyF := h1 (-(n + 1))
      have keyP := zPrimitive_succ f (-(n + 1))
      simp only [Int.natAbs_neg, Int.natAbs_natCast, neg_add_rev, Int.reduceNeg,
                 neg_add_cancel_comm, Nat.cast_add, Nat.cast_one] at ih keyF keyP âŠ¢
      rw [keyF, keyP] at ih
      exact add_right_cancel_iff.mp ih
  ext m
  by_cases hm : 0 â‰¤ m
  Â· rw [(Int.toNat_of_nonneg hm).symm]
    exact obsP m.toNat
  Â· have hm' : m < 0 := Int.lt_of_not_ge hm
    rw [show m = -m.natAbs from Int.eq_neg_comm.mp (by simpa [hm'] using hm'.le)]
    exact obsM m.natAbs

lemma zPrimitive_add {R : Type*} [AddCommGroup R] (f g : â„¤ â†’ R) :
    zPrimitive (f + g) = zPrimitive f + zPrimitive g  := by
  apply (eq_zPrimitive_of_eq_zero_of_forall_eq_add ..).symm
  Â· simp
  Â· intro n
    simp only [Pi.add_apply, zPrimitive_succ]
    ac_rfl

lemma zPrimitive_smul {R S : Type*} [AddCommGroup R] [DistribSMul S R]
    (c : S) (f : â„¤ â†’ R) :
    zPrimitive (c â€¢ f) = c â€¢ (zPrimitive f) := by
  apply (eq_zPrimitive_of_eq_zero_of_forall_eq_add ..).symm <;> simp

lemma zPrimitive_sub {R : Type*} [AddCommGroup R] (f g : â„¤ â†’ R) :
    zPrimitive (f - g) = zPrimitive f - zPrimitive g  := by
  apply (eq_zPrimitive_of_eq_zero_of_forall_eq_add ..).symm
  Â· simp
  Â· intro n
    simp only [Pi.sub_apply, zPrimitive_succ, â†sub_sub, sub_eq_add_neg, neg_add_rev, â†add_assoc]
    ac_rfl

lemma zPrimitive_mul_left {R : Type*} [Ring R] (c : R) (f : â„¤ â†’ R) :
    zPrimitive (fun n â†¦ c * f n) = fun n â†¦ c * zPrimitive f n := by
  apply (eq_zPrimitive_of_eq_zero_of_forall_eq_add ..).symm
  Â· simp
  Â· intro n
    simp [mul_add]

lemma zPrimitive_mul_right {R : Type*} [Ring R] (c : R) (f : â„¤ â†’ R) :
    zPrimitive (fun n â†¦ f n * c) = fun n â†¦ zPrimitive f n * c := by
  apply (eq_zPrimitive_of_eq_zero_of_forall_eq_add ..).symm
  Â· simp
  Â· intro n
    simp [add_mul]

def zMonomialF (R : Type*) [AddCommGroup R] [One R] (d : â„•) : â„¤ â†’ R := match d with
  | 0 => fun _ â†¦ 1
  | d + 1 => zPrimitive (zMonomialF R d)

lemma zMonomialF_eq (R : Type*) [Field R] [CharZero R] (d : â„•) :
    (zMonomialF R d) = (fun (n : â„¤) â†¦ ((âˆ j âˆˆ range d, (n - j : R)) / (Nat.factorial d : R))) := by
  induction' d with d ihd
  Â· funext n
    simp [zMonomialF]
  rw [zMonomialF, Eq.comm]
  apply eq_zPrimitive_of_eq_zero_of_forall_eq_add
  Â· simp only [Int.cast_zero, zero_sub, prod_div_distrib, prod_const, card_range, div_eq_zero_iff,
               ne_eq, AddLeftCancelMonoid.add_eq_zero, one_ne_zero, and_false, not_false_eq_true,
               pow_eq_zero_iff, Nat.cast_eq_zero]
    left
    exact prod_eq_zero_iff.mpr âŸ¨0, by simp, by simpâŸ©
  Â· intro n
    simp only [Int.cast_add, Int.cast_one] at *
    simp [ihd]
    simp only [Nat.factorial_succ, Nat.cast_mul, Nat.cast_add, Nat.cast_one]
    have auxâ‚€ : (d.factorial : R) â‰  0 := by simp [Nat.factorial_ne_zero _]
    have auxâ‚: ((d+1).factorial : R) â‰  0 := by simp [Nat.factorial_ne_zero _]
    have aux' : ((d+1) : R) â‰  0 := by norm_cast
    field_simp
    simp only [â† mul_assoc, mul_eq_mul_right_iff, auxâ‚€, or_false]
    simp only [â† add_mul, â† mul_add]
    simp only [mul_assoc, mul_comm _ (d.factorial : R)]
    rw [mul_right_inj' auxâ‚€]
    simp only [â† mul_assoc, mul_left_inj' aux']
    rw [prod_range_succ (fun a â†¦ (n : R) - a), â† mul_add]
    rw [show (n - d + (d + 1) : R) = n + 1 by ring]
    simp [prod_range_succ']

lemma zMonomialF_zero_eq (R : Type*) [Field R] [CharZero R] (n : â„¤) :
    zMonomialF R 0 n = 1 := by
  simp [zMonomialF]

lemma zMonomialF_one_eq (R : Type*) [Field R] [CharZero R] (n : â„¤) :
    zMonomialF R 1 n = n := by
  simp [zMonomialF_eq]

lemma zMonomialF_two_eq (R : Type*) [Field R] [CharZero R] (n : â„¤) :
    zMonomialF R 2 n = n * (n - 1) / 2 := by
  simp [zMonomialF_eq, prod_range_succ]

lemma zMonomialF_three_eq (R : Type*) [Field R] [CharZero R] (n : â„¤) :
    zMonomialF R 3 n = n * (n - 1) * (n - 2) / 6 := by
  simp [zMonomialF_eq, prod_range_succ, show Nat.factorial 3 = 6 from rfl]

lemma zMonomialF_four_eq (R : Type*) [Field R] [CharZero R] (n : â„¤) :
    zMonomialF R 4 n = n * (n - 1) * (n - 2) * (n - 3) / 24 := by
  simp [zMonomialF_eq, prod_range_succ, show Nat.factorial 4 = 24 from rfl]

lemma zMonomialF_five_eq (R : Type*) [Field R] [CharZero R] (n : â„¤) :
    zMonomialF R 5 n = n * (n - 1) * (n - 2) * (n - 3) * (n - 4) / 120 := by
  simp [zMonomialF_eq, prod_range_succ, show Nat.factorial 5 = 120 from rfl]

lemma zMonomialF_apply_eq_zero_of_of_nonneg_lt (R : Type*) [Field R] [CharZero R]
    (d : â„•) {n : â„•} (n_lt : n < d) :
    zMonomialF R d n = 0 := by
  simp only [zMonomialF_eq R d, Int.cast_natCast, div_eq_zero_iff, Nat.cast_eq_zero]
  exact Or.inl <| prod_eq_zero_iff.mpr âŸ¨n, âŸ¨mem_range.mpr n_lt, by simpâŸ©âŸ©

lemma bosonic_sugawara_cc_calc (R : Type*) [Field R] [CharZero R] (n : â„¤) :
    zPrimitive (fun l â†¦ (l : R) * (n - l)) n = (n^3 - n) / 6 := by
  have obs : (n^3 - n) / 6 = (n - 1 : R) * zMonomialF R 2 n - 2 * zMonomialF R 3 n := by
    rw [zMonomialF_two_eq, zMonomialF_three_eq]
    field_simp
    ring
  have key : (zPrimitive fun l â†¦ (n - 1 : R) * l) - (zPrimitive fun l â†¦ 2 * zMonomialF R 2 l)
              = zPrimitive ((fun (l : â„¤) â†¦ (l : R) * (n - l))) := by
    rw [â† zPrimitive_sub]
    apply congr_arg zPrimitive
    ext l
    simp [zMonomialF_two_eq]
    ring
  simp_rw [obs, â† key, zPrimitive_mul_left]
  dsimp
  congr
  ext m
  rw [zMonomialF_one_eq]

lemma bosonic_sugawara_cc_calc_nonneg (n : â„•) :
    âˆ‘ l âˆˆ Finset.range n, (l : â„š) * (n - l) = (n^3 - n) / 6 := by
  have key := bosonic_sugawara_cc_calc â„š n
  simp only [Int.cast_natCast, Nat.cast_nonneg, zPrimitive_apply_of_nonneg, Int.toNat_natCast]
    at key
  rw [â† key]

end central_charge_calculation



section commutator_sugawaraGen

include heiComm in
/-- `[L(n), L(m)] = (n-m) â€¢ L(n+m) + (n^3 - n) / 12 * Î´[n+m,0] â€¢ 1` -/
lemma commutator_sugawaraGen [CharZero ğ•œ] (n m : â„¤) :
    (sugawaraGen heiTrunc n).commutator (sugawaraGen heiTrunc m)
      = (n-m) â€¢ (sugawaraGen heiTrunc (n+m))
        + if n + m = 0 then ((n ^ 3 - n : ğ•œ) / (12 : ğ•œ)) â€¢ (1 : V â†’â‚—[ğ•œ] V) else 0 := by
  ext v
  rw [sugawaraGen_commutator_apply_eq_tsum_commutator_apply]
  simp only [heiOper_pairNO_eq_pairNO' heiComm]
  have aux_commutator (k : â„¤) :=
    commutator_sugawaraGen_heiPairNO'_apply heiTrunc heiComm n m (m-k) v
  simp only [show âˆ€ k, m - (m-k) = k by intro k; ring] at aux_commutator
  simp_rw [aux_commutator, sub_eq_add_neg, smul_add, â† add_assoc]
  rw [finsum_add_distrib]
  Â· simp only [neg_add_rev, neg_neg, le_add_neg_iff_add_le, zero_add, add_neg_lt_iff_lt_add,
          lt_neg_add_iff_add_lt, neg_add_le_iff_le_add, smul_ite, smul_zero, smul_add, zsmul_eq_mul,
          Int.cast_add, Int.cast_neg, LinearMap.add_apply, Module.End.mul_apply,
          Module.End.intCast_apply, LinearMap.neg_apply]
    rw [finsum_add_distrib]
    Â· simp only [smul_add]
      rw [add_comm, â† add_assoc]
      congr 1
      Â· -- The dummy index reshuffling.
        rw [â† finsum_comp_equiv âŸ¨fun k â†¦ k - n, fun k â†¦ k + n, fun _ â†¦ by simp, fun _ â†¦ by simpâŸ©]
        dsimp only [Equiv.coe_fn_mk]
        rw [â† smul_add, â† finsum_add_distrib]
        Â· simp only [neg_sub, add_sub_assoc', â† add_assoc]
          simp_rw [show âˆ€ k, n + m + k - n + -m = k by intro k; ring]
          simp_rw [show âˆ€ k, m + n - k = n + m - k by intro k; ring]
          simp_rw [add_smul, sub_smul, â† add_assoc, neg_sub, sub_eq_add_neg]
          simp_rw [neg_add_cancel_right]
          rw [finsum_add_distrib]
          Â· simp_rw [(Int.cast_smul_eq_zsmul ğ•œ _ _).symm, â† smul_finsum]
            rw [smul_add]
            congr 1 <;>
            Â· rw [smul_comm]
              simp [â† sub_eq_add_neg, heiOper_pairNO_eq_pairNO' heiComm, sugawaraGen_apply]
          Â· simp_rw [â† sub_eq_add_neg]
            exact finite_support_smul_pairNO'_heiOper_applyâ‚€ heiTrunc heiComm ..
          Â· simp_rw [â† sub_eq_add_neg]
            exact finite_support_smul_pairNO'_heiOper_applyâ‚€ heiTrunc heiComm ..
        Â· have (k : â„¤) : n + m + k - n - m = k := by ring
          simpa [â† sub_eq_add_neg, add_sub_assoc', this] using
            finite_support_smul_pairNO'_heiOper_applyâ‚€ heiTrunc heiComm ..
        Â· simp_rw [â† sub_eq_add_neg]
          exact finite_support_smul_pairNO'_heiOper_applyâ‚€ heiTrunc heiComm ..
      Â· -- The central charge calculation.
        by_cases hnm : n + m = 0
        Â· have m_eq_neg_n : m = -n := by linarith
          simp only [m_eq_neg_n, add_neg_cancel, and_true, neg_neg, add_zero, zero_add, neg_smul,
                     smul_neg, â†“reduceIte, LinearMap.smul_apply, Module.End.one_apply]
          by_cases hn : 0 â‰¤ n
          Â· have obs (i : â„¤) : Â¬ (i â‰¤ -n âˆ§ 0 < i) := by intro maybe ; linarith
            simp only [obs, â†“reduceIte]
            rw [finsum_eq_sum_of_support_subset _ (s := Finset.Ioc (-n) 0) ?_]
            Â· rw [Finset.sum_congr rfl (g := fun i â†¦ -(i + n) â€¢ i â€¢ v)]
              Â· simp only [â† smul_assoc]
                rw [â† Finset.sum_smul]
                suffices ((2â»Â¹ : ğ•œ) * (âˆ‘ i âˆˆ Finset.Ioc (-n) 0, -(i + n) * i)) â€¢ v
                            = (((n : ğ•œ) ^ 3 + (-n : ğ•œ)) / 12) â€¢ v by
                  rw [â† this, â† smul_eq_mul, smul_assoc]
                  congr 1
                  norm_cast
                congr 1
                have key := bosonic_sugawara_cc_calc ğ•œ n
                rw [zPrimitive_apply_of_nonneg _ (by linarith)] at key
                field_simp at key âŠ¢
                rw [â† sub_eq_add_neg, â† key]
                simp only [mul_assoc]
                norm_num
                rw [@Finset.sum_of_injOn â„• â„¤ ğ•œ _ (Finset.range n.toNat) (Finset.Ioc (-n) 0)
                          (fun x â†¦ â†‘x * (n - x)) (fun x â†¦ (-â†‘n + -â†‘x) * x)
                          (fun i â†¦ -i) ?_ ?_ ?_ ?_]
                Â· intro i _ j _ hij
                  simpa using hij
                Â· intro i hi
                  simpa using hi
                Â· intro k hk hk'
                  exfalso
                  simp only [Finset.mem_Ioc, Finset.coe_range, Set.mem_image, Set.mem_Iio,
                             Int.lt_toNat, not_exists, not_and] at hk hk'
                  apply hk' (-k).toNat ?_
                  Â· simp [hk.2]
                  Â· exact (le_of_eq (by simp [hk.2])).trans_lt (show -k < n by linarith)
                Â· intro k _
                  simp ; ring
              Â· intro i hi
                simp only [Finset.mem_Ioc.mp hi, and_self, â†“reduceIte, neg_smul]
            Â· refine Function.support_subset_iff'.mpr ?_
              intro k hk
              simp only [Finset.coe_Ioc, Set.mem_Ioc, and_comm] at hk
              simp [hk]
          Â· have obs (i : â„¤) : Â¬ (-n < i âˆ§ i â‰¤ 0) := by intro maybe ; linarith
            simp only [obs, â†“reduceIte]
            rw [finsum_eq_sum_of_support_subset _ (s := Finset.Ioc 0 (-n)) ?_]
            Â· rw [Finset.sum_congr rfl (g := fun i â†¦ (i + n) â€¢ i â€¢ v)]
              Â· simp only [â† smul_assoc]
                rw [â† Finset.sum_smul]
                suffices ((2â»Â¹ : ğ•œ) * (âˆ‘ i âˆˆ Finset.Ioc 0 (-n), (i + n) * i)) â€¢ v
                            = (((n : ğ•œ) ^ 3 + (-n : ğ•œ)) / 12) â€¢ v by
                  rw [â† this, â† smul_eq_mul, smul_assoc]
                  congr 1
                  norm_cast
                congr 1
                have key' := bosonic_sugawara_cc_calc ğ•œ n
                rw [zPrimitive_apply_of_nonpos _ (by linarith)] at key'
                field_simp at key' âŠ¢
                rw [â† sub_eq_add_neg, â† key']
                have aux (k : ğ•œ) : (-k - 1) = -(k + 1) := by ring
                simp only [aux, neg_mul, sub_neg_eq_add, neg_mul, mul_assoc,
                           Finset.sum_neg_distrib, neg_mul, neg_neg]
                norm_num
                have n_natAbs : -n = n.natAbs := by
                  simpa [hn] using (abs_of_neg <| not_le.mp hn).symm
                rw [@Finset.sum_of_injOn â„• â„¤ ğ•œ _ (Finset.range n.natAbs) (Finset.Ioc 0 (-n))
                          (fun x â†¦ (â†‘x + 1) * (n + (x + 1))) (fun x â†¦ (â†‘x + â†‘n) * x)
                          (fun i â†¦ i + 1) ?_ ?_ ?_ ?_]
                Â· intro i _ j _ hij
                  simpa using hij
                Â· intro i hi
                  simp only [Finset.coe_range, Set.mem_Iio, Finset.coe_Ioc, Set.mem_Ioc,
                             Int.succ_ofNat_pos, true_and, n_natAbs] at hi âŠ¢
                  exact Int.toNat_le.mp hi
                Â· intro k hk hk'
                  exfalso
                  simp only [n_natAbs, Finset.mem_Ioc, Finset.coe_range,
                             Set.mem_image, Set.mem_Iio, not_exists, not_and] at hk hk'
                  apply hk' (k - 1).toNat
                  Â· simp only [Int.pred_toNat]
                    exact (Nat.pred_lt (by simpa using hk.1)).trans_le (by simpa using hk.2)
                  Â· simp only [Int.pred_toNat]
                    norm_cast
                    rw [Nat.sub_add_eq_max,
                        max_eq_left <| (Int.le_toNat (by linarith)).mpr (show 1 â‰¤ k by linarith)]
                    exact Int.toNat_of_nonneg (by linarith)
                Â· intro k _
                  simp ; ring
              Â· intro i hi
                simp [(Finset.mem_Ioc.mp hi).symm]
            Â· refine Function.support_subset_iff'.mpr ?_
              intro k hk
              simp only [Finset.coe_Ioc, Set.mem_Ioc, and_comm] at hk
              simp [hk]
        Â· simp [hnm]
    Â· simpa [â† sub_eq_add_neg] using
        finite_support_smul_pairNO'_heiOper_applyâ‚€ heiTrunc heiComm ..
    Â· apply ((Set.finite_Ioc (n+m) m).union (Set.finite_Ioc m (n+m))).subset
      refine Function.support_subset_iff'.mpr ?_
      intro k hk
      simp only [Set.Ioc_union_Ioc_symm, Set.mem_Ioc, inf_lt_iff, le_sup_iff, not_le] at hk
      have hkâ‚‚' : Â¬ ((k â‰¤ m) âˆ§ (n + m < k) âˆ§ n + m = 0) := by grind
      have hkâ‚ƒ' : Â¬ (m < k âˆ§ k â‰¤ n + m âˆ§ n + m = 0) := by grind
      simp [hkâ‚‚', hkâ‚ƒ']
  Â· have auxâ‚€ := finite_support_pairNO'_heiOper_apply heiTrunc heiComm 0 (n + m) v
    simp only [sub_eq_add_neg, zero_add] at auxâ‚€
    apply ((auxâ‚€.union (Set.finite_Ioc (m+n) m)).union (Set.finite_Ioc m (m+n))).subset
    refine Function.support_subset_iff'.mpr ?_
    intro k hk
    simp only [Set.mem_union, Function.mem_support, ne_eq, Set.mem_Ioc, not_or, not_not, not_and,
               not_le] at hk
    rcases hk with âŸ¨âŸ¨hkâ‚, hkâ‚‚âŸ©, hkâ‚ƒâŸ©
    have hkâ‚‚' : Â¬ ((0 â‰¤ m + -k) âˆ§ (m + -k < -n) âˆ§ n + m = 0) := by grind
    have hkâ‚ƒ' : Â¬ (m + -k < 0 âˆ§ -n â‰¤ m + -k âˆ§ n + m = 0) := by grind
    simp_rw [hkâ‚‚', hkâ‚ƒ']
    simp [hkâ‚]
  Â· have (k : â„¤) : n + m - (m - k) = n + k := by ring
    simp_rw [â† sub_eq_add_neg, this, Function.support_neg]
    exact finite_support_smul_pairNO'_heiOper_apply heiTrunc heiComm ..

end commutator_sugawaraGen



section representation

noncomputable def _root_.LieAlgebra.representationOfBasisAux
    {ğ•‚ : Type*} [Field ğ•‚] {V : Type*} [AddCommGroup V] [Module ğ•‚ V]
    {ğ“– : Type*} [LieRing ğ“–] [LieAlgebra ğ•‚ ğ“–] {Î¹ : Type*} (B : Basis Î¹ ğ•‚ ğ“–)
    (genOper : Î¹ â†’ (V â†’â‚—[ğ•‚] V)) :
    ğ“– â†’â‚—[ğ•‚] (V â†’â‚—[ğ•‚] V) :=
  B.constr ğ•‚ <| fun i â†¦ genOper i

@[simp] lemma _root_.LieAlgebra.representationOfBasisAux_apply_basis
    {ğ•‚ : Type*} [Field ğ•‚] {V : Type*} [AddCommGroup V] [Module ğ•‚ V]
    {ğ“– : Type*} [LieRing ğ“–] [LieAlgebra ğ•‚ ğ“–] {Î¹ : Type*} (B : Basis Î¹ ğ•‚ ğ“–)
    (genOper : Î¹ â†’ (V â†’â‚—[ğ•‚] V)) (i : Î¹) :
    LieAlgebra.representationOfBasisAux B genOper (B i) = genOper i := by
  simp [LieAlgebra.representationOfBasisAux]

lemma _root_.LieAlgebra.representationOfBasisAux_property
    {ğ•‚ : Type*} [Field ğ•‚] {V : Type*} [AddCommGroup V] [Module ğ•‚ V]
    {ğ“– : Type*} [LieRing ğ“–] [LieAlgebra ğ•‚ ğ“–] {Î¹ : Type*} (B : Basis Î¹ ğ•‚ ğ“–)
    {genOper : Î¹ â†’ (V â†’â‚—[ğ•‚] V)}
    (genComm : âˆ€ i j, (genOper i).commutator (genOper j)
      = LieAlgebra.representationOfBasisAux B genOper â…B i, B jâ†) :
    (LieAlgebra.representationOfBasisAux B genOper).compRight.comp (LieAlgebra.bracketHom ğ•‚ ğ“–)
      = (LinearMap.commutatorBilin V).complâ‚â‚‚
          (LieAlgebra.representationOfBasisAux B genOper)
          (LieAlgebra.representationOfBasisAux B genOper) :=
  B.ext fun i â†¦ B.ext fun j â†¦ by simp [genComm i j]

noncomputable def _root_.LieAlgebra.representationOfBasis
    {ğ•‚ : Type*} [Field ğ•‚] {V : Type*} [AddCommGroup V] [Module ğ•‚ V]
    {ğ“– : Type*} [LieRing ğ“–] [LieAlgebra ğ•‚ ğ“–] {Î¹ : Type*} (B : Basis Î¹ ğ•‚ ğ“–)
    {genOper : Î¹ â†’ (V â†’â‚—[ğ•‚] V)}
    (genComm : âˆ€ i j, (genOper i).commutator (genOper j)
      = LieAlgebra.representationOfBasisAux B genOper â…B i, B jâ†) :
    ğ“– â†’â‚—â…ğ•‚â† (V â†’â‚—[ğ•‚] V) where
  toFun := LieAlgebra.representationOfBasisAux B genOper
  map_add' := by simp
  map_smul' := by simp
  map_lie' := by
    intro X Y
    have key := LieAlgebra.representationOfBasisAux_property B genComm
    exact LinearMap.congr_fun (LinearMap.congr_fun key X) Y

lemma commutator_smul_one (A : V â†’â‚—[ğ•œ] V) (c : ğ•œ) :
    A.commutator (c â€¢ 1) = 0 := by
  simp [LinearMap.commutator]

lemma smul_one_commutator (A : V â†’â‚—[ğ•œ] V) (c : ğ•œ) :
    (c â€¢ 1 : V â†’â‚—[ğ•œ] V).commutator A = 0 := by
  simp [LinearMap.commutator]

/-- Construct a representation of Virasoro algebra from a central charge value `c` and a
collection `(Lâ‚™)`, `n âˆˆ â„¤`, of operators satisfying the commutation relations of Virasoro
generators with that central charge. -/
noncomputable def VirasoroAlgebra.representationOfCentralChangeOfL
    {ğ•‚ : Type*} [Field ğ•‚] [CharZero ğ•‚]
    {V : Type*} [AddCommGroup V] [Module ğ•‚ V] (c : ğ•‚) {lOper : â„¤ â†’ (V â†’â‚—[ğ•‚] V)}
    (lComm : âˆ€ n m, (lOper n).commutator (lOper m)
      = (n-m) â€¢ lOper (n+m) + if n + m = 0 then (c / 12 * (n^3 - n)) â€¢ (1 : V â†’â‚—[ğ•‚] V) else 0) :
    VirasoroAlgebra ğ•‚ â†’â‚—â…ğ•‚â† (V â†’â‚—[ğ•‚] V) := by
  let ops : Option â„¤ â†’ (V â†’â‚—[ğ•‚] V) := fun n' â†¦ match n' with
    | none => c â€¢ 1
    | some n => lOper n
  apply LieAlgebra.representationOfBasis (VirasoroAlgebra.basisLC ğ•‚) (genOper := ops)
  intro n' m'
  match n' with
  | none => simpa [ops] using smul_one_commutator ..
  | some n => match m' with
    | none => simpa [ops] using commutator_smul_one ..
    | some m =>
      simp only [ops, lComm, basisLC_some, lgen_bracket, map_add, map_smul]
      congr 1
      Â· have obs (k : â„¤) : lgen ğ•‚ k = (VirasoroAlgebra.basisLC ğ•‚) (some k) := by simp
        rw [obs]
        simp only [LieAlgebra.representationOfBasisAux_apply_basis, ops]
        ext v
        simp only [Module.End.mul_apply, LinearMap.sub_apply, Module.End.intCast_apply, sub_smul,
                   LinearMap.smul_apply]
        congr 1 <;> rw [Int.cast_smul_eq_zsmul]
      Â· by_cases hnm : n + m = 0
        Â· have obs : cgen ğ•‚ = (VirasoroAlgebra.basisLC ğ•‚) none := by simp
          simp only [hnm, â†“reduceIte, map_smul, ops]
          simp only [obs, LieAlgebra.representationOfBasisAux_apply_basis]
          simp only [â† smul_assoc, smul_eq_mul]
          congr 1
          field_simp
          rw [mul_comm]
        Â· simp [hnm]

lemma VirasoroAlgebra.representationOfCentralChangeOfL_cgen
    {ğ•‚ : Type*} [Field ğ•‚] [CharZero ğ•‚]
    {V : Type*} [AddCommGroup V] [Module ğ•‚ V] (c : ğ•‚) {lOper : â„¤ â†’ (V â†’â‚—[ğ•‚] V)}
    (lComm : âˆ€ n m, (lOper n).commutator (lOper m)
      = (n-m) â€¢ lOper (n+m) + if n + m = 0 then (c / 12 * (n^3 - n)) â€¢ (1 : V â†’â‚—[ğ•‚] V) else 0) :
    (representationOfCentralChangeOfL c lComm) (cgen ğ•‚) = c â€¢ 1 := by
  convert LieAlgebra.representationOfBasisAux_apply_basis (VirasoroAlgebra.basisLC ğ•‚) _ none
  simp

lemma VirasoroAlgebra.representationOfCentralChangeOfL_lgen
    {ğ•‚ : Type*} [Field ğ•‚] [CharZero ğ•‚]
    {V : Type*} [AddCommGroup V] [Module ğ•‚ V] (c : ğ•‚) {lOper : â„¤ â†’ (V â†’â‚—[ğ•‚] V)}
    (lComm : âˆ€ n m, (lOper n).commutator (lOper m)
      = (n-m) â€¢ lOper (n+m) + if n + m = 0 then (c / 12 * (n^3 - n)) â€¢ (1 : V â†’â‚—[ğ•‚] V) else 0)
    (n : â„¤) :
    (representationOfCentralChangeOfL c lComm) (lgen ğ•‚ n) = lOper n := by
  convert LieAlgebra.representationOfBasisAux_apply_basis (VirasoroAlgebra.basisLC ğ•‚) _ (some n)
  simp

variable {heiOper} in
/-- **The basic bosonic Sugawara representation of Virasoro algebra (c=1)**:
On a vector space with a representation of the Heisenberg algebra that acts locally truncatedly,
we get a representation of the Virasoro algebra with central charge 1 by the Sugawara
construction. -/
noncomputable def VirasoroAlgebra.sugawaraRepresentation [CharZero ğ•œ] :
    VirasoroAlgebra ğ•œ â†’â‚—â…ğ•œâ† (V â†’â‚—[ğ•œ] V) := by
  apply VirasoroAlgebra.representationOfCentralChangeOfL 1 (lOper := sugawaraGen heiTrunc)
  intro n m
  simp only [commutator_sugawaraGen heiOper heiTrunc heiComm n m, zsmul_eq_mul, Int.cast_sub,
             one_div, add_right_inj]
  by_cases hnm : n + m = 0
  Â· simp [hnm]
    congr 1
    field_simp
  Â· simp [hnm]

/-- The central element `C` of the Virasoro algebra acts as `1` on the representation obtained
by the basic bosonic Sugawara construction. -/
lemma VirasoroAlgebra.sugawaraRepresentation_cgen [CharZero ğ•œ] :
    VirasoroAlgebra.sugawaraRepresentation heiTrunc heiComm (cgen ğ•œ) = 1 := by
  convert VirasoroAlgebra.representationOfCentralChangeOfL_cgen ..
  simp

/-- The formula for the action of the Virasoro generator `Lâ‚™` on the representation obtained
by the basic bosonic Sugawara construction. -/
lemma VirasoroAlgebra.sugawaraRepresentation_lgen [CharZero ğ•œ] (n : â„¤) (v : V) :
    VirasoroAlgebra.sugawaraRepresentation heiTrunc heiComm (lgen ğ•œ n) v =
      (2 : ğ•œ)â»Â¹ â€¢ âˆ‘á¶  k, pairNO heiOper (n-k) k v := by
  rw [â† sugawaraGen_apply heiTrunc]
  apply LinearMap.congr_fun _ v
  convert VirasoroAlgebra.representationOfCentralChangeOfL_lgen ..

end representation

end Sugawara_boson -- section

end VirasoroProject -- namespace
