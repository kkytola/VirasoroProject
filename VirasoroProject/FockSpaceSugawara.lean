/-
Copyright (c) 2025 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib
import VirasoroProject.Sugawara
import VirasoroProject.FockSpace

namespace VirasoroProject



section Fock_space_Sugawara_construction

variable (ğ•œ : Type*) [Field ğ•œ] [CharZero ğ•œ]

section auxiliary

variable {V : Type*} [AddCommGroup V] [Module (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) V]

open HeisenbergAlgebra in
private lemma commutator_lsmul_jgen_of_module_uea_heisenbergAlgebra
    (hc : âˆ€ (v : V), (Î¹UEA ğ•œ (kgen ğ•œ)) â€¢ v = v) (k l : â„¤) :
    (ModuleOfModuleAlgebra.lsmul ğ•œ V (Î¹UEA ğ•œ (jgen ğ•œ k))).commutator
      (ModuleOfModuleAlgebra.lsmul ğ•œ V (Î¹UEA ğ•œ (jgen ğ•œ l)))
    = if k + l = 0 then (k : ğ•œ) â€¢ 1 else 0 := by
  have key (w : V) :
      (Î¹UEA ğ•œ (jgen ğ•œ k)) â€¢ (Î¹UEA ğ•œ (jgen ğ•œ l)) â€¢ w - (Î¹UEA ğ•œ (jgen ğ•œ l)) â€¢ (Î¹UEA ğ•œ (jgen ğ•œ k)) â€¢ w
        = if k + l = 0 then â†‘k â€¢ w else 0 := by
    have key := congr_arg (fun b â†¦ b â€¢ w) <| (Î¹UEA ğ•œ).map_lie (jgen ğ•œ k) (jgen ğ•œ l)
    rw [lie_jgen ğ•œ k l] at key
    by_cases hkl : k + l = 0
    Â· simp only [hkl, â†“reduceIte, LieHom.map_smul] at key âŠ¢
      convert key.symm using 1
      Â· simp_rw [â† smul_assoc, â† sub_smul]
        rfl
      Â· have same :
            k â€¢ w = (algebraMap ğ•œ (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) â†‘k â€¢ Î¹UEA ğ•œ (kgen ğ•œ)) â€¢ w := by
          rw [smul_assoc, hc]
          simpa [map_intCast] using (Int.cast_smul_eq_zsmul (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) k w).symm
        convert same using 1
        congr 1
        exact algebra_compatible_smul (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) (â†‘k) (Î¹UEA ğ•œ (kgen ğ•œ))
    Â· simp only [hkl, â†“reduceIte, LieHom.map_zero, zero_smul] at key âŠ¢
      simp_rw [â† smul_assoc, â† sub_smul]
      convert key.symm using 1
  ext v
  convert key v using 1
  by_cases hkl : k + l = 0 <;> simp [hkl, Int.cast_smul_eq_zsmul]

open HeisenbergAlgebra Filter in
-- TODO: Generalize to `kgen` acting as `Îº â€¢ 1`, maybe.
/-- **The basic bosonic Sugawara representation of Virasoro algebra (c=1)**:
On a module over the universal enveloping algebra of the Heisenberg algebra in which the Heisenberg
algebra acts locally truncatedly (and the central element `k` acts as `1`), we get a representation
of the Virasoro algebra with central charge `c = 1` by the Sugawara construction. -/
noncomputable def sugawaraRepresentation_of_module_uea_heisenbergAlgebra
    (htrunc : âˆ€ (v : V), âˆ€á¶  (k : â„¤) in atTop, Î¹UEA ğ•œ (jgen ğ•œ k) â€¢ v = 0)
    (hc : âˆ€ (v : V), (Î¹UEA ğ•œ (kgen ğ•œ)) â€¢ v = v) :
    LieAlgebra.Representation ğ•œ ğ•œ (VirasoroAlgebra ğ•œ)
      (ModuleOfModuleAlgebra ğ•œ (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) V) :=
  let heiOper (k : â„¤) :
      ModuleOfModuleAlgebra ğ•œ (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) V
        â†’â‚—[ğ•œ] ModuleOfModuleAlgebra ğ•œ (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) V :=
    ModuleOfModuleAlgebra.lsmul ğ•œ V (Î¹UEA ğ•œ (jgen ğ•œ k))
  sugawaraRepresentation (heiOper := heiOper)
    (fun v â†¦ htrunc ((ModuleOfModuleAlgebra.unMkAddHom ğ•œ (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) V) v))
    (commutator_lsmul_jgen_of_module_uea_heisenbergAlgebra ğ•œ hc)

open HeisenbergAlgebra Filter in
lemma sugawaraRepresentation_of_module_uea_heisenbergAlgebra_lgen_apply
    (htrunc : âˆ€ (v : V), âˆ€á¶  (k : â„¤) in atTop, Î¹UEA ğ•œ (jgen ğ•œ k) â€¢ v = 0)
    (hc : âˆ€ (v : V), Î¹UEA ğ•œ (kgen ğ•œ) â€¢ v = v)
    (n : â„¤) (v : ModuleOfModuleAlgebra ğ•œ (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) V) :
    sugawaraRepresentation_of_module_uea_heisenbergAlgebra ğ•œ htrunc hc (.lgen ğ•œ n) v =
      (2 : ğ•œ)â»Â¹ â€¢ ModuleOfModuleAlgebra.mkAddHom ğ•œ (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) V (
          ((âˆ‘á¶  k â‰¥ 0, Î¹UEA ğ•œ (jgen ğ•œ (n-k)) â€¢ Î¹UEA ğ•œ (jgen ğ•œ k)
                      â€¢ ModuleOfModuleAlgebra.unMkAddHom ğ•œ _ V v)
          + (âˆ‘á¶  k < 0, Î¹UEA ğ•œ (jgen ğ•œ k) â€¢ Î¹UEA ğ•œ (jgen ğ•œ (n-k))
                      â€¢ ModuleOfModuleAlgebra.unMkAddHom ğ•œ _ V v))) := by
  apply sugawaraRepresentation_lgen_apply _
    ((fun v â†¦ htrunc ((ModuleOfModuleAlgebra.unMkAddHom ğ•œ (ğ“¤ ğ•œ (HeisenbergAlgebra ğ•œ)) V) v)))
    (commutator_lsmul_jgen_of_module_uea_heisenbergAlgebra ğ•œ hc)

end auxiliary


namespace ChargedFockSpace

/-- **Virasoro algebra representation on Fock space by basic bosonic Sugawara construction (c=1)**:
-/
noncomputable def sugawaraRepresentation (Î± : ğ•œ) :
    LieAlgebra.Representation ğ•œ ğ•œ (VirasoroAlgebra ğ•œ) (ChargedFockSpace ğ•œ Î±) :=
  sugawaraRepresentation_of_module_uea_heisenbergAlgebra ğ•œ (V := ChargedFockSpace ğ•œ Î±)
      (fun _ â†¦ eventually_jgen_smul_eq_zero ..) (fun _ â†¦ ChargedFockSpace.kgen_smul ..)

open HeisenbergAlgebra in
/-- The formula for the action of the Virasoro generators in the (basic) Sugawara
representation on the charged Fock space. -/
lemma sugawaraRepresentation_lgen_apply
    (Î± : ğ•œ) (n : â„¤) (v : ChargedFockSpace ğ•œ Î±) :
    ChargedFockSpace.sugawaraRepresentation ğ•œ Î± (.lgen ğ•œ n) v =
      (2 : ğ•œ)â»Â¹
        â€¢ ((âˆ‘á¶  k â‰¥ 0, Î¹UEA ğ•œ (jgen ğ•œ (n-k)) â€¢ Î¹UEA ğ•œ (jgen ğ•œ k) â€¢ v)
          + (âˆ‘á¶  k < 0, Î¹UEA ğ•œ (jgen ğ•œ k) â€¢ Î¹UEA ğ•œ (jgen ğ•œ (n-k)) â€¢ v)) := by
  apply sugawaraRepresentation_of_module_uea_heisenbergAlgebra_lgen_apply

open HeisenbergAlgebra in
lemma sugawaraRepresentation_lgen_apply_vacuum (Î± : ğ•œ) (n : â„¤) :
    ChargedFockSpace.sugawaraRepresentation ğ•œ Î± (.lgen ğ•œ n) (vacuum ğ•œ Î±) =
      (2 : ğ•œ)â»Â¹
        â€¢ ((Î¹UEA ğ•œ (jgen ğ•œ n) â€¢ Î¹UEA ğ•œ (jgen ğ•œ 0) â€¢ (vacuum ğ•œ Î±))
          + (âˆ‘á¶  k < 0, Î¹UEA ğ•œ (jgen ğ•œ k) â€¢ Î¹UEA ğ•œ (jgen ğ•œ (n-k)) â€¢ (vacuum ğ•œ Î±))) := by
  simp only [sugawaraRepresentation_lgen_apply, ge_iff_le]
  congr 1
  simp only [add_left_inj]
  convert @finsum_eq_single (ChargedFockSpace ğ•œ Î±) â„¤ _ _ 0 ?_
  Â· simp
  Â· intro k k_ne_zero
    by_cases k_nn : 0 â‰¤ k
    Â· simp only [k_nn, finsum_true]
      rw [jgen_pos_vacuum ğ•œ Î± (show 0 < k by grind), smul_zero]
    Â· simp [k_nn]

open HeisenbergAlgebra in
lemma sugawaraRepresentation_lgen_nonneg_apply_vacuum
    (Î± : ğ•œ) {n : â„¤} (n_nn : 0 â‰¤ n) :
    ChargedFockSpace.sugawaraRepresentation ğ•œ Î± (.lgen ğ•œ n) (vacuum ğ•œ Î±) =
      (2 : ğ•œ)â»Â¹ â€¢ Î¹UEA ğ•œ (jgen ğ•œ n) â€¢ Î¹UEA ğ•œ (jgen ğ•œ 0) â€¢ (vacuum ğ•œ Î±) := by
  simp only [sugawaraRepresentation_lgen_apply_vacuum, smul_add]
  convert add_zero ..
  convert smul_zero ..
  Â· convert finsum_zero with k
    by_cases k_neg : k < 0
    Â· simp only [k_neg, finsum_true]
      rw [jgen_pos_vacuum ğ•œ Î± (show 0 < n - k by linarith), smul_zero]
    Â· simp [k_neg]

/-- The vacuum in the Fock space of charge Î± has Lâ‚€-eigenvalue Î±Â²/2. -/
lemma sugawaraRepresentation_lgen_zero_apply_vacuum (Î± : ğ•œ) :
    sugawaraRepresentation ğ•œ Î± (.lgen ğ•œ 0) (vacuum ğ•œ Î±) =
      (Î±^2 / 2) â€¢ (vacuum ğ•œ Î±) := by
  rw [sugawaraRepresentation_lgen_nonneg_apply_vacuum ğ•œ Î± le_rfl]
  simp only [jgen_zero_smul, â† smul_assoc, smul_eq_mul]
  grind

/-- The vacuum in the Fock space of charge Î± is annihilated by Lâ‚™ for n > 0. -/
lemma sugawaraRepresentation_lgen_pos_apply_vacuum (Î± : ğ•œ)
    {n : â„¤} (n_pos : 0 < n) :
    sugawaraRepresentation ğ•œ Î± (.lgen ğ•œ n) (vacuum ğ•œ Î±) = 0 := by
  rw [sugawaraRepresentation_lgen_nonneg_apply_vacuum ğ•œ Î± n_pos.le]
  convert smul_zero ..
  simp only [jgen_zero_smul]
  rw [smul_comm, jgen_pos_vacuum ğ•œ Î± n_pos, smul_zero]

end ChargedFockSpace

end Fock_space_Sugawara_construction

end VirasoroProject
