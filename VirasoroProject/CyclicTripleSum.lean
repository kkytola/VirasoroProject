/-
Copyright (c) 2024 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.Algebra.Module.LinearMap.Defs

/-!
# Cyclic triple sums

Cyclic triple sums are meant as auxiliary tools for proving properties like Jacobi
identities and Lie algebra 2-cocycle conditions.

## Main definitions

* `cyclicTripleSum`: An auxiliary function given by
  âŸ¨x,y,zâŸ© â†¦ Ï†(x,Î²(y,z)) + Ï†(y,Î²(z,x)) + Ï†(z,Î²(x,y)) where Ï† and Î² are two-variable functions
  on a set V with values in sets W and V, respectively.
* `cyclicTripleSumHom`: A trilinear function given by
  âŸ¨x,y,zâŸ© â†¦ Ï†(x,Î²(y,z)) + Ï†(y,Î²(z,x)) + Ï†(z,Î²(x,y)) where Ï† and Î² are bilinear functions on V
  with values in sets W and V, respectively. This function can be used in calculations towards
  the Jacobi identity in Lie algebras and the Lie algebra 2-cocycle condition.

## Main statements

A number of easy but convenient auxiliary properties towards trilinearity
of `cyclicTripleSumHom Î² Ï†` (which is mathematically trivial but should not be repeated
too often in Lean) are proven for the cyclic triple sums:
* `cyclicTripleSum_map_add_of_bilin`: If the functions Ï† and Î² are bilinear, then the cyclic triple
  sum is additive in its last variable. Additivity in the first and second variable are similarly
  obtained (the results immediately after).
* `cyclicTripleSum_map_smul_of_bilin`: If the functions Ï† and Î² are bilinear, then the cyclic triple
  sum respects scalar multiplication in its last variable. Scalar multiplication in the first and
  second variable are similarly obtained (the results immediately after).

## Tags

Jacobi identity, Lie algebra 2-cocycle condition

-/

namespace VirasoroProject

section cyclicTripleSum

/-! ### Cyclic triple sums as tools for Jacobi identities and 2-cocycle conditions -/

variable {V W : Type*}

/-- An auxiliary function for proofs of Jacobi identities etc.

Given functions Î² : V Ã— V â†’ V and Ï† : V Ã— V â†’ W where W has additive structure,
`cyclicTripleSum Î² Ï†` is the function of three variables on V defined by:
âŸ¨x,y,zâŸ© â†¦ Ï†(x,Î²(y,z)) + Ï†(y,Î²(z,x)) + Ï†(z,Î²(x,y)). -/
def cyclicTripleSum [Add W] (Î² : V â†’ V â†’ V) (Ï† : V â†’ V â†’ W) (x y z : V) : W :=
  Ï† x (Î² y z) + Ï† y (Î² z x) + Ï† z (Î² x y)

lemma cyclicTripleSum_apply [Add W] (Î² : V â†’ V â†’ V) (Ï† : V â†’ V â†’ W) (x y z : V) :
    cyclicTripleSum Î² Ï† x y z = Ï† x (Î² y z) + Ï† y (Î² z x) + Ï† z (Î² x y) := rfl

variable [AddCommMonoid W]

lemma cyclicTripleSum_cyclic (Î² : V â†’ V â†’ V) (Ï† : V â†’ V â†’ W) (x y z : V) :
    cyclicTripleSum Î² Ï† x y z = cyclicTripleSum Î² Ï† y z x := by
  simp only [cyclicTripleSum]
  ac_rfl

lemma cyclicTripleSum_cyclic' (Î² : V â†’ V â†’ V) (Ï† : V â†’ V â†’ W) (x y z : V) :
    cyclicTripleSum Î² Ï† x y z = cyclicTripleSum Î² Ï† z x y := by
  simp_rw [cyclicTripleSum_cyclic]

lemma cyclicTripleSum_map_add_fst_of_map_add [Add V] (Î² : V â†’ V â†’ V) (Ï† : V â†’ V â†’ W)
    (h : âˆ€ x y zâ‚ zâ‚‚ : V, cyclicTripleSum Î² Ï† x y (zâ‚ + zâ‚‚)
      = cyclicTripleSum Î² Ï† x y zâ‚ + cyclicTripleSum Î² Ï† x y zâ‚‚) (xâ‚ xâ‚‚ y z : V) :
    cyclicTripleSum Î² Ï† (xâ‚ + xâ‚‚) y z
      = cyclicTripleSum Î² Ï† xâ‚ y z + cyclicTripleSum Î² Ï† xâ‚‚ y z := by
  simpa only [cyclicTripleSum_cyclic _ _ _ y] using h y z xâ‚ xâ‚‚

lemma cyclicTripleSum_map_add_snd_of_map_add [Add V] (Î² : V â†’ V â†’ V) (Ï† : V â†’ V â†’ W)
    (h : âˆ€ x y zâ‚ zâ‚‚ : V, cyclicTripleSum Î² Ï† x y (zâ‚ + zâ‚‚)
      = cyclicTripleSum Î² Ï† x y zâ‚ + cyclicTripleSum Î² Ï† x y zâ‚‚) (x yâ‚ yâ‚‚ z : V) :
    cyclicTripleSum Î² Ï† x (yâ‚ + yâ‚‚) z
      = cyclicTripleSum Î² Ï† x yâ‚ z + cyclicTripleSum Î² Ï† x yâ‚‚ z := by
  simpa only [cyclicTripleSum_cyclic' _ _ x] using h z x yâ‚ yâ‚‚

lemma cyclicTripleSum_map_smul_fst_of_map_smul {R : Type*} [SMul R V] [SMul R W]
    (Î² : V â†’ V â†’ V) (Ï† : V â†’ V â†’ W)
    (h : âˆ€ c : R, âˆ€ x y z : V,
      cyclicTripleSum Î² Ï† x y (c â€¢ z) = c â€¢ cyclicTripleSum Î² Ï† x y z) (c : R) (x y z : V) :
    cyclicTripleSum Î² Ï† (c â€¢ x) y z = c â€¢ cyclicTripleSum Î² Ï† x y z := by
  simpa only [cyclicTripleSum_cyclic _ _ _ y] using h c y z x

lemma cyclicTripleSum_map_smul_snd_of_map_smul {R : Type*} [SMul R V] [SMul R W]
    (Î² : V â†’ V â†’ V) (Ï† : V â†’ V â†’ W)
    (h : âˆ€ c : R, âˆ€ x y z : V,
      cyclicTripleSum Î² Ï† x y (c â€¢ z) = c â€¢ cyclicTripleSum Î² Ï† x y z) (c : R) (x y z : V) :
    cyclicTripleSum Î² Ï† x (c â€¢ y) z = c â€¢ cyclicTripleSum Î² Ï† x y z := by
  simpa only [cyclicTripleSum_cyclic' _ _ x] using h c z x y

end cyclicTripleSum

section cyclicTripleSumAdditive

variable {V W : Type*} [AddCommMonoid V] [AddCommMonoid W]

lemma cyclicTripleSum_map_add_of_bilin (Î² : V â†’+ V â†’+ V) (Ï† : V â†’+ V â†’+ W) (x y zâ‚ zâ‚‚ : V) :
    cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x y (zâ‚ + zâ‚‚)
      = cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x y zâ‚
        + cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x y zâ‚‚ := by
  simp only [cyclicTripleSum, map_add, AddMonoidHom.add_apply]
  ac_rfl

lemma cyclicTripleSum_map_add_snd_of_bilin (Î² : V â†’+ V â†’+ V) (Ï† : V â†’+ V â†’+ W) (x yâ‚ yâ‚‚ z : V) :
    cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x (yâ‚ + yâ‚‚) z
      = cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x yâ‚ z
        + cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x yâ‚‚ z := by
  apply cyclicTripleSum_map_add_snd_of_map_add
  exact cyclicTripleSum_map_add_of_bilin Î² Ï†

lemma cyclicTripleSum_map_add_fst_of_bilin (Î² : V â†’+ V â†’+ V) (Ï† : V â†’+ V â†’+ W) (xâ‚ xâ‚‚ y z : V) :
    cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) (xâ‚ + xâ‚‚) y z
      = cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) xâ‚ y z
        + cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) xâ‚‚ y z := by
  apply cyclicTripleSum_map_add_fst_of_map_add
  exact cyclicTripleSum_map_add_of_bilin Î² Ï†

variable {ğ•œ} [CommSemiring ğ•œ]
variable [Module ğ•œ V] [Module ğ•œ W]

lemma cyclicTripleSum_map_smul_of_bilin (Î² : V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] V) (Ï† : V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] W)
    (c : ğ•œ) (x y z : V) :
    cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x y (c â€¢ z)
      = c â€¢ cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x y z := by
  simp [cyclicTripleSum]

lemma cyclicTripleSum_map_smul_fst_of_bilin (Î² : V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] V) (Ï† : V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] W)
    (c : ğ•œ) (x y z : V) :
    cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) (c â€¢ x) y z
      = c â€¢ cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x y z := by
  apply cyclicTripleSum_map_smul_fst_of_map_smul
  exact cyclicTripleSum_map_smul_of_bilin Î² Ï†

lemma cyclicTripleSum_map_smul_snd_of_bilin (Î² : V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] V) (Ï† : V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] W)
    (c : ğ•œ) (x y z : V) :
    cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x (c â€¢ y) z
      = c â€¢ cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x y z := by
  apply cyclicTripleSum_map_smul_snd_of_map_smul
  exact cyclicTripleSum_map_smul_of_bilin Î² Ï†

end cyclicTripleSumAdditive

section cyclicTripleSumBilin

-- TODO: Does a more convenient coercion exist? Should this be made to a literal coercion?
/-- "Coerce" a bilinear map into a biadditive map. -/
def _root_.LinearMap.toBiadditive
    {Vâ‚ Vâ‚‚ Vâ‚ƒ : Type*} [AddCommMonoid Vâ‚] [AddCommMonoid Vâ‚‚] [AddCommMonoid Vâ‚ƒ]
    {Râ‚ Râ‚‚ Râ‚ƒ : Type*} [CommSemiring Râ‚] [CommSemiring Râ‚‚] [CommSemiring Râ‚ƒ]
    {Ïƒ : Râ‚ â†’+* Râ‚ƒ} {Ï„ : Râ‚‚ â†’+* Râ‚ƒ}
    [Module Râ‚ Vâ‚] [Module Râ‚‚ Vâ‚‚] [Module Râ‚ƒ Vâ‚ƒ]
    (f : Vâ‚ â†’â‚›â‚—[Ïƒ] Vâ‚‚ â†’â‚›â‚—[Ï„] Vâ‚ƒ) :
    Vâ‚ â†’+ Vâ‚‚ â†’+ Vâ‚ƒ where
  toFun := fun x â†¦ {
    toFun := fun y â†¦ f x y
    map_zero' := by simp
    map_add' := by simp
  }
  map_zero' := by ext y ; simp
  map_add' xâ‚ xâ‚‚ := by ext y ; simp

variable {V W : Type*} [AddCommMonoid V] [AddCommMonoid W]
variable {ğ•œ : Type*} [CommSemiring ğ•œ]
variable [Module ğ•œ V] [Module ğ•œ W]

/-- An auxiliary trilinear map for proofs of Jacobi identities.

Given bilinear functions Î² : V Ã— V â†’ V and Ï† : V Ã— V â†’ W, `cyclicTripleSumHom Î² Ï†` is the
trilinear function on V defined by: âŸ¨x,y,zâŸ© â†¦ Ï†(x,Î²(y,z)) + Ï†(y,Î²(z,x)) + Ï†(z,Î²(x,y)). -/
noncomputable def cyclicTripleSumHom (Î² : V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] V) (Ï† : V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] W) :
    V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] W where
  toFun := fun x â†¦
    { toFun := fun y â†¦
        { toFun := fun z â†¦ cyclicTripleSum (fun a â†¦ â‡‘(Î² a)) (fun a â†¦ â‡‘(Ï† a)) x y z
          map_add' zâ‚ zâ‚‚ :=
            cyclicTripleSum_map_add_of_bilin Î².toBiadditive Ï†.toBiadditive x y zâ‚ zâ‚‚
          map_smul' c z := cyclicTripleSum_map_smul_of_bilin Î² Ï† c x y z }
      map_add' yâ‚ yâ‚‚ := by
        ext z
        exact cyclicTripleSum_map_add_snd_of_bilin Î².toBiadditive Ï†.toBiadditive x yâ‚ yâ‚‚ z
      map_smul' c y := by
        ext z
        exact cyclicTripleSum_map_smul_snd_of_bilin Î² Ï† c x y z }
  map_add' xâ‚ xâ‚‚ := by
    ext y z
    exact cyclicTripleSum_map_add_fst_of_bilin Î².toBiadditive Ï†.toBiadditive xâ‚ xâ‚‚ y z
  map_smul' c x := by
    ext y z
    exact cyclicTripleSum_map_smul_fst_of_bilin Î² Ï† c x y z

lemma cyclicTripleSumHom_apply (Î² : V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] V) (Ï† : V â†’â‚—[ğ•œ] V â†’â‚—[ğ•œ] W) (x y z : V) :
    cyclicTripleSumHom Î² Ï† x y z = Ï† x (Î² y z) + Ï† y (Î² z x) + Ï† z (Î² x y) := rfl

end cyclicTripleSumBilin

end VirasoroProject -- namespace
