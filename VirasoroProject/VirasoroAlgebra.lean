/-
Copyright (c) 2024 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import VirasoroProject.IsCentralExtension
import VirasoroProject.ToMathlib.Algebra.Lie.Abelian
import VirasoroProject.VirasoroCocycle

/-!
# The Virasoro algebra

This file defines the Virasoro algebra, an infinite-dimensional Lie algebra which is the unique
one-dimensional central extension of the Witt algebra.

(In two-dimensional conformal field theory (CFT), the Virasoro algebra describes the effects of
infinitesimal conformal transformations on the state space of the theory, or equivalently on its
space of local fields.)

## Main definitions

* `VirasoroAlgebra`: The Virasoro algebra.
* `VirasoroAlgebra.lgen`: The (commonly used) elements Lâ‚™, n âˆˆ â„¤, of the Virasoro algebra.
* `VirasoroAlgebra.cgen`: The (commonly used) central element C of the Virasoro algebra.
* `VirasoroAlgebra.ofCentral` and `VirasoroAlgebra.toWittAlgebra`: The maps in the short exact
  sequence 0 âŸ¶ ð•œ âŸ¶ VirasoroAlgebra âŸ¶ WittAlgebra âŸ¶ 0.

## Main statements

* `VirasoroAlgebra.instLieAlgebra`: The Virasoro algebra is a Lie algebra.
* `VirasoroAlgebra.isCentralExtension`: The Virasoro algebra is a cetral extension of the
  Witt algebra.

## Implementation notes

The Virasoro algebra is defined as a central extension of the Witt algebra. (A more direct
definition based on defining a Lie bracket on a countably infinite dimensional vector space
would also be possible.)

## Tags

Virasoro algebra

-/

namespace VirasoroProject

section VirasoroAlgebra

/-! ### The Virasoro algebra -/

variable (ð•œ : Type*) [Field ð•œ]
variable [CharZero ð•œ]

/-- The Virasoro algebra. -/
def VirasoroAlgebra := LieTwoCocycle.CentralExtension (WittAlgebra.virasoroCocycle ð•œ)

namespace VirasoroAlgebra

lemma ext' {X Y : VirasoroAlgebra ð•œ} (hâ‚ : X.1 = Y.1) (hâ‚‚ : X.2 = Y.2) :
    X = Y :=
  LieTwoCocycle.CentralExtension.ext hâ‚ hâ‚‚

/-- The Virasoro algebra is a Lie ring. -/
noncomputable instance : LieRing (VirasoroAlgebra ð•œ) :=
  LieTwoCocycle.CentralExtension.instLieRing _

/-- The Virasoro algebra is a Lie algebra. -/
noncomputable instance : LieAlgebra ð•œ (VirasoroAlgebra ð•œ) :=
  LieTwoCocycle.CentralExtension.instLieAlgebra _

variable {ð•œ}

/-- The projection from Virasoro algebra to Witt algebra. -/
def toWittAlgebra : VirasoroAlgebra ð•œ â†’â‚—â…ð•œâ† WittAlgebra ð•œ :=
  LieTwoCocycle.CentralExtension.proj (WittAlgebra.virasoroCocycle ð•œ)

variable (ð•œ)

/-- The embedding of central elements to Virasoro algebra. -/
noncomputable def ofCentral : ð•œ â†’â‚—â…ð•œâ† VirasoroAlgebra ð•œ :=
  LieTwoCocycle.CentralExtension.emb (WittAlgebra.virasoroCocycle ð•œ)

lemma bracket_def' (X Y : VirasoroAlgebra ð•œ) :
    â…X, Yâ† = âŸ¨â…toWittAlgebra X, toWittAlgebra Yâ†,
              (WittAlgebra.virasoroCocycle ð•œ) (toWittAlgebra X) (toWittAlgebra Y)âŸ© := by
  rfl

@[simp] lemma bracket_fst (X Y : VirasoroAlgebra ð•œ) :
    â…X, Yâ†.1 = â…toWittAlgebra X, toWittAlgebra Yâ† := rfl

@[simp] lemma bracket_snd (X Y : VirasoroAlgebra ð•œ) :
    â…X, Yâ†.2 = (WittAlgebra.virasoroCocycle ð•œ) (toWittAlgebra X) (toWittAlgebra Y) := rfl

lemma add_def' (X Y : VirasoroAlgebra ð•œ) :
    X + Y = âŸ¨X.1 + Y.1, X.2 + Y.2âŸ© := rfl

lemma smul_def' (c : ð•œ) (X : VirasoroAlgebra ð•œ) :
    c â€¢ X = âŸ¨c â€¢ X.1, c * X.2âŸ© := rfl

@[simp] lemma add_fst (X Y : VirasoroAlgebra ð•œ) :
    (X + Y).1 = X.1 + Y.1 := rfl

@[simp] lemma add_snd (X Y : VirasoroAlgebra ð•œ) :
    (X + Y).2 = X.2 + Y.2 := rfl

@[simp] lemma smul_fst (c : ð•œ) (X : VirasoroAlgebra ð•œ) :
    (c â€¢ X).1 = c â€¢ X.1 := rfl

@[simp] lemma smul_snd (c : ð•œ) (X : VirasoroAlgebra ð•œ) :
    (c â€¢ X).2 = c * X.2 := rfl

/-- The Virasoro algebra is a central extension of the Witt algebra. -/
instance isCentralExtension : LieAlgebra.IsCentralExtension (ofCentral ð•œ) toWittAlgebra :=
  LieTwoCocycle.CentralExtension.isCentralExtension _

/-- The (commonly used) `Lâ‚™` elements of the Virasoro algebra, for `n âˆˆ â„¤`. -/
noncomputable def lgen (n : â„¤) : VirasoroAlgebra ð•œ :=
  âŸ¨WittAlgebra.lgen ð•œ n, 0âŸ©

/-- The (commonly used) `C` central element of the Virasoro algebra. -/
noncomputable def cgen : VirasoroAlgebra ð•œ := ofCentral ð•œ 1

lemma cgen_eq_ofCentral_one : cgen ð•œ = ofCentral ð•œ 1 := rfl

lemma cgen_eq' : cgen ð•œ = âŸ¨0, 1âŸ© := rfl

lemma lgen_eq' (n : â„¤) : lgen ð•œ n = âŸ¨WittAlgebra.lgen ð•œ n, 0âŸ© := rfl

lemma toWittAlgebra_cgen :
  toWittAlgebra (cgen ð•œ) = 0 := rfl

@[simp] lemma toWittAlgebra_lgen (n : â„¤) :
  toWittAlgebra (lgen ð•œ n) = WittAlgebra.lgen ð•œ n := rfl

@[simp] lemma cgen_bracket (Z : VirasoroAlgebra ð•œ) :
    â…cgen ð•œ, Zâ† = 0 :=
  (isCentralExtension ð•œ).central 1 Z

@[simp] lemma lgen_bracket (n m : â„¤) :
    â…lgen ð•œ n, lgen ð•œ mâ†
      = (n - m : ð•œ) â€¢ lgen ð•œ (n + m) + if n + m = 0 then ((n^3 - n : ð•œ)/12) â€¢ cgen ð•œ else 0 := by
  simp_rw [bracket_def']
  by_cases h : n + m = 0
  Â· simp [h]
    apply ext'
    Â· simp [lgen, cgen_eq']
    Â· simp [WittAlgebra.virasoroCocycle_apply_lgen_lgen, lgen, cgen_eq', h]
  Â· simp [h]
    apply ext'
    Â· simp [lgen]
    Â· simp [WittAlgebra.virasoroCocycle_apply_lgen_lgen, h, lgen]

lemma lgen_bracket' (n m : â„¤) :
    â…lgen ð•œ n, lgen ð•œ mâ†
      = (n - m : ð•œ) â€¢ lgen ð•œ (n + m) + if n + m = 0 then ((n-1 : ð•œ)*n*(n+1)/12) â€¢ cgen ð•œ else 0 := by
  rw [lgen_bracket] ; congr ; ring

noncomputable def stdSection : WittAlgebra ð•œ â†’â‚—[ð•œ] VirasoroAlgebra ð•œ where
  toFun L := âŸ¨L, 0âŸ©
  map_add' Lâ‚ Lâ‚‚ := by rw [LieTwoCocycle.CentralExtension.add_def] ; simp
  map_smul' c L := by rw [LieTwoCocycle.CentralExtension.smul_def] ; simp

lemma stdSection_prop :
    (toWittAlgebra (ð•œ := ð•œ)) âˆ˜â‚— (stdSection ð•œ) = (1 : WittAlgebra ð•œ â†’â‚—[ð•œ] WittAlgebra ð•œ) :=
  rfl

noncomputable def basisLC : Basis (Option â„¤) ð•œ (VirasoroAlgebra ð•œ) :=
  ((isCentralExtension ð•œ).basis (stdSection ð•œ) (stdSection_prop ð•œ)
          (WittAlgebra.lgen ð•œ) (Basis.singleton Unit ð•œ)).reindex
    { toFun uz := match uz with
        | Sum.inl _ => none
        | Sum.inr l => some l
      invFun oz := match oz with
        | none => Sum.inl âŸ¨âŸ©
        | some l => Sum.inr l
      left_inv uz := match uz with
        | Sum.inl _ => rfl
        | Sum.inr _ => rfl
      right_inv oz := match oz with
        | none => rfl
        | some _ => rfl }

end VirasoroAlgebra -- namespace

end VirasoroAlgebra -- section

end VirasoroProject -- namespace
