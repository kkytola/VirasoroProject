/-
Copyright (c) 2024 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import VirasoroProject.IsCentralExtension
import VirasoroProject.ToMathlib.Algebra.Lie.Abelian
import VirasoroProject.VirasoroCocycle

/-!
# The Virasoro algebra

This file defines the Virasoro algebra, an infinite-dimensional Lie algebra which is the unique
one-dimensional central extension of the Witt algebra.

(In two-dimensional conformal field theory (CFT), the Virasoro algebra describes the effects of
infinitesimal conformal transformations on the state space of the theory, or equivalently on its
space of local fields.)

## Main definitions

* ...
* ...

## Main statements

* ...

* ...

## Implementation notes

## Tags

Virasoro algebra

-/

namespace VirasoroProject

section VirasoroAlgebra

/-! ### The Virasoro algebra -/

variable (ğ•œ : Type*) [Field ğ•œ]
variable [CharZero ğ•œ]

/-- The Virasoro algebra. -/
def VirasoroAlgebra := LieTwoCocycle.CentralExtension (WittAlgebra.virasoroCocycle ğ•œ)

namespace VirasoroAlgebra

private lemma ext' {X Y : VirasoroAlgebra ğ•œ} (hâ‚ : X.1 = Y.1) (hâ‚‚ : X.2 = Y.2) :
    X = Y :=
  LieTwoCocycle.CentralExtension.ext hâ‚ hâ‚‚

/-- The Virasoro algebra is a Lie ring. -/
noncomputable instance : LieRing (VirasoroAlgebra ğ•œ) :=
  LieTwoCocycle.CentralExtension.instLieRing _

/-- The Virasoro algebra is a Lie algebra. -/
noncomputable instance : LieAlgebra ğ•œ (VirasoroAlgebra ğ•œ) :=
  LieTwoCocycle.CentralExtension.instLieAlgebra _

variable {ğ•œ}

/-- The projection from Virasoro algebra to Witt algebra. -/
def toWittAlgebra : VirasoroAlgebra ğ•œ â†’â‚—â…ğ•œâ† WittAlgebra ğ•œ :=
  LieTwoCocycle.CentralExtension.proj (WittAlgebra.virasoroCocycle ğ•œ)

variable (ğ•œ)

/-- The embedding of central elements to Virasoro algebra. -/
noncomputable def ofCentral : ğ•œ â†’â‚—â…ğ•œâ† VirasoroAlgebra ğ•œ :=
  LieTwoCocycle.CentralExtension.emb (WittAlgebra.virasoroCocycle ğ•œ)

private lemma bracket_def' (X Y : VirasoroAlgebra ğ•œ) :
    â…X, Yâ† = âŸ¨â…toWittAlgebra X, toWittAlgebra Yâ†,
              (WittAlgebra.virasoroCocycle ğ•œ) (toWittAlgebra X) (toWittAlgebra Y)âŸ© := by
  rfl

@[simp] private lemma bracket_fst (X Y : VirasoroAlgebra ğ•œ) :
    â…X, Yâ†.1 = â…toWittAlgebra X, toWittAlgebra Yâ† := rfl

@[simp] private lemma bracket_snd (X Y : VirasoroAlgebra ğ•œ) :
    â…X, Yâ†.2 = (WittAlgebra.virasoroCocycle ğ•œ) (toWittAlgebra X) (toWittAlgebra Y) := rfl

private lemma add_def' (X Y : VirasoroAlgebra ğ•œ) :
    X + Y = âŸ¨X.1 + Y.1, X.2 + Y.2âŸ© := rfl

private lemma smul_def' (c : ğ•œ) (X : VirasoroAlgebra ğ•œ) :
    c â€¢ X = âŸ¨c â€¢ X.1, c * X.2âŸ© := rfl

@[simp] private lemma add_fst (X Y : VirasoroAlgebra ğ•œ) :
    (X + Y).1 = X.1 + Y.1 := rfl

@[simp] private lemma add_snd (X Y : VirasoroAlgebra ğ•œ) :
    (X + Y).2 = X.2 + Y.2 := rfl

@[simp] private lemma smul_fst (c : ğ•œ) (X : VirasoroAlgebra ğ•œ) :
    (c â€¢ X).1 = c â€¢ X.1 := rfl

@[simp] private lemma smul_snd (c : ğ•œ) (X : VirasoroAlgebra ğ•œ) :
    (c â€¢ X).2 = c * X.2 := rfl

/-- The Virasoro algebra is a central extension of the Witt algebra. -/
instance isCentralExtension : LieAlgebra.IsCentralExtension (ofCentral ğ•œ) toWittAlgebra :=
  LieTwoCocycle.CentralExtension.isCentralExtension _

/-- The (commonly used) `Lâ‚™` elements of the Virasoro algebra, for `n âˆˆ â„¤`. -/
noncomputable def lgen (n : â„¤) : VirasoroAlgebra ğ•œ :=
  âŸ¨WittAlgebra.lgen ğ•œ n, 0âŸ©

/-- The (commonly used) `C` central element of the Virasoro algebra. -/
noncomputable def cgen : VirasoroAlgebra ğ•œ := ofCentral ğ•œ 1

lemma cgen_eq_ofCentral_one : cgen ğ•œ = ofCentral ğ•œ 1 := rfl

private lemma cgen_eq' : cgen ğ•œ = âŸ¨0, 1âŸ© := rfl

private lemma lgen_eq' (n : â„¤) : lgen ğ•œ n = âŸ¨WittAlgebra.lgen ğ•œ n, 0âŸ© := rfl

lemma toWittAlgebra_cgen :
  toWittAlgebra (cgen ğ•œ) = 0 := rfl

@[simp] lemma toWittAlgebra_lgen (n : â„¤) :
  toWittAlgebra (lgen ğ•œ n) = WittAlgebra.lgen ğ•œ n := rfl

@[simp] lemma cgen_bracket (Z : VirasoroAlgebra ğ•œ) :
    â…cgen ğ•œ, Zâ† = 0 :=
  (isCentralExtension ğ•œ).central 1 Z

@[simp] lemma lgen_bracket (n m : â„¤) :
    â…lgen ğ•œ n, lgen ğ•œ mâ†
      = (n - m : ğ•œ) â€¢ lgen ğ•œ (n + m) + if n + m = 0 then ((n^3 - n : ğ•œ)/12) â€¢ cgen ğ•œ else 0 := by
  simp_rw [bracket_def']
  by_cases h : n + m = 0
  Â· simp [h]
    apply ext'
    Â· simp [lgen, cgen_eq']
    Â· simp [WittAlgebra.virasoroCocycle_apply_lgen_lgen, lgen, cgen_eq', h]
  Â· simp [h]
    apply ext'
    Â· simp [lgen]
    Â· simp [WittAlgebra.virasoroCocycle_apply_lgen_lgen, h, lgen]

lemma lgen_bracket' (n m : â„¤) :
    â…lgen ğ•œ n, lgen ğ•œ mâ†
      = (n - m : ğ•œ) â€¢ lgen ğ•œ (n + m) + if n + m = 0 then ((n-1 : ğ•œ)*n*(n+1)/12) â€¢ cgen ğ•œ else 0 := by
  rw [lgen_bracket] ; congr ; ring

end VirasoroAlgebra -- namespace

end VirasoroAlgebra -- section

end VirasoroProject -- namespace
