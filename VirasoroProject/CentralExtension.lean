/-
Copyright (c) 2024 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import VirasoroProject.LieCohomologySmallDegree

/-!
# Central extensions of Lie algebras defined by 2-cocycles

This file defines the central extension of a Lie algebra determined by a 2-cocycle. It is proven
that two central extensions are isomorphic if the corresponding cocycles differ by a coboundary.

## Main definitions

* `LieTwoCocycle.CentralExtension`: The central extension of a Lie algebra ğ“– by an abelian Lie
  algebra ğ“ defined by a 2-cocycle Î³ âˆˆ HÂ²(ğ“–,ğ“).
* `LieTwoCocycle.CentralExtension.equiv_of_lieTwoCoboundary`: An isomorphism between the central
  extensions defined by two 2-cocycles which differ by a coboundary.

## Main statements

* `LieTwoCocycle.CentralExtension.instLieAlgebra`: The central extension defined by a 2-cocycle
  is a Lie algebra.

## Implementation notes

`LieTwoCocycle.CentralExtension` is the concrete construction of a central extension. The defining
property (characteristic predicate) of central extensions is `IsCentralExtension`
(see the file `IsCentralExtension.lean`.)

## Tags

Lie algebra, central extension, 2-cocycle

-/

namespace VirasoroProject

universe u
variable (ğ•œ : Type*) [CommRing ğ•œ]
variable (ğ“– ğ“ : Type u) [LieRing ğ“–] [AddCommGroup ğ“] [LieAlgebra ğ•œ ğ“–] [Module ğ•œ ğ“]

variable {ğ•œ ğ“– ğ“}

section LieTwoCocycle.CentralExtension

/-! ### Lie algebra central extensions determined by 2-cocycles -/

namespace LieTwoCocycle

set_option linter.unusedVariables false in
/-- The underlying type of the central extension of Lie algebras determined by a Lie
algebra 2-cocycle. -/
def CentralExtension (Î³ : LieTwoCocycle ğ•œ ğ“– ğ“) := ğ“– Ã— ğ“

variable {Î³ : LieTwoCocycle ğ•œ ğ“– ğ“}

namespace CentralExtension

@[ext] lemma ext {Z W : Î³.CentralExtension} (hX : Z.1 = W.1) (hA : Z.2 = W.2) :
    Z = W := Prod.ext hX hA

/-- Coercion of an element in a central extension to a pair. -/
def coeProd (Z : Î³.CentralExtension) : ğ“– Ã— ğ“ := Z

instance : AddCommGroup (Î³.CentralExtension) := Prod.instAddCommGroup

instance : Module ğ•œ (Î³.CentralExtension) := Prod.instModule

lemma add_def (Zâ‚ Zâ‚‚ : Î³.CentralExtension) :
  Zâ‚ + Zâ‚‚ = âŸ¨Zâ‚.1 + Zâ‚‚.1, Zâ‚.2 + Zâ‚‚.2âŸ© := rfl

lemma smul_def (c : ğ•œ) (Z : Î³.CentralExtension) :
  c â€¢ Z = âŸ¨c â€¢ Z.1, c â€¢ Z.2âŸ© := rfl

@[simp] lemma add_fst (Z W : Î³.CentralExtension) :
    (Z + W).1 = Z.1 + W.1 := rfl

@[simp] lemma add_snd (Z W : Î³.CentralExtension) :
    (Z + W).2 = Z.2 + W.2 := rfl

@[simp] lemma smul_fst (c : ğ•œ) (Z : Î³.CentralExtension) :
    (c â€¢ Z).1 = c â€¢ Z.1 := rfl

@[simp] lemma smul_snd (c : ğ•œ) (Z : Î³.CentralExtension) :
    (c â€¢ Z).2 = c â€¢ Z.2 := rfl

end CentralExtension -- namespace

variable (Î³)

open LinearMapClass RingHom in
/-- The Lie bracket in a central extension defined by a Lie algebra 2-cocycle. -/
def bracket : Î³.CentralExtension
      â†’â‚—[ğ•œ] Î³.CentralExtension â†’â‚—[ğ•œ] Î³.CentralExtension where
  toFun := fun âŸ¨X,_âŸ© â†¦ {
    toFun := fun âŸ¨Y,_âŸ© â†¦ âŸ¨â…X,Yâ†, Î³ X YâŸ©
    map_add' := by intros ; simp_all only [lie_add, map_add] ; rfl
    map_smul' := by intros ; simp_all only [lie_smul, map_smul, id_apply] ; rfl }
  map_add' := by
    intros
    simp_all only [add_lie, map_add, LinearMap.add_apply]
    rfl
  map_smul' := by
    intros
    simp_all only [smul_lie, map_smul, LinearMap.smul_apply, id_apply]
    rfl

@[simp] lemma bracket_apply (Z W : Î³.CentralExtension) :
    Î³.bracket Z W = âŸ¨â…Z.fst, W.fstâ†, Î³ Z.fst W.fstâŸ© := rfl

lemma bracket_self (Z : Î³.CentralExtension) :
    Î³.bracket Z Z = 0 := by
  simp
  rfl

lemma bracket_smul (c : ğ•œ) (Z W : Î³.CentralExtension) :
    Î³.bracket Z (c â€¢ W) = c â€¢ Î³.bracket Z W := by
  simp only [LinearMapClass.map_smul, LieTwoCocycle.bracket_apply]

lemma bracket_leibniz (Z Wâ‚ Wâ‚‚ : Î³.CentralExtension) :
    Î³.bracket Z (Î³.bracket Wâ‚ Wâ‚‚)
      = Î³.bracket (Î³.bracket Z Wâ‚) Wâ‚‚
        + Î³.bracket Wâ‚ (Î³.bracket Z Wâ‚‚) := by
  simp only [Î³.bracket_apply]
  ext
  Â· exact leibniz_lie Z.1 Wâ‚.1 Wâ‚‚.1
  Â· apply Î³.leibniz

namespace CentralExtension

/-- The central extension is a Lie ring. -/
instance : LieRing Î³.CentralExtension where
  bracket Z W := Î³.bracket Z W
  add_lie Zâ‚ Zâ‚‚ W := by simp
  lie_add Z Wâ‚ Wâ‚‚ := by simp ; rfl
  lie_self := Î³.bracket_self
  leibniz_lie Zâ‚ Zâ‚‚ W := Î³.bracket_leibniz Zâ‚ Zâ‚‚ W

/-- The central extension is a Lie algebra. -/
instance : LieAlgebra ğ•œ Î³.CentralExtension where
  lie_smul := Î³.bracket_smul

lemma lie_def (Z W : Î³.CentralExtension) :
    â…Z, Wâ† = âŸ¨â…Z.1, W.1â†, Î³ Z.1 W.1âŸ© := rfl

@[simp] lemma lie_fst (Z W : Î³.CentralExtension) :
    â…Z, Wâ†.1 = â…Z.1, W.1â† := rfl

@[simp] lemma lie_snd (Z W : Î³.CentralExtension) :
    â…Z, Wâ†.2 = Î³ Z.1 W.1 := rfl

end CentralExtension -- namespace

end LieTwoCocycle -- namespace

variable (Î² : LieOneCocycle ğ•œ ğ“– ğ“)
variable (Î³)

/-- A Lie algebra homomorphism between two central extensions determined by cocycles
which differ by a coboundary. -/
def LieOneCocycle.bdryHom :
    (Î³.CentralExtension) â†’â‚—â…ğ•œâ† (Î³ + Î².bdry).CentralExtension where
  toFun := fun Z â†¦ âŸ¨Z.1, Z.2 + Î² Z.1âŸ©
  map_add' Z W := by
    ext
    Â· rfl
    Â· calc Z.2 + W.2 + Î² (Z.1 + W.1)
       _ = Z.2 + Î² Z.1 + (W.2 + Î² W.1)                   := by simp only [map_add] ; ac_rfl
       _ = ((Z.1, Z.2 + Î² Z.1) + (W.1, W.2 + Î² W.1)).2   := by rfl
  map_smul' c Z := by
    ext
    Â· rfl
    Â· calc ((c â€¢ Z).1, (c â€¢ Z).2 + Î² (c â€¢ Z).1).2
       _ = c â€¢ Z.2 + Î² (c â€¢ Z.1)           := by rfl
       _ = c â€¢ (Z.2 + Î² Z.1)               := by simp only [LinearMapClass.map_smul, smul_add]
       _ = (c â€¢ (Z.1, Z.2 + Î² Z.1)).2      := by rfl
  map_lie' := by
    intro Z W
    ext <;> rfl

namespace LieTwoCocycle.CentralExtension

/-- Annoyingly the dependent types make it difficult to identify central extensions with equal
but not definitionally equal cocycles (e.g. `Î³ + (Î²â‚ + Î²â‚‚).bdry` vs. `(Î³ + Î²â‚.bdry) + Î²â‚‚.bdry`).
This isomorphism allows to  -/
def congr {Î³â‚ Î³â‚‚ : LieTwoCocycle ğ•œ ğ“– ğ“} (h : Î³â‚ = Î³â‚‚) :
    Î³â‚.CentralExtension â‰ƒâ‚—â…ğ•œâ† Î³â‚‚.CentralExtension where
  toFun := fun Z â†¦ âŸ¨Z.1, Z.2âŸ©
  map_add' Zâ‚ Zâ‚‚ := rfl
  map_smul' c Z := rfl
  map_lie' := by
    intro Zâ‚ Zâ‚‚
    ext <;>
    Â· simp only [lie_def, h, Prod.mk.eta] ; rfl
  invFun := fun Z â†¦ âŸ¨Z.1, Z.2âŸ©
  left_inv := by
    intro Z
    ext <;> dsimp only
  right_inv := by
    intro Z
    ext <;> dsimp only

lemma congr_apply {Î³â‚ Î³â‚‚ : LieTwoCocycle ğ•œ ğ“– ğ“} (h : Î³â‚ = Î³â‚‚) (Z : Î³â‚.CentralExtension) :
    congr h Z = âŸ¨Z.1, Z.2âŸ© := rfl

@[simp] lemma congr_trans {Î³â‚ Î³â‚‚ Î³â‚ƒ : LieTwoCocycle ğ•œ ğ“– ğ“} (hâ‚â‚‚ : Î³â‚ = Î³â‚‚) (hâ‚‚â‚ƒ : Î³â‚‚ = Î³â‚ƒ) :
    (congr hâ‚â‚‚).trans (congr hâ‚‚â‚ƒ) = (congr (hâ‚â‚‚.trans hâ‚‚â‚ƒ)) :=
  rfl

lemma congr_congr_symm {Î³â‚ Î³â‚‚ : LieTwoCocycle ğ•œ ğ“– ğ“} (h : Î³â‚ = Î³â‚‚) :
    (congr h).trans (congr h.symm) = LieEquiv.refl :=
  rfl

lemma hom_of_coboundary_refl (Î³ : LieTwoCocycle ğ•œ ğ“– ğ“) :
    congr (Eq.refl Î³) = LieEquiv.refl (R := ğ•œ) (Lâ‚ := Î³.CentralExtension) :=
  rfl

lemma hom_of_coboundary_add (Î³â‚ Î³â‚‚ Î³â‚ƒ : LieTwoCocycle ğ•œ ğ“– ğ“)
    (Î²â‚ Î²â‚‚ : LieOneCocycle ğ•œ ğ“– ğ“) (hâ‚‚ : Î³â‚ + Î²â‚.bdry = Î³â‚‚) (hâ‚ƒ : Î³â‚‚ + Î²â‚‚.bdry = Î³â‚ƒ) :
    ((congr hâ‚ƒ).toLieHom.comp (Î²â‚‚.bdryHom Î³â‚‚)).comp ((congr hâ‚‚).toLieHom.comp (Î²â‚.bdryHom Î³â‚))
      = (congr (show Î³â‚ + (Î²â‚ + Î²â‚‚).bdry = Î³â‚ƒ by rw [â† hâ‚ƒ, â† hâ‚‚] ; ac_rfl)).toLieHom.comp
          ((Î²â‚ + Î²â‚‚).bdryHom Î³â‚) := by
  ext Z
  Â· rfl
  Â· simp only [LieTwoCocycle.CentralExtension.congr, Prod.mk.eta, LieOneCocycle.bdryHom,
               LieHom.comp_apply, LieHom.coe_mk]
    ac_rfl

/-- A Lie algebra isomorphism between two central extensions determined by cocycles
which differ by a coboundary. -/
noncomputable def equiv_of_lieTwoCoboundary {Î³' : LieTwoCocycle ğ•œ ğ“– ğ“}
    (h : Î³' - Î³ âˆˆ LieTwoCoboundary ğ•œ ğ“– ğ“) :
    (Î³.CentralExtension) â‰ƒâ‚—â…ğ•œâ† (Î³'.CentralExtension) := by
  -- TODO: I should not construct data in tactic mode, but this is tedious to write in term mode...
  simp only [LinearMap.mem_range] at h
  set hÎ² := Exists.choose_spec h
  set Î² := Exists.choose h
  have obs : Î³ + Î².bdry = Î³' := by
    change Î³ + lieOneCocycle_bdryHom _ _ _ Î² = Î³' ; simp [hÎ²]
  have obs' : Î³' + -Î².bdry = Î³ := by
    change Î³' - lieOneCocycle_bdryHom _ _ _ Î² = Î³ ; simp [hÎ²]
  set f := (LieTwoCocycle.CentralExtension.congr obs).toLieHom.comp <| Î².bdryHom Î³
  set g := (LieTwoCocycle.CentralExtension.congr obs').toLieHom.comp <| (-Î²).bdryHom Î³'
  apply LieEquiv.mk_of_comp_eq_id (f := f) (g := g)
  Â· convert LieTwoCocycle.CentralExtension.hom_of_coboundary_add Î³ Î³' Î³ Î² (-Î²) obs obs'
    ext1 Z
    simp only [LieHom.coe_id, id_eq, LieTwoCocycle.CentralExtension.congr, Prod.mk.eta,
      LieOneCocycle.bdryHom, add_neg_cancel, LieHom.comp_apply, LieHom.coe_mk]
    ext
    Â· rfl
    Â· simp only [left_eq_add]
      rfl
  Â· convert LieTwoCocycle.CentralExtension.hom_of_coboundary_add Î³' Î³ Î³' (-Î²) Î² obs' obs
    ext1 Z
    simp only [LieHom.coe_id, id_eq, LieTwoCocycle.CentralExtension.congr, Prod.mk.eta,
      LieOneCocycle.bdryHom, add_neg_cancel, LieHom.comp_apply, LieHom.coe_mk]
    ext
    Â· rfl
    Â· simp only [neg_add_cancel, left_eq_add]
      rfl

end CentralExtension -- namespace

end LieTwoCocycle -- namespace

end LieTwoCocycle.CentralExtension -- section

end VirasoroProject -- namespace
