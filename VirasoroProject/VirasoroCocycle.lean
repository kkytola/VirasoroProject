/-
Copyright (c) 2024 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.Algebra.Azumaya.Basic
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Analysis.Normed.Ring.Lemmas
import Mathlib.Data.Int.Star
import Mathlib.GroupTheory.MonoidLocalization.Basic
import Mathlib.LinearAlgebra.Dual.Lemmas
import VirasoroProject.LieCohomologySmallDegree
import VirasoroProject.WittAlgebra
/-!
# The Virasoro 2-cocycle of the Witt algebra

This file defines the 2-cocycle of the Witt algebra with coefficients in ground field ğ•œ, which
defines the (unique) one-dimenensional central extension known as the Virasoro algebra.
It is proven that the Virasoro cocycle is nontrivial, and in particular the cohomology of the
Witt algebra in degree two does not vanish.

## Main definitions

* `WittAlgebra.virasoroCocycle`: The 2-cocycle which defines the Virasoro algebra as a central
  extension of the Witt algebra and whose cohomology class generates HÂ²(WittAlgebra, ğ•œ).

## Main statements

* `WittAlgebra.cohomologyClass_virasoroCocycle_ne_zero`: The cohomology class of the Virasoro
  cocycle is nonzero.
* `WittAlgebra.nontrivial_lieTwoCohomology`: The Witt algebra cohomology in degree two is
  nontrivial, HÂ²(WittAlgebra, ğ•œ) â‰  0.

## Tags

Witt algebra, Virasoro algebra, Lie algebra cohomology

-/

namespace VirasoroProject

namespace WittAlgebra

variable (ğ•œ : Type*) [Field ğ•œ]

/-- A bilinear map version of the Virasoro cocycle.
(Defining formula: `Î³ (lgen n) (lgen m) = (n^3 - n) / 12 * Î´[n+m,0]`.) -/
noncomputable def virasoroCocycleBilin : (WittAlgebra ğ•œ) â†’â‚—[ğ•œ] (WittAlgebra ğ•œ) â†’â‚—[ğ•œ] ğ•œ :=
  (lgen ğ•œ).constr ğ•œ <| fun n â†¦ (lgen ğ•œ).constr ğ•œ <| fun m â†¦
      if n + m = 0 then (n^3 - n) / 12 else 0

lemma virasoroCocycleBilin_apply_lgen_lgen (n m : â„¤) :
    virasoroCocycleBilin ğ•œ (lgen ğ•œ n) (lgen ğ•œ m)
      = if n + m = 0 then (n^3 - n : ğ•œ) / 12 else 0 := by
  simp [virasoroCocycleBilin]

lemma virasoroCocycleBilin_apply_lgen_lgen' (n m : â„¤) :
    virasoroCocycleBilin ğ•œ (lgen ğ•œ n) (lgen ğ•œ m)
      = if n + m = 0 then (n-1 : ğ•œ) * n * (n + 1) / 12 else 0 := by
  rw [virasoroCocycleBilin_apply_lgen_lgen] ; congr ; ring

lemma virasoroCocycleBilin_eq_neg_flip :
    virasoroCocycleBilin ğ•œ = -(virasoroCocycleBilin ğ•œ).flip := by
  apply LinearMap.ext_basis (lgen _) (lgen _)
  intro n m
  simp [virasoroCocycleBilin]
  by_cases opp : n + m = 0
  Â· simp only [add_comm m n, opp, â†“reduceIte]
    have obs : m = -n := by linarith
    rw [show (m : ğ•œ) = -(n : ğ•œ) by simp [obs]]
    ring
  Â· simp only [add_comm m n, opp, â†“reduceIte, neg_zero]

/-- An auxiliary for of the Leibniz identity of the Virasoro cocycle `WittAlgebra`. -/
 lemma virasoroCocycleBracketCyclic_eq_zero :
    cyclicTripleSumHom (bracket ğ•œ) (virasoroCocycleBilin ğ•œ) = 0 := by
  apply LinearMap.ext_basis (lgen _) (lgen _)
  intro n m
  apply (lgen _).ext
  intro k
  simp only [cyclicTripleSumHom_apply, bracket_lgen_lgen', map_smul, smul_eq_mul,
             LinearMap.zero_apply]
  simp only [virasoroCocycleBilin, Module.Basis.constr_basis, mul_ite, mul_zero]
  rw [show n + (m + k) = n + m + k by ring,
      show m + (k + n) = n + m + k by ring,
      show k + (n + m) = n + m + k by ring]
  by_cases degzero : n + m + k = 0
  Â· simp only [degzero, â†“reduceIte]
    simp only [show k = -m + -n by linarith, Int.cast_add, Int.cast_neg]
    ring
  Â· simp [degzero]

variable [CharZero ğ•œ]

/-- The Virasoro cocycle. -/
noncomputable def virasoroCocycle :
    LieTwoCocycle ğ•œ (WittAlgebra ğ•œ) ğ•œ where
  toBilin := virasoroCocycleBilin ğ•œ
  self' X := by
    apply (self_eq_neg ğ•œ ğ•œ).mp
    simpa only [LinearMap.neg_apply, LinearMap.coe_mk, AddHom.coe_mk]
      using LinearMap.congr_funâ‚‚ (virasoroCocycleBilin_eq_neg_flip ğ•œ) X X
  leibniz' X Y Z := by
    have key' := LinearMap.congr_funâ‚‚ (virasoroCocycleBracketCyclic_eq_zero ğ•œ) X Y
    have key := LinearMap.congr_fun key' Z
    simp only [LinearMap.zero_apply, cyclicTripleSumHom_apply] at key
    rw [add_assoc] at key
    have aux := eq_neg_of_add_eq_zero_left key
    rw [neg_add, show (bracket ğ•œ) Y Z = â…Y, Zâ† from rfl] at aux
    have aux' : ((virasoroCocycleBilin ğ•œ) â…X, Yâ†) Z = -((virasoroCocycleBilin ğ•œ) Z) â…X, Yâ† := by
      simp only [LinearMap.congr_funâ‚‚ (virasoroCocycleBilin_eq_neg_flip ğ•œ) Z â…X, Yâ†,
                 LinearMap.neg_apply, neg_neg]
      rfl
    rw [aux, aux', (lie_skew X Z).symm, map_neg,
        show (bracket ğ•œ) X Y = â…X, Yâ† from rfl, show (bracket ğ•œ) Z X = â…Z, Xâ† from rfl]
    ring

lemma virasoroCocycle_apply_lgen_lgen (n m : â„¤) :
    virasoroCocycle ğ•œ (lgen ğ•œ n) (lgen ğ•œ m) = if n + m = 0 then (n^3 - n : ğ•œ)/12 else 0 :=
  virasoroCocycleBilin_apply_lgen_lgen ğ•œ n m

variable {ğ•œ}

lemma bdry_lgen_lgen_neg_eq (Î² : LieOneCocycle ğ•œ (WittAlgebra ğ•œ) ğ•œ) (n : â„¤) :
    Î².bdry (lgen ğ•œ n) (lgen ğ•œ (-n)) = 2 * n * Î² (lgen ğ•œ 0) := by
  simp [LieOneCocycle.bdry_apply, â† (two_mul (n : ğ•œ))]

variable (ğ•œ)

/-- The Virasoro cocycle is cohomologically nontrivial. -/
theorem cohomologyClass_virasoroCocycle_ne_zero :
    (virasoroCocycle ğ•œ).cohomologyClass â‰  0 := by
  intro con
  obtain âŸ¨Î², hÎ²âŸ© := LieTwoCocycle.exists_eq_bdry _ con
  have hÎ²' (n : â„¤) :
      (virasoroCocycle ğ•œ) (lgen ğ•œ n) (lgen ğ•œ (-n)) = Î².bdry (lgen ğ•œ n) (lgen ğ•œ (-n)) := by
    grind
  simp_rw [bdry_lgen_lgen_neg_eq Î²] at hÎ²'
  have obsVâ‚ := virasoroCocycle_apply_lgen_lgen ğ•œ 3 (-3)
  have obsVâ‚‚ := virasoroCocycle_apply_lgen_lgen ğ•œ 6 (-6)
  rw [hÎ²'] at obsVâ‚ obsVâ‚‚
  norm_num at obsVâ‚ obsVâ‚‚
  have aux := congrArg (2 * Â·) obsVâ‚
  simp only [â† mul_assoc] at aux
  norm_num at aux
  apply (show (4 : ğ•œ) â‰  35/2 by norm_num) <| by grind

/-- The Witt algebra 2-cohomology `HÂ²(WittAlgebra, ğ•œ)` is nontrivial. -/
theorem nontrivial_lieTwoCohomology :
    Nontrivial (LieTwoCohomology ğ•œ (WittAlgebra ğ•œ) ğ•œ) :=
  nontrivial_of_ne _ _ (cohomologyClass_virasoroCocycle_ne_zero ğ•œ)

end WittAlgebra

end VirasoroProject -- namespace
