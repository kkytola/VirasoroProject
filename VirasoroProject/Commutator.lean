/-
Copyright (c) 2025 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.Algebra.Algebra.Basic
import Mathlib.Algebra.EuclideanDomain.Field

/-!
# Commutators of linear maps

This file defines commutators of linear operators, and proves a few useful properties of them.

## Main definitions

* `LinearMap.commutator`: The commutator `[A,B] := AB-BA` of two linear operators `A`, `B`.
* `LinearMap.commutatorBilin`: The commutator `[â¬,â¬]` as a bilinear map on the space of linear maps.

-/

namespace LinearMap



section commutator

variable {ğ•œ : Type*} [Semiring ğ•œ] {V : Type*} [AddCommGroup V] [Module ğ•œ V]

/-- Commutator `[A,B] := AB-BA` of two linear operators `A`, `B`. -/
def commutator (A B : V â†’â‚—[ğ•œ] V) : V â†’â‚—[ğ•œ] V :=
  A * B - B * A

/-- `[A,B] = -[B,A]` -/
lemma commutator_comm (A B : V â†’â‚—[ğ•œ] V) :
    A.commutator B = - B.commutator A := by
  simp [LinearMap.commutator]

lemma mul_eq_mul_add_commutator (A B : V â†’â‚—[ğ•œ] V) :
    A * B = B * A + A.commutator B := by
  simp [LinearMap.commutator]

/-- `[AB,C] = A[B,C] + [A,C]B` -/
lemma commutator_pair (A B C : V â†’â‚—[ğ•œ] V) :
    (A * B).commutator C = A * B.commutator C + A.commutator C * B := by
  calc  A * B * C - C * (A * B)
    _ = A * B * C - A * C * B + A * C * B - C * A * B     := by simp [â† mul_assoc]
    _ = A * (B * C - C * B) + (A * C - C * A) * B         := by simp [mul_sub, sub_mul, â† mul_assoc]

/-- `[A,BC] = B[A,C] + [A,B]C` -/
lemma commutator_pair' (A B C : V â†’â‚—[ğ•œ] V) :
    A.commutator (B * C) = B * A.commutator C + A.commutator B * C := by
  calc  A * (B * C) - B * C * A
    _ = A * B * C - B * A * C + B * A * C - B * C * A     := by simp [â† mul_assoc]
    _ = B * (A * C - C * A) + (A * B - B * A) * C         := by simp [mul_sub, sub_mul, â† mul_assoc]

@[simp] lemma commutator_smul_one {ğ•œ : Type*} [Field ğ•œ] (V : Type*) [AddCommGroup V] [Module ğ•œ V]
    (A : V â†’â‚—[ğ•œ] V) (c : ğ•œ) :
    A.commutator (c â€¢ 1) = 0 := by
  simp [LinearMap.commutator]

@[simp] lemma smul_one_commutator {ğ•œ : Type*} [Field ğ•œ] (V : Type*) [AddCommGroup V] [Module ğ•œ V]
    (A : V â†’â‚—[ğ•œ] V) (c : ğ•œ) :
    (c â€¢ 1 : V â†’â‚—[ğ•œ] V).commutator A = 0 := by
  simp [LinearMap.commutator]

end commutator



section commutatorBilin

variable {ğ•œ : Type*} [Field ğ•œ] (V : Type*) [AddCommGroup V] [Module ğ•œ V]

/-- Commutator `[â¬,â¬]` as a bilinear map on the space of linear maps. -/
noncomputable def commutatorBilin :
    (V â†’â‚—[ğ•œ] V) â†’â‚—[ğ•œ] (V â†’â‚—[ğ•œ] V) â†’â‚—[ğ•œ] (V â†’â‚—[ğ•œ] V) where
  toFun A :=
    { toFun := fun B â†¦ A.commutator B
      map_add' Bâ‚ Bâ‚‚ := by
        simp [LinearMap.commutator, mul_add, add_mul, sub_eq_add_neg]
        ac_rfl
      map_smul' c B := by simp [LinearMap.commutator, smul_sub] }
  map_add' Aâ‚ Aâ‚‚ := by
    ext1 B
    simp [LinearMap.commutator, add_mul, mul_add, sub_eq_add_neg]
    ac_rfl
  map_smul' c A := by
    ext1 B
    simp [LinearMap.commutator, smul_sub]

variable {V}
@[simp] lemma commutatorBilin_applyâ‚‚ (A B : V â†’â‚—[ğ•œ] V) :
    LinearMap.commutatorBilin V A B = A.commutator B := rfl

end commutatorBilin

end LinearMap -- namespace
