/-
Copyright (c) 2024 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import VirasoroProject.CentralExtension
import VirasoroProject.SectionSES

/-!
# Abstract central extensions of Lie algebras (characteristic predicate)

This file defines the short exact sequence characteristic predicate for a central extension of
a Lie algebra. It is proven that central extension defined by a 2-cocycle satisfy this
characteristic predicate.

## Main definitions

* `LieAlgebra.IsCentralExtension`: The abstract definition (characteristic predicate) of a
  central extension of a Lie algebra ğ“° by an abelian Lie algebra ğ“ª: there exists a short exact
  sequence 0 âŸ¶ ğ“ª âŸ¶ ğ“® âŸ¶ ğ“° âŸ¶ 0 of Lie algebras, where the image of ğ“ª is contained in the centre
  of ğ“®.
* `LieTwoCocycle.CentralExtension.emb`: Given a 2-cocycle Î³ âˆˆ CÂ²(ğ“°,ğ“ª) and the correspondingly
  constructed central extension ğ“®, this is the map ğ“ª âŸ¶ ğ“® in the short exact sequence.
* `LieTwoCocycle.CentralExtension.proj`: Given a 2-cocycle Î³ âˆˆ CÂ²(ğ“°,ğ“ª) and the correspondingly
  constructed central extension ğ“®, this is the map ğ“® âŸ¶ ğ“° in the short exact sequence.

## Main statements

* `LieTwoCocycle.CentralExtension.isCentralExtension`: The central extension defined by a 2-cocycle
  is a central extension in the abstract sense (it satisfies the characteristic predicate).

## Tags

Lie algebra, central extension, short exact sequence

-/

namespace VirasoroProject

section IsCentralExtension

/-! ### Lie algebra central extensions defined by short exact sequences -/

universe u
variable {ğ•œ : Type u} [CommRing ğ•œ]
variable {ğ“° ğ“ª ğ“® : Type u} [LieRing ğ“°] [LieAlgebra ğ•œ ğ“°] [LieRing ğ“ª] [LieAlgebra ğ•œ ğ“ª]
         [LieRing ğ“®] [LieAlgebra ğ•œ ğ“®]

/-- An extension `ğ“®` of a Lie algebra `ğ“°` by a Lie algebra `ğ“ª` is a short exact sequence
`0 âŸ¶ ğ“ª âŸ¶ ğ“® âŸ¶ ğ“° âŸ¶ 0`. The structure `LieAlgebra.IsExtension` bundles the maps `ğ“ª âŸ¶ ğ“®` and
`ğ“® âŸ¶ ğ“°` together with their trivial kernel and full range, respectively, and the exactness
in the middle. -/
structure LieAlgebra.IsExtension (i : ğ“ª â†’â‚—â…ğ•œâ† ğ“®) (p : ğ“® â†’â‚—â…ğ•œâ† ğ“°) : Prop where
  ker_eq_bot : i.ker = âŠ¥
  range_eq_top : p.range = âŠ¤
  exact : i.range = p.ker

/-- A central extension `ğ“®` of a Lie algebra `ğ“°` by a Lie algebra `ğ“ª` is an extension
`0 âŸ¶ ğ“ª âŸ¶ ğ“® âŸ¶ ğ“° âŸ¶ 0` where the image of `ğ“ª` is contained in the centre of `ğ“®`. -/
structure LieAlgebra.IsCentralExtension {ğ“® : Type u} [LieRing ğ“®] [LieAlgebra ğ•œ ğ“®]
    (i : ğ“ª â†’â‚—â…ğ•œâ† ğ“®) (p : ğ“® â†’â‚—â…ğ•œâ† ğ“°) extends IsExtension i p where
  central : âˆ€ (A : ğ“ª), âˆ€ (E : ğ“®), â…i A, Eâ† = 0

end IsCentralExtension

section LieTwoCocycle.CentralExtension

/-! ### Lie algebra central extensions defined by 2-cocycles -/

universe u
variable {ğ•œ : Type u} [CommRing ğ•œ]
variable {ğ“° ğ“ª : Type u} [LieRing ğ“°] [LieAlgebra ğ•œ ğ“°] [LieRing ğ“ª] [LieAlgebra ğ•œ ğ“ª]

variable (Î³ : LieTwoCocycle ğ•œ ğ“° ğ“ª)

namespace LieTwoCocycle.CentralExtension

/-- If `ğ“®` is the (central) extension of `ğ“°` by `ğ“ª` defined by a 2-cocycle `Î³ âˆˆ CÂ²(ğ“°,ğ“ª)`,
then `LieTwoCocycle.CentralExtension.emb` gives the corresponding embedding `ğ“ª âŸ¶ ğ“®`. -/
def emb [IsLieAbelian ğ“ª] : ğ“ª â†’â‚—â…ğ•œâ† Î³.CentralExtension where
  toFun := fun A â†¦ âŸ¨0, AâŸ©
  map_add' Aâ‚ Aâ‚‚ := by simp [add_def]
  map_smul' c A := by simp [smul_def]
  map_lie' := by intro Aâ‚ Aâ‚‚ ; simp [lie_def]

/-- If `ğ“®` is the (central) extension of `ğ“°` by `ğ“ª` defined by a 2-cocycle `Î³ âˆˆ CÂ²(ğ“°,ğ“ª)`,
then `LieTwoCocycle.CentralExtension.proj` gives the corresponding projection `ğ“® âŸ¶ ğ“°`. -/
def proj : Î³.CentralExtension â†’â‚—â…ğ•œâ† ğ“° where
  toFun := fun âŸ¨X, _âŸ© â†¦ X
  map_add' := by intro âŸ¨Xâ‚, Aâ‚âŸ© âŸ¨Xâ‚‚, Aâ‚‚âŸ© ; rfl
  map_smul' := by intro c âŸ¨X, AâŸ© ; rfl
  map_lie' := by intro âŸ¨Xâ‚, Aâ‚âŸ© âŸ¨Xâ‚‚, Aâ‚‚âŸ© ; rfl

lemma range_proj_eq_top :
    (LieTwoCocycle.CentralExtension.proj Î³).range = âŠ¤ :=
  (LieHom.range_eq_top (proj Î³)).mpr fun X â†¦ âŸ¨âŸ¨X, 0âŸ©, rflâŸ©

lemma ker_emb_eq_bot [IsLieAbelian ğ“ª] :
    (LieTwoCocycle.CentralExtension.emb Î³).ker = âŠ¥ :=
  (LieHom.ker_eq_bot (emb Î³)).mpr fun _ _ hA â†¦ congr_arg (fun Z â†¦ Z.2) hA

lemma mem_range_emb_iff [IsLieAbelian ğ“ª] (Z : Î³.CentralExtension) :
    Z âˆˆ (LieTwoCocycle.CentralExtension.emb Î³).range â†” Z.1 = 0 := by
  rw [LieHom.mem_range]
  refine âŸ¨?_, ?_âŸ©
  Â· intro âŸ¨A, hAâŸ©
    simp [â† hA, emb]
  Â· intro h
    use Z.2
    simp only [emb, LieHom.coe_mk]
    ext <;> simp_all

lemma mem_ker_proj_iff (Z : Î³.CentralExtension) :
    Z âˆˆ (LieTwoCocycle.CentralExtension.proj Î³).ker â†” Z.1 = 0 := by
  rw [LieHom.mem_ker]
  refine âŸ¨?_, ?_âŸ©
  Â· intro h ; simpa [proj]
  Â· intro h ; simpa only [proj, LieHom.coe_mk] using h

lemma range_emb_eq_ker_proj [IsLieAbelian ğ“ª] :
    (LieTwoCocycle.CentralExtension.emb Î³).range = (LieTwoCocycle.CentralExtension.proj Î³).ker := by
  ext Z
  change Z âˆˆ (LieTwoCocycle.CentralExtension.emb Î³).range
        â†” Z âˆˆ (LieTwoCocycle.CentralExtension.proj Î³).ker
  rw [mem_range_emb_iff, mem_ker_proj_iff]

/-- If `ğ“®` is the (central) extension of `ğ“°` by `ğ“ª` defined by a 2-cocycle `Î³ âˆˆ CÂ²(ğ“°,ğ“ª)`,
then `ğ“®` is an extension of `ğ“°` by `ğ“ª` in the sense that there is a short exact sequence
`0 âŸ¶ ğ“ª âŸ¶ ğ“® âŸ¶ ğ“° âŸ¶ 0` where the two maps are `LieTwoCocycle.CentralExtension.emb` and
`LieTwoCocycle.CentralExtension.proj`. -/
instance isExtension [IsLieAbelian ğ“ª] :
    LieAlgebra.IsExtension (emb Î³) (proj Î³) where
  ker_eq_bot := ker_emb_eq_bot Î³
  range_eq_top := range_proj_eq_top Î³
  exact := range_emb_eq_ker_proj Î³

/-- If `ğ“®` is the central extension of `ğ“°` by `ğ“ª` defined by a 2-cocycle `Î³ âˆˆ CÂ²(ğ“°,ğ“ª)`,
then `ğ“®` is a central extension of `ğ“°` by `ğ“ª` in the sense that there is a short exact sequence
`0 âŸ¶ ğ“ª âŸ¶ ğ“® âŸ¶ ğ“° âŸ¶ 0` where the two maps are `LieTwoCocycle.CentralExtension.emb` and
`LieTwoCocycle.CentralExtension.proj` and the image of `ğ“ª` is contained in the centre of `ğ“®`. -/
instance isCentralExtension [IsLieAbelian ğ“ª] (Î³ : LieTwoCocycle ğ•œ ğ“° ğ“ª) :
    LieAlgebra.IsCentralExtension (emb Î³) (proj Î³) where
  __ := LieTwoCocycle.CentralExtension.isExtension Î³
  central := by
    intro A Z
    simp only [emb, LieHom.coe_mk, lie_def, zero_lie, map_zero, LinearMap.zero_apply]
    rfl

/-- A standard section of a Lie algebra central extension associated to a Lie 2-cocycle. -/
noncomputable def stdSection (Î³ : LieTwoCocycle ğ•œ ğ“° ğ“ª) :
    ğ“° â†’â‚—[ğ•œ] Î³.CentralExtension where
  toFun X := âŸ¨X, 0âŸ©
  map_add' Xâ‚ Xâ‚‚ := by rw [LieTwoCocycle.CentralExtension.add_def] ; simp
  map_smul' c X := by rw [LieTwoCocycle.CentralExtension.smul_def] ; simp

lemma stdSection_prop (Î³ : LieTwoCocycle ğ•œ ğ“° ğ“ª) :
    proj Î³ âˆ˜â‚— stdSection Î³ = (1 : ğ“° â†’â‚—[ğ•œ] ğ“°) :=
  rfl

end LieTwoCocycle.CentralExtension --namespace

end LieTwoCocycle.CentralExtension -- section


section Basis

namespace LieAlgebra.IsExtension

open Module

universe u u'
variable {ğ•œ : Type u} [CommRing ğ•œ]
variable {ğ“° ğ“ª ğ“® : Type u} [LieRing ğ“°] [LieAlgebra ğ•œ ğ“°] [LieRing ğ“ª] [LieAlgebra ğ•œ ğ“ª]
         [LieRing ğ“®] [LieAlgebra ğ•œ ğ“®]
variable {i : ğ“ª â†’â‚—â…ğ•œâ† ğ“®} {p : ğ“® â†’â‚—â…ğ•œâ† ğ“°} (ex : LieAlgebra.IsExtension i p)
variable (Ïƒ : ğ“° â†’â‚—[ğ•œ] ğ“®) (hÏƒ : p.toLinearMap âˆ˜â‚— Ïƒ = 1)

/-- A basis of a central extension of Lie algebras constructed from a section and bases of the
extending Lie algebras. -/
noncomputable def basis {Î¹A Î¹G  : Type u'} (basA : Basis Î¹A ğ•œ ğ“ª) (basG : Basis Î¹G ğ•œ ğ“°) :
    Basis (Î¹A âŠ• Î¹G) ğ•œ ğ“® :=
  ses_basis basA basG (LieSubmodule.mk_eq_bot_iff.mp ex.ker_eq_bot)
    (congr_arg LieSubalgebra.toSubmodule ex.exact) hÏƒ

@[simp] lemma basis_eq_of_left {Î¹A Î¹G  : Type u'} (basA : Basis Î¹A ğ•œ ğ“ª) (basG : Basis Î¹G ğ•œ ğ“°)
    (ia : Î¹A) :
    basis ex Ïƒ hÏƒ basA basG (Sum.inl ia) = i (basA ia) := by
  simp [basis]

@[simp] lemma basis_eq_of_right {Î¹A Î¹G  : Type u'} (basA : Basis Î¹A ğ•œ ğ“ª) (basG : Basis Î¹G ğ•œ ğ“°)
    (ig : Î¹G):
    basis ex Ïƒ hÏƒ basA basG (Sum.inr ig) = Ïƒ (basG ig) := by
  simp [basis]

end LieAlgebra.IsExtension

end Basis

end VirasoroProject -- namespace
