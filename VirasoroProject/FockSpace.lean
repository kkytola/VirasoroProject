/-
Copyright (c) 2025 Kalle Kytölä. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kytölä
-/
import VirasoroProject.HeisenbergAlgebra
import VirasoroProject.IndexTri
import VirasoroProject.LieVerma

/-!
# Verma modules for the Virasoro algebra

This file defines Verma modules used in Lie algebra representation theory.

## Main definitions

* `HasCentralCharge`: A typeclass for Vierasoro modules where the central element acts by
  multiplication by a particular scalar (the scalar is called the *central charge*).
* `heisenbergTri`: The triangular decomposition of the Heisenberg algebra whose positive (resp.
  negative) part is spanned by `Jₖ` with `k` positive (resp. negative) and the Cartan subalgebra
  is spanned by `J₀` and the central element `K`.
* `HeisenbergAlgebra.hw α`: The (highest) weight of the Heisenberg algebra specified by the
  value `α` of the charge (`J₀`-eigenvalue); the central element `K` is by convention normalized to
  have value `1`.
* `ChargedFockSpace α`: The charged Fock space representation of the Heisenberg algebra with
  charge (`J₀`-eigenvalue) `α`.
* `ChargedFockSpace.vacuum`: The vacuum vector (highest weight vector) of the charged Fock space.
* `VirasoroVerma.universalMap`: The universal property of the Virasoro Verma module:
  Given another module `M` over `𝓤 𝕜 (VirasoroAlgebra 𝕜)` and a "target" vector `hwv ∈ M` satisfying
  `Lₙ • hwv = 0` for all `n > 0`, `L₀ • hwv = h • hwv`, and `C • hwv = c • hwv`,
  one obtains an `A`-module map from the Verma module to `M` uniquely specified by the requirement
  `hwVec c h ↦ hwv`.

## Main statements

* `ChargedFockSpace.vacuum_cyclic`: The charged Fock space is generated by its vacuum vector.
* `TriangularDecomposition.VermaHW.cartan_smul_hwVec`: The highest weight vector of a Verma module
  is an eigenvector of the Cartan subalgebra with the eigenvalues specified by the highest weight.
* `ChargedFockSpace.jgen_pos_vacuum`: The highest weight vector of a Virasoro Verma module
  is annihilated `Jₖ` for `k > 0`.
* `ChargedFockSpace.jgen_zero_smul`: The Heisenberg generator `J₀` acts as scalar `α` (the charge)
  on the charged Fock space.
* `ChargedFockSpace.kgen_smul`: The central element `K` acts as scalar `1` in the charged Fock
  space.
* `ChargedFockSpace.eventually_jgen_smul_eq_zero`: The charged Fock space satisfies the local
  truncation condition (which is needed, e.g., for the Sugawara construction).

## Implementation notes

The charged Fock space is defined as a Verma module for the Heisenberg Lie algebra (with an
appropriate triangular decomposition).

## Tags

Heisenberg algebra, Fock space

-/



namespace VirasoroProject

open Module

section HasCharge

variable (𝕜 : Type*) [Field 𝕜] [CharZero 𝕜]
variable (M : Type*) [AddCommGroup M] [Module 𝕜 M] [Module (𝓤 𝕜 (HeisenbergAlgebra 𝕜)) M]

abbrev HasUnitCentral := HasCentralValue 𝕜 (HeisenbergAlgebra 𝕜) M (.kgen _) 1

@[simp] lemma HasUnitCentral.kgen_smul [HasUnitCentral 𝕜 M] (v : M) :
    ιUEA 𝕜 (HeisenbergAlgebra.kgen 𝕜) • v = v := by
  nth_rw 2 [show v = (1 : 𝕜) • v by rw [one_smul]]
  exact HasCentralValue.central_smul ..

abbrev HasCharge (α : 𝕜) := HasCentralValue 𝕜 (HeisenbergAlgebra 𝕜) M (.jgen _ 0) α

@[simp] lemma HasCharge.jgen_zero_smul {α : 𝕜} [HasCharge 𝕜 M α] (v : M) :
    ιUEA 𝕜 (HeisenbergAlgebra.jgen 𝕜 0) • v = α • v :=
  HasCentralValue.central_smul ..

end HasCharge

section ChargedFockSpace

variable (𝕜 : Type*) [Field 𝕜] [CharZero 𝕜]

open HeisenbergAlgebra in
/-- The triangular decomposition of the Heisenberg algebra with upper and lower (essentially
nilpotent) parts spanned by the `Jₖ` with positive and negative `k`, respectively, and the
Cartan subalgebra spanned by `J₀` and the central element `K`. -/
noncomputable def heisenbergTri :
    TriangularDecomposition 𝕜 (HeisenbergAlgebra 𝕜) :=
  TriangularDecomposition.ofBasis (basisJK 𝕜) indexTri pairwise_disjoint_indexTri iUnion_indexTri

open HeisenbergAlgebra TriangularDecomposition in
lemma heisenbergTri_cartan :
    (heisenbergTri 𝕜).cartan = Submodule.span 𝕜 {.kgen 𝕜, .jgen 𝕜 0} := by
  simp only [heisenbergTri, indexTri, ofBasis, cartan]
  congr 1
  aesop

open HeisenbergAlgebra TriangularDecomposition in
lemma heisenbergTri_upper :
    (heisenbergTri 𝕜).upper = Submodule.span 𝕜 ((fun n ↦ jgen 𝕜 n) '' {n | 0 < n}) := by
  simp only [heisenbergTri, indexTri, ofBasis, upper]
  congr 1
  aesop

open HeisenbergAlgebra TriangularDecomposition in
lemma heisenbergTri_lower :
    (heisenbergTri 𝕜).lower = Submodule.span 𝕜 ((fun n ↦ jgen 𝕜 n) '' {n | n < 0}) := by
  simp only [heisenbergTri, indexTri, ofBasis, lower]
  congr 1
  aesop

open HeisenbergAlgebra in
noncomputable def heisenbergTri_cartan_basis :
    Basis ({none, some 0} : Set (Option ℤ)) 𝕜 (heisenbergTri 𝕜).cartan :=
  TriangularDecomposition.ofBasis.basis_part (HeisenbergAlgebra.basisJK 𝕜) indexTri
    pairwise_disjoint_indexTri iUnion_indexTri 0

/-- The highest weight of Heisenberg algebra determined by charge `α` (`J₀`-eigenvalue).
The `K`-eigenvalue is by convention normalized to `1` by default. -/
noncomputable def HeisenbergAlgebra.hw (α : 𝕜) :
    (heisenbergTri 𝕜).cartan →ₗ[𝕜] 𝕜 :=
  (heisenbergTri_cartan_basis 𝕜).constr (M' := 𝕜) 𝕜 (fun i ↦ if i.val = none then 1 else α)

/-- The Heisenberg generator `K` as an element of the Cartan subalgebra. -/
noncomputable def heisenbergTri_kgen : (heisenbergTri 𝕜).part 0 :=
  ⟨.kgen 𝕜, Submodule.mem_span_of_mem (by simp [indexTri])⟩

/-- The Heisenberg generator `J₀` as an element of the Cartan subalgebra. -/
noncomputable def heisenbergTri_jzero : (heisenbergTri 𝕜).part 0 :=
  ⟨.jgen 𝕜 0, Submodule.mem_span_of_mem (by simp [indexTri])⟩

@[simp] lemma heisenbergTri_kgen_val : (heisenbergTri_kgen 𝕜).val = .kgen 𝕜 := rfl
@[simp] lemma heisenbergTri_jzero_val : (heisenbergTri_jzero 𝕜).val = .jgen 𝕜 0 := rfl

open HeisenbergAlgebra in
lemma heisenbergTri_cartan_basis_none_eq_kgen :
    (heisenbergTri_cartan_basis 𝕜) ⟨none, Set.mem_insert none {some 0}⟩ = heisenbergTri_kgen 𝕜 := by
  ext
  simp only [heisenbergTri_cartan_basis, TriangularDecomposition.ofBasis.basis_part, indexTri,
             heisenbergTri_kgen_val]
  convert (basisJK 𝕜).basis_submodule_span_apply {none, some 0} ⟨none, Set.mem_insert none {some 0}⟩
  simp

open HeisenbergAlgebra in
lemma heisenbergTri_cartan_basis_some_eq_jzero :
    (heisenbergTri_cartan_basis 𝕜) ⟨some 0, by exact Set.mem_insert_of_mem none rfl⟩
      = heisenbergTri_jzero 𝕜 := by
  ext
  simp only [heisenbergTri_cartan_basis, TriangularDecomposition.ofBasis.basis_part, indexTri,
             heisenbergTri_jzero_val]
  convert (basisJK 𝕜).basis_submodule_span_apply {none, some 0}
          ⟨some 0, Set.mem_insert_of_mem none rfl⟩
  simp

lemma heisenbergTri_kgen_mem_cartan :
    .kgen 𝕜 ∈ (heisenbergTri 𝕜).cartan := by
  simpa only [heisenbergTri_cartan] using Submodule.mem_span_of_mem (by simp)

lemma heisenbergTri_jgen_zero_mem_cartan :
    .jgen 𝕜 0 ∈ (heisenbergTri 𝕜).cartan := by
  simpa only [heisenbergTri_cartan] using Submodule.mem_span_of_mem (by simp)

lemma heisenbergTri_jgen_pos_mem_upper {n : ℤ} (n_pos : 0 < n) :
    .jgen 𝕜 n ∈ (heisenbergTri 𝕜).upper := by
  simp only [heisenbergTri_upper]
  apply Submodule.mem_span_of_mem
  exact (Set.mem_image ..).mpr ⟨n, ⟨n_pos, rfl⟩⟩

lemma HeisenbergAlgebra.hw_apply_kgen (α : 𝕜) :
    hw 𝕜 α (heisenbergTri_kgen 𝕜) = 1 := by
  rw [← heisenbergTri_cartan_basis_none_eq_kgen]
  simp only [hw, Basis.constr_apply_fintype]
  simp only [Basis.equivFun_self, smul_eq_mul, mul_ite, ite_mul, one_mul, zero_mul]
  rw [Finset.sum_eq_single ⟨none, Set.mem_insert none {some 0}⟩]
  · simp
  · intro j _ hj
    simp [hj.symm, show ¬ (j : Option ℤ) = none by aesop]
  · simp

lemma HeisenbergAlgebra.hw_apply_jzero (α : 𝕜) :
    hw 𝕜 α (heisenbergTri_jzero 𝕜) = α := by
  rw [← heisenbergTri_cartan_basis_some_eq_jzero]
  simp only [hw, Basis.constr_apply_fintype]
  simp only [Basis.equivFun_self, smul_eq_mul, mul_ite, ite_mul, one_mul, zero_mul]
  rw [Finset.sum_eq_single ⟨some 0, by exact Set.mem_insert_of_mem none rfl⟩]
  · simp
  · intro j _ hj
    simp [hj.symm]
  · simp

/-- The charged Fock space with charge `α`. -/
abbrev ChargedFockSpace (α : 𝕜) := (heisenbergTri 𝕜).VermaHW (HeisenbergAlgebra.hw 𝕜 α)

/-- The vacuum vector in the charged Fock space with charge `α`. -/
noncomputable abbrev ChargedFockSpace.vacuum (α : 𝕜) :=
   TriangularDecomposition.VermaHW.hwVec (HeisenbergAlgebra.hw 𝕜 α)

lemma ChargedFockSpace.vacuum_cyclic (α : 𝕜) :
    Submodule.span (𝓤 𝕜 (HeisenbergAlgebra 𝕜)) {ChargedFockSpace.vacuum 𝕜 α} = ⊤ :=
  VermaModule.hwVec_cyclic ..

/-- `Jₖ • vacuum(α) = 0` for `k > 0` -/
lemma ChargedFockSpace.jgen_pos_vacuum (α : 𝕜) {k : ℤ} (k_pos : 0 < k) :
    ιUEA 𝕜 (HeisenbergAlgebra.jgen 𝕜 k) • vacuum 𝕜 α = 0 :=
  TriangularDecomposition.VermaHW.upper_smul_hwVec
    (HeisenbergAlgebra.hw 𝕜 α) (E := .jgen 𝕜 k) (heisenbergTri_jgen_pos_mem_upper 𝕜 k_pos)

lemma ChargedFockSpace.jgen_zero_vacuum (α : 𝕜) :
    ιUEA 𝕜 (HeisenbergAlgebra.jgen 𝕜 0) • vacuum 𝕜 α = α • vacuum 𝕜 α := by
  convert TriangularDecomposition.VermaHW.cartan_smul_hwVec
          (HeisenbergAlgebra.hw 𝕜 α) (H := .jgen 𝕜 0) (heisenbergTri_jgen_zero_mem_cartan 𝕜)
  exact (HeisenbergAlgebra.hw_apply_jzero 𝕜 α).symm

lemma ChargedFockSpace.kgen_vacuum (α : 𝕜) :
    ιUEA 𝕜 (HeisenbergAlgebra.kgen 𝕜) • vacuum 𝕜 α = vacuum 𝕜 α := by
  have key := TriangularDecomposition.VermaHW.cartan_smul_hwVec
          (HeisenbergAlgebra.hw 𝕜 α) (H := .kgen 𝕜) (heisenbergTri_kgen_mem_cartan 𝕜)
  rw [← heisenbergTri_kgen, HeisenbergAlgebra.hw_apply_kgen, one_smul] at key
  convert key

/-- `J₀ • v = α • v` for all `v` in `ChargedFockSpace 𝕜 α` -/
lemma ChargedFockSpace.kgen_smul (α : 𝕜) (v : ChargedFockSpace 𝕜 α) :
    ιUEA 𝕜 (HeisenbergAlgebra.kgen 𝕜) • v = v := by
  have aux : ιUEA 𝕜 (HeisenbergAlgebra.kgen 𝕜) • vacuum 𝕜 α = (1 : 𝕜) • vacuum 𝕜 α := by
    rw [kgen_vacuum 𝕜 α, one_smul]
  simpa using UniversalEnvelopingAlgebra.smul_eq_of_cyclic_of_forall_lie_eq_zero
    _ _ (fun _ ↦ rfl) (vacuum_cyclic 𝕜 α) aux v

/-- `J₀ • v = α • v` for all `v` in `ChargedFockSpace 𝕜 α` -/
@[simp] lemma ChargedFockSpace.jgen_zero_smul (α : 𝕜) (v : ChargedFockSpace 𝕜 α) :
    (ιUEA 𝕜 (HeisenbergAlgebra.jgen 𝕜 0)) • v = α • v := by
  exact UniversalEnvelopingAlgebra.smul_eq_of_cyclic_of_forall_lie_eq_zero 𝕜
    (HeisenbergAlgebra 𝕜) (Z := .jgen 𝕜 0) (ζ := α) (HeisenbergAlgebra.lie_jgen_zero _)
    (vacuum_cyclic 𝕜 α) (jgen_zero_vacuum 𝕜 α) v

/-- The charged Fock space with charge `α` has `K` action by unit scalar. -/
instance (α : 𝕜) : HasUnitCentral 𝕜 (ChargedFockSpace 𝕜 α) where
  central_smul' v := by rw [ChargedFockSpace.kgen_smul 𝕜 α v, one_smul]

/-- The charged Fock space with charge `α` has charge `α`. -/
instance (α : 𝕜) : HasCharge 𝕜 (ChargedFockSpace 𝕜 α) α :=
  ⟨fun v ↦ ChargedFockSpace.jgen_zero_smul 𝕜 α v⟩

open Filter in
lemma HeisenbergAlgebra.uea_eventually_commute_jgen (a : 𝓤 𝕜 (HeisenbergAlgebra 𝕜)) :
    ∀ᶠ k in atTop, Commute (ιUEA 𝕜 (jgen 𝕜 k)) a := by
  apply UniversalEnvelopingAlgebra.induction 𝕜 _
    (C := fun a' ↦ ∀ᶠ k in atTop, Commute (ιUEA 𝕜 (jgen 𝕜 k)) a')
  · intro r
    filter_upwards [Ioi_mem_atTop 0] with k k_pos
    exact (Algebra.commutes r _).symm
  · let whole := Submodule.span 𝕜 (Set.range (basisJK 𝕜))
    suffices ∀ X ∈ whole, ∀ᶠ k in atTop,
        Commute (ιUEA 𝕜 (jgen 𝕜 k)) (ιUEA 𝕜 X) from
      fun X ↦ this X (by simp [whole])
    apply Submodule.span_induction
    · intro Z ⟨i, hiZ⟩
      match i with
      | none =>
        simp only [← hiZ, basisJK_none]
        filter_upwards [Ioi_mem_atTop 0] with k k_pos
        exact (UniversalEnvelopingAlgebra.central_of_forall_lie_eq_zero 𝕜 (HeisenbergAlgebra 𝕜)
                (congrFun rfl) ((ιUEA 𝕜) (jgen 𝕜 k))).symm
      | some l =>
        simp only [← hiZ, basisJK_some]
        filter_upwards [Ioi_mem_atTop |l|] with k hk
        rw [commute_iff_lie_eq, ← LieHom.map_lie]
        have obs : ¬ k + l = 0 := by
          simp only [Set.mem_Ioi, abs_lt] at hk
          grind
        simp [lie_jgen, obs]
    · simp
    · intro a b a_mem b_mem ha hb
      filter_upwards [ha, hb] with k hka hkb
      simpa only [LieHom.map_add] using Commute.add_right hka hkb
    · intro r a a_mem ha
      filter_upwards [ha] with k hka
      simpa only [LieHom.map_smul] using Commute.smul_right hka _
  · intro a b ha hb
    filter_upwards [ha, hb] with k hka hkb using Commute.mul_right hka hkb
  · intro a b ha hb
    filter_upwards [ha, hb] with k hka hkb using Commute.add_right hka hkb

open Filter HeisenbergAlgebra in
lemma ChargedFockSpace.eventually_jgen_smul_eq_zero (α : 𝕜) (v : ChargedFockSpace 𝕜 α) :
    ∀ᶠ k in atTop, (ιUEA 𝕜 (jgen 𝕜 k)) • v = 0 := by
  have aux : v ∈ Submodule.span (𝓤 𝕜 (HeisenbergAlgebra 𝕜)) {vacuum 𝕜 α} := by
    simp [vacuum_cyclic 𝕜 α]
  obtain ⟨a, hav⟩ := Submodule.mem_span_singleton.mp aux
  filter_upwards [uea_eventually_commute_jgen _ a, Ioi_mem_atTop 0] with k hk k_pos
  -- `calcify`?
  rw [← hav, ← mul_smul]
  rw [show _ * a = a * _ from hk]
  rw [mul_smul]
  rw [jgen_pos_vacuum _ _ k_pos, smul_zero]

end ChargedFockSpace

end VirasoroProject
