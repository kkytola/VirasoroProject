/-
Copyright (c) 2024 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.LinearAlgebra.Basis.Bilinear
import VirasoroProject.CyclicTripleSum
import VirasoroProject.ToMathlib.LinearAlgebra.Basis.Defs

/-!
# Witt algebra

This file defines the Witt algebra, an infinite-dimensional Lie algebra.
A few assumptions are made of the ground ring `ğ•œ`, mainly that it is of characteristic zero.

Typical interpretations of the Witt algebra are the following:
 * In the case that the ground field is the real numbers, `ğ•œ = â„`, the Witt algebra is the Lie
   algebra of polynomial vector fields on the circle.
 * In the case that the ground field is the complex numbers, `ğ•œ = â„‚`, the Witt algebra is the
   Lie algebra of meromorphic vector fields on the Riemann sphere with poles only at 0 and âˆ.

## Main definitions

* ...
* ...

## Main statements

* ...

* ...

## Implementation notes

We define the Witt algebra based on an explicit basis indexed by the integers `â„¤`. This should be
the most basic implementation.

TODO: Prove that the Witt algebra is isomorphic, e.g., to the Lie algebra of meromorphic vector
fields on the Riemann sphere with poles only at 0 and âˆ. (This of course needs some amount of
differential geometry to be added to Lean.)

## Tags

Witt algebra

-/

namespace VirasoroProject

variable (ğ•œ : Type*) [CommRing ğ•œ]

/-- The Witt algebra: an âˆ-dimensional Lie algebra (polynomial vector fields on a circle). -/
def WittAlgebra := â„¤ â†’â‚€ ğ•œ

noncomputable instance : AddCommGroup (WittAlgebra ğ•œ) := Finsupp.instAddCommGroup

noncomputable instance : Module ğ•œ (WittAlgebra ğ•œ) := Finsupp.module ..

namespace WittAlgebra

/-- The basis of `â„“â‚™` generators of the Witt algebra (indices `n : â„¤`). -/
def lgen : Basis â„¤ ğ•œ (WittAlgebra ğ•œ) := Finsupp.basisFun _ _

lemma lgen_eq_single (n : â„¤) : lgen ğ•œ n = Finsupp.single n 1 := rfl

/-- The Lie bracket for the Witt algebra `WittAlgebra` as a bilinear map. -/
noncomputable def bracket :
    (WittAlgebra ğ•œ) â†’â‚—[ğ•œ] (WittAlgebra ğ•œ) â†’â‚—[ğ•œ] (WittAlgebra ğ•œ) :=
  (lgen ğ•œ).constr ğ•œ <| fun n â†¦ (lgen ğ•œ).constr ğ•œ <| fun m â†¦ (n - m : ğ•œ) â€¢ lgen ğ•œ (n + m)

/-- `â…â„“(n), â„“(m)â† = (n-m) â€¢ â„“(n+m)` in `WittAlgebra`. -/
@[simp]
lemma bracket_lgen_lgen' (n m : â„¤) :
    bracket ğ•œ (lgen ğ•œ n) (lgen ğ•œ m) = (n - m : ğ•œ) â€¢ lgen ğ•œ (n + m) := by
  simp only [bracket, Basis.constr_basis]

lemma bracket_eq_neg_flip :
    bracket ğ•œ = -(bracket ğ•œ).flip := by
  apply LinearMap.ext_basis (lgen _) (lgen _)
  intro n m
  simp [add_comm m, â† neg_smul, neg_sub]

variable {ğ•œ}

/-- Antisymmetry of the Lie bracket of the Witt algebra `WittAlgebra`. -/
lemma bracket_antisymm (X Y : WittAlgebra ğ•œ) :
    bracket ğ•œ X Y = - bracket ğ•œ Y X := by
  simpa using LinearMap.congr_fun (LinearMap.congr_fun (bracket_eq_neg_flip ğ•œ) X) Y

/-- Antisymmetry (`â…X, Xâ† = 0` form) of the Lie bracket of the Witt algebra `WittAlgebra`. -/
lemma bracket_self [CharZero ğ•œ] [NoZeroSMulDivisors ğ•œ (WittAlgebra ğ•œ)] (X : WittAlgebra ğ•œ) :
    bracket ğ•œ X X = 0 := by
  have aux : (2 : ğ•œ) â€¢ bracket ğ•œ X X = 0 := by
    have obs := congr_arg (Â· + bracket ğ•œ X X) (bracket_antisymm X X)
    simp only [neg_add_cancel] at obs
    rwa [â† one_smul ğ•œ (bracket ğ•œ X X), â† add_smul, one_add_one_eq_two] at obs
  simpa only [OfNat.ofNat_ne_zero, false_or] using eq_zero_or_eq_zero_of_smul_eq_zero aux

variable (ğ•œ)

/-- The Jacobi identity of the Lie bracket of the Witt algebra `WittAlgebra`. -/
lemma bracketCyclic_eq_zero :
    cyclicTripleSumHom (bracket ğ•œ) (bracket ğ•œ) = 0 := by
  apply LinearMap.ext_basis (lgen _) (lgen _)
  intro n m
  apply (lgen _).ext
  intro k
  simp only [cyclicTripleSumHom_apply, LinearMap.coe_mk, AddHom.coe_mk, bracket_lgen_lgen',
             LinearMapClass.map_smul, Int.cast_add, LinearMap.zero_apply, smul_smul]
  rw [show n + (m + k) = n + m + k by ring,
      show m + (k + n) = n + m + k by ring,
      show k + (n + m) = n + m + k by ring]
  simp only [â† add_smul]
  convert zero_smul ğ•œ ((lgen ğ•œ) (n + m + k))
  ring

variable {ğ•œ}

/-- The Leibniz property (Jacobi identity) of the Lie bracket of the Witt algebra `WittAlgebra`. -/
lemma bracket_leibniz (X Y Z : WittAlgebra ğ•œ) :
    bracket ğ•œ X (bracket ğ•œ Y Z) =
      bracket ğ•œ (bracket ğ•œ X Y) Z + bracket ğ•œ Y (bracket ğ•œ X Z) := by
  have key := LinearMap.congr_fun (LinearMap.congr_fun (LinearMap.congr_fun
                (bracketCyclic_eq_zero ğ•œ) X) Y) Z
  simp only [cyclicTripleSumHom_apply, LinearMap.coe_mk, AddHom.coe_mk, LinearMap.zero_apply] at key
  rw [add_assoc (bracket ğ•œ X _)] at key
  rw [eq_neg_of_add_eq_zero_left key]
  rw [bracket_antisymm Z X, bracket_antisymm Z _]
  simp

variable [CharZero ğ•œ] [NoZeroSMulDivisors ğ•œ (WittAlgebra ğ•œ)]

/-- The Lie ring structure on the Witt algebra `WittAlgebra`. -/
noncomputable instance : LieRing (WittAlgebra ğ•œ) where
  bracket X Y := bracket _ X Y
  add_lie Xâ‚ Xâ‚‚ Y := by simp only [bracket, map_add, LinearMap.add_apply]
  lie_add X Yâ‚ Yâ‚‚ := by simp only [map_add]
  lie_self X := bracket_self X
  leibniz_lie X Y Z := bracket_leibniz X Y Z

/-- The Lie algebra structure on the Witt algebra `WittAlgebra`. -/
noncomputable instance : LieAlgebra ğ•œ (WittAlgebra ğ•œ) where
  lie_smul c X Y := map_smul (bracket ğ•œ X) c Y

variable (ğ•œ)

/-- `â…â„“(n), â„“(m)â† = (n-m) â€¢ â„“(n+m)` in `WittAlgebra`. -/
@[simp]
lemma bracket_lgen_lgen (n m : â„¤) :
    â…lgen ğ•œ n, lgen ğ•œ mâ† = (n - m : ğ•œ) â€¢ lgen ğ•œ (n + m) :=
  bracket_lgen_lgen' ğ•œ n m

end WittAlgebra -- namespace

end VirasoroProject -- namespace
