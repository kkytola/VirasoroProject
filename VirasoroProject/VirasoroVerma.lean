/-
Copyright (c) 2025 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib
import VirasoroProject.VermaModule
import VirasoroProject.VirasoroAlgebra



namespace VirasoroProject



section

variable (ğ•œ : Type*) [CommRing ğ•œ]
variable (ğ“° : Type*) [LieRing ğ“°] [LieAlgebra ğ•œ ğ“°]

structure TriangularDecomposition where
  --subalg : SignType â†’ LieSubalgebra ğ•œ ğ“°
  --directSum : DirectSum.IsInternal subalg
  --commutative : âˆ€ (Hâ‚ Hâ‚‚ : subalg 0), â…Hâ‚, Hâ‚‚â† = 0
  --nilpotent : ...
  part : SignType â†’ Submodule ğ•œ ğ“°
  directSum : DirectSum.IsInternal part

namespace TriangularDecomposition

-- TODO: To Mathlib...
lemma finsum_mem_span {Î¹ R V : Type*} [Semiring R] [AddCommMonoid V] [Module R V]
    (vs : Î¹ â†’ V) (cfs : Î¹ â†’ R) :
    âˆ‘á¶  i, cfs i â€¢ vs i âˆˆ Submodule.span R (Set.range vs) := by
  by_cases h : {i | cfs i â€¢ vs i â‰  0}.Finite
  Â· let s : Finset Î¹ := h.toFinset
    rw [finsum_eq_finset_sum_of_support_subset (s := s) _ (fun i hi â†¦ by simpa [s] using hi)]
    apply Submodule.sum_smul_mem
    exact fun i his â†¦ Submodule.mem_span_of_mem (Set.mem_range_self i)
  Â· suffices junk : âˆ‘á¶  i, cfs i â€¢ vs i = 0 by simp [junk]
    simpa using finsum_mem_eq_zero_of_infinite (s := Set.univ) (by simpa using h)

-- TODO: To Mathlib... (Golf first, though...)
lemma finsum_mem_mem_span {Î¹ R V : Type*}
    [Semiring R] [AddCommMonoid V] [Module R V]
    (vs : Î¹ â†’ V) (cfs : Î¹ â†’ R) (s : Set Î¹) :
    âˆ‘á¶  i âˆˆ s, cfs i â€¢ vs i âˆˆ Submodule.span R (vs '' s) := by
  by_cases h : {i | cfs i â€¢ vs i â‰  0 âˆ§ i âˆˆ s}.Finite
  Â· let t : Finset Î¹ := h.toFinset
    rw [finsum_eq_finset_sum_of_support_subset (s := t) _ ?_]
    Â· classical
      have aux : âˆ‘ i âˆˆ t, âˆ‘á¶  (_ : i âˆˆ s), cfs i â€¢ vs i = âˆ‘ i âˆˆ t.filter s, cfs i â€¢ vs i  := by
        rw [Finset.sum_filter]
        congr 1
        funext i
        by_cases his : i âˆˆ s <;>
        Â· simpa [his] using fun con â†¦ by contradiction
      rw [aux]
      apply Submodule.sum_smul_mem
      intro i hist
      apply Submodule.mem_span_of_mem <| Set.mem_image_of_mem ..
      simp_all only [ne_eq, Finset.mem_filter, Set.Finite.mem_toFinset, Set.mem_setOf_eq, t]
    Â· intro i hi
      simp only [ne_eq, Set.Finite.coe_toFinset, Set.mem_setOf_eq, t]
      refine âŸ¨?_, ?_âŸ© <;>
      Â· by_contra con
        simp [con] at hi
  Â· suffices junk : âˆ‘á¶  i âˆˆ s, cfs i â€¢ vs i = 0 by simp [junk]
    exact finsum_mem_eq_zero_of_infinite (by simpa [and_comm] using h)

-- TODO: To Mathlib.
lemma _root_.Basis.finsum_repr_smul_basis {R M Î¹ : Type*} [Semiring R] [Nontrivial R]
    [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M] (B : Basis Î¹ R M) (v : M) :
    âˆ‘á¶  i, B.repr v i â€¢ B i = v := by
  have obs : (Function.support fun i â†¦ B.repr v i â€¢ B i).Finite := by
    apply (Finsupp.finite_support (B.repr v)).subset
    intro i hi
    simp only [Function.support, ne_eq, smul_eq_zero, not_or, Set.mem_setOf_eq] at hi âŠ¢
    exact hi.1
  rw [finsum_eq_sum _ obs]
  apply B.repr.injective
  rw [map_sum]
  simp only [map_smul, Basis.repr_self, Finsupp.smul_single, smul_eq_mul, mul_one]
  ext i
  simp only [Finsupp.coe_finset_sum, Finset.sum_apply]
  rw [Finset.sum_eq_single i]
  Â· simp
  Â· intro j _ j_ne_i
    simp [j_ne_i]
  Â· simp only [Set.Finite.mem_toFinset, Function.mem_support, ne_eq, smul_eq_zero, not_or, not_and,
               not_not, Finsupp.single_eq_same]
    intro hi
    rw [â† not_imp_not, not_not] at hi
    exact hi (B.ne_zero i)

-- TODO: To Mathlib.
lemma _root_.Basis.repr_finsum {R M Î¹ : Type*} [Semiring R] [Nontrivial R]
    [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M] (B : Basis Î¹ R M) (cf : Î¹ â†’â‚€ R) :
    B.repr (âˆ‘á¶  i, cf i â€¢ B i) = cf := by
  convert show B.repr (B.repr.symm cf) = cf by simp
  have aux_finite : (Function.support fun i â†¦ cf i â€¢ B i).Finite := by
    apply (Finsupp.finite_support cf).subset
    intro i hi
    simp only [Function.support, ne_eq, smul_eq_zero, not_or, Set.mem_setOf_eq] at hi âŠ¢
    exact hi.1
  rw [finsum_eq_sum _ aux_finite]
  simp only [Basis.repr_symm_apply, Finsupp.linearCombination, Finsupp.coe_lsum,
             LinearMap.coe_smulRight, LinearMap.id_coe, id_eq, Finsupp.sum]
  congr
  ext i
  simp [Basis.ne_zero B i]

-- TODO: To Mathlib.
lemma _root_.Basis.repr_finsum_mem_eq_ite {R M Î¹ : Type*} [Semiring R] [Nontrivial R]
    [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M] (B : Basis Î¹ R M) (cf : Î¹ â†’â‚€ R)
    (I : Set Î¹) [DecidablePred fun i â†¦ i âˆˆ I] (j : Î¹) :
    B.repr (âˆ‘á¶  i âˆˆ I, cf i â€¢ B i) j = if j âˆˆ I then cf j else 0 := by
  let cf'' := I.indicator cf
  let cf' : Î¹ â†’â‚€ R := {
    support := (cf.support.finite_toSet.inter_of_left I).toFinset
    toFun := I.indicator cf
    mem_support_toFun i := by simp [and_comm]
  }
  have key := B.repr_finsum cf'
  simp only [Finsupp.ext_iff] at key
  have aux (i : Î¹) : cf' i = if i âˆˆ I then cf i else 0 := by simp [cf', Set.indicator]
  rw [â† aux j, â† key j]
  congr
  ext i
  by_cases hi : i âˆˆ I <;> simp [hi, aux i]

-- TODO: To Mathlib.
lemma _root_.Basis.iSupIndep_submodule_span_of_pairwise_disjoint
    {R M Î¹ Îº : Type*} [Semiring R] [AddCommGroup M] [Module R M]
    (B : Basis Î¹ R M) (Is : Îº â†’ Set Î¹)
    (Is_disj : Pairwise (fun kâ‚ kâ‚‚ â†¦ Disjoint (Is kâ‚) (Is kâ‚‚))) :
    iSupIndep fun k â†¦ Submodule.span R (B '' (Is k)) := by
  intro k
  simp only [Disjoint, ne_eq, le_bot_iff, Submodule.eq_bot_iff]
  intro U hUk hU v hv
  have hvk := hUk hv
  have hv' := hU hv
  simp only [Submodule.iSup_span, â† Set.image_iUnion] at hv'
  rw [Basis.mem_span_image] at hvk hv'
  suffices B.repr v = 0 by simpa using this
  suffices (B.repr v).support = âˆ… by simpa using this
  apply Finset.eq_empty_of_forall_notMem
  exact iSupIndep_iff_pairwiseDisjoint.mpr Is_disj k hvk hv'

variable {ğ•œ ğ“°} in
def ofBasis {Î¹ : Type*} [Nontrivial ğ•œ] [NoZeroSMulDivisors ğ•œ ğ“°]
    (B : Basis Î¹ ğ•œ ğ“°) (Bp : SignType â†’ Set Î¹)
    (Bp_disj : Pairwise (fun Îµâ‚ Îµâ‚‚ â†¦ Disjoint (Bp Îµâ‚) (Bp Îµâ‚‚)))
    (Bp_cover : â‹ƒ Îµ, Bp Îµ = Set.univ) :
    TriangularDecomposition ğ•œ ğ“° where
  part Îµ := Submodule.span ğ•œ (B '' Bp Îµ)
    --Submodule.span ğ•œ (Set.range fun (j : Bp Îµ) â†¦ B j)
  directSum := by
    rw [DirectSum.isInternal_submodule_iff_iSupIndep_and_iSup_eq_top]
    constructor
    Â· exact B.iSupIndep_submodule_span_of_pairwise_disjoint _ Bp_disj
    Â· rw [Submodule.eq_top_iff']
      intro X
      have Xpart_mem (Îµ) : âˆ‘á¶  i âˆˆ Bp Îµ, B.repr X i â€¢ B i âˆˆ Submodule.span ğ•œ (B '' Bp Îµ) :=
        finsum_mem_mem_span (fun i â†¦ B i) (B.repr X) (Bp Îµ)
      have X_eq :
          X = âˆ‘á¶  i âˆˆ Bp 1, B.repr X i â€¢ B i + âˆ‘á¶  i âˆˆ Bp (-1), B.repr X i â€¢ B i
              + âˆ‘á¶  i âˆˆ Bp 0, B.repr X i â€¢ B i := by
        nth_rw 1 [â† B.finsum_repr_smul_basis X]
        have supp_finite_aux : (Function.support (fun i â†¦ B.repr X i â€¢ B i)).Finite := by
          apply (Finsupp.finite_support (B.repr X)).subset
          intro i hi
          simp only [Function.support, ne_eq, smul_eq_zero, not_or, Set.mem_setOf_eq] at hi âŠ¢
          exact hi.1
        have supp_finite (Îµ : SignType) := supp_finite_aux.inter_of_right (Bp Îµ)
        rw [â† finsum_mem_union' (Bp_disj (by simp)) (supp_finite 1) (supp_finite (-1))]
        rw [â† finsum_mem_union' ?_ ?_ (supp_finite 0)]
        Â· have Bp_cover' : Bp 1 âˆª Bp (-1) âˆª Bp 0 = Set.univ := by
            rw [â† Bp_cover]
            apply subset_antisymm
            Â· refine Set.union_subset (Set.union_subset ?_ ?_) ?_ <;>
              Â· exact Set.subset_iUnion_of_subset _ subset_rfl
            Â· apply Set.iUnion_subset
              intro Îµ
              match Îµ with
              | 1 => apply Set.subset_union_of_subset_left (by simp)
              | 0 => simp
              | -1 => apply Set.subset_union_of_subset_left (by simp)
          simp [Bp_cover']
        Â· exact Disjoint.union_left (Bp_disj (by simp)) (Bp_disj (by simp))
        Â· exact Set.Finite.inter_of_right supp_finite_aux (Bp 1 âˆª Bp (-1))
      rw [X_eq]
      apply Submodule.add_mem _ (Submodule.add_mem _ ?_ ?_)
      Â· exact Submodule.mem_iSup_of_mem 0 (Xpart_mem 0)
      Â· exact Submodule.mem_iSup_of_mem 1 (Xpart_mem 1)
      Â· exact Submodule.mem_iSup_of_mem (-1) (Xpart_mem (-1))

lemma smul_support_subset_left {R M Î¹ : Type*} [Semiring R]
    [AddCommGroup M] [Module R M] (v : Î¹ â†’ M) (cf : Î¹ â†’ R) :
    (fun i â†¦ cf i â€¢ v i).support âŠ† cf.support := by
  simp only [Function.support_subset_iff, ne_eq, Function.mem_support]
  intro i
  rw [not_imp_not]
  intro hcf
  simp [hcf]

-- TODO: To Mathlib?
-- The closest I found: `Basis.ofSpan_subset`, `Basis.ofSpan`, `Basis.flag`
noncomputable def _root_.Basis.basis_submodule_span {R M Î¹ : Type*} [Semiring R] [Nontrivial R]
    [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M] (B : Basis Î¹ R M) (I : Set Î¹) :
    Basis I R (Submodule.span R (B '' I)) :=
  let f : Submodule.span R (B '' I) â†’â‚—[R] (I â†’â‚€ R) := {
    toFun v := Finsupp.subtypeDomain (fun x â†¦ x âˆˆ I) (B.repr v)
    map_add' vâ‚ vâ‚‚ := by simp
    map_smul' r v := by
      simp only [SetLike.val_smul, map_smul, RingHom.id_apply]
      exact Finsupp.ext (congrFun rfl) }
  let g : (I â†’â‚€ R) â†’â‚—[R] Submodule.span R (B '' I) := {
    toFun cf := âŸ¨âˆ‘á¶  i, cf i â€¢ B i, by convert finsum_mem_span (fun (i : I) â†¦ B i) cf ; aesopâŸ©
    map_add' cfâ‚ cfâ‚‚ := by
      simp only [Finsupp.coe_add, Pi.add_apply, AddMemClass.mk_add_mk, Subtype.mk.injEq]
      rw [â† finsum_add_distrib]
      Â· simp [add_smul]
      Â· exact cfâ‚.finite_support.subset <| smul_support_subset_left ..
      Â· exact cfâ‚‚.finite_support.subset <| smul_support_subset_left ..
    map_smul' r cf := by
      simp only [Finsupp.coe_smul, Pi.smul_apply, smul_assoc, RingHom.id_apply, SetLike.mk_smul_mk,
                 smul_finsum] }
  --have f_injective : Function.Injective f := by
  --  rw [Setoid.injective_iff_ker_bot]
  --  ext uâ‚ uâ‚‚
  --  simp only [Setoid.ker, LinearMap.coe_mk, AddHom.coe_mk, Function.onFun, Setoid.bot_def, f]
  --  constructor
  --  Â· intro h
  --    suffices B.repr uâ‚ = B.repr uâ‚‚ by simpa using this
  --    ext i
  --    by_cases hiI : i âˆˆ I
  --    Â· simp only [Finsupp.subtypeDomain, Finsupp.mk.injEq] at h
  --      exact congr_fun h.2 âŸ¨i, hiIâŸ©
  --    Â· have huâ‚ := (B.mem_span_image (s := I)).mp (Submodule.coe_mem uâ‚)
  --      have huâ‚‚ := (B.mem_span_image (s := I)).mp (Submodule.coe_mem uâ‚‚)
  --      simp only [Finsupp.support_subset_iff] at huâ‚ huâ‚‚
  --      simp [huâ‚ _ hiI, huâ‚‚ _ hiI]
  --  Â· intro hu
  --    simp [hu]
  --have f_surjective : Function.Surjective f := by
  --  intro cf
  --  use âŸ¨âˆ‘á¶  i, cf i â€¢ B i, by convert finsum_mem_span (fun (i : I) â†¦ B i) cf ; aesopâŸ©
  --  ext i
  --  simp only [f, LinearMap.coe_mk, AddHom.coe_mk, Finsupp.subtypeDomain_apply]
  --  classical
  --  have aux := B.repr_finsum_mem_eq_ite cf.extendDomain I i
  --  simp only [Finsupp.extendDomain_toFun, dite_smul, zero_smul, Subtype.coe_prop, â†“reduceIte,
  --             â†“reduceDIte, Subtype.coe_eta] at aux
  --  simp [â† aux, â† finsum_set_coe_eq_finsum_mem]
  --âŸ¨LinearEquiv.ofBijective f âŸ¨f_injective, f_surjectiveâŸ©âŸ©
  have fog : f âˆ˜ g = id := by
    funext cf
    ext i
    simp only [f, g, LinearMap.coe_mk, AddHom.coe_mk]
    classical
    have aux := B.repr_finsum_mem_eq_ite cf.extendDomain I i
    simp only [Finsupp.extendDomain_toFun, dite_smul, zero_smul, Subtype.coe_prop, â†“reduceIte,
               â†“reduceDIte, Subtype.coe_eta] at aux
    simp [â† aux, â† finsum_set_coe_eq_finsum_mem]
  have gof : g âˆ˜ f = id := by
    funext v
    simp only [LinearMap.coe_mk, AddHom.coe_mk, Function.comp_apply, Finsupp.subtypeDomain_apply,
               id_eq, g, f]
    ext
    dsimp
    nth_rw 2 [â† B.finsum_repr_smul_basis v]
    rw [@finsum_set_coe_eq_finsum_mem Î¹ M _ (fun i â†¦ (B.repr v) i â€¢ B i) I]
    congr 1
    ext j
    by_cases hj : j âˆˆ I
    Â· simp [hj]
    Â· simp only [hj, finsum_false]
      simp [show B.repr v j = 0
            from Finsupp.notMem_support_iff.mp fun h â†¦
              hj ((Basis.mem_span_image ..).mp (Submodule.coe_mem v) h)]
  âŸ¨{
    toFun := f
    map_add' x y := LinearMap.map_add f x y
    map_smul' m x := LinearMap.CompatibleSMul.map_smul f m x
    invFun := g
    left_inv := by exact congrFun gof
    right_inv := by exact congrFun fog
  }âŸ©
  --âŸ¨LinearEquiv.ofBijective f âŸ¨f_injective, f_surjectiveâŸ©âŸ©

-- TODO: To Mathlib?
@[simp] lemma _root_.Basis.basis_submodule_span_apply {R M Î¹ : Type*}
    [Semiring R] [Nontrivial R] [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M]
    (B : Basis Î¹ R M) (I : Set Î¹) (i : I) :
    B.basis_submodule_span I i = B i.val := by
  simp only [Basis.basis_submodule_span, LinearMap.coe_mk, AddHom.coe_mk, Basis.coe_ofRepr,
             LinearEquiv.coe_symm_mk]
  rw [finsum_eq_single _ i]
  Â· simp
  Â· intro j hj
    simp [hj.symm]

variable {ğ•œ ğ“°} in
/-- The parts of a triangular decomposition determined by a basis have natural bases by
construction. -/
noncomputable def ofBasis.basis_part {Î¹ : Type*} [Nontrivial ğ•œ] [NoZeroSMulDivisors ğ•œ ğ“°]
    (B : Basis Î¹ ğ•œ ğ“°) (Bp : SignType â†’ Set Î¹)
    (Bp_disj : Pairwise (fun Îµâ‚ Îµâ‚‚ â†¦ Disjoint (Bp Îµâ‚) (Bp Îµâ‚‚)))
    (Bp_cover : â‹ƒ Îµ, Bp Îµ = Set.univ) (Îµ : SignType) :
    Basis (Bp Îµ) ğ•œ ((ofBasis B Bp Bp_disj Bp_cover).part Îµ) :=
  Basis.basis_submodule_span B (Bp Îµ)

variable {ğ•œ ğ“°}
variable (tri : TriangularDecomposition ğ•œ ğ“°)

/-- The Cartan subalgebra of a given triangular decomposition of a Lie algebra. -/
abbrev cartan := tri.part 0

abbrev upper := tri.part 1

abbrev lower := tri.part (-1)

/-- Weights of a Lie algebra with triangular decomposition are functionals on the
Cartan subalgebra. -/
abbrev weight := tri.cartan â†’â‚—[ğ•œ] ğ•œ

variable {tri}

/-- The data associated to a weight for the purpose of constructing a highest weight
representation. -/
noncomputable def weightHW (Î· : weight tri) (i : tri.cartan âŠ• tri.upper) :
    ğ“¤ ğ•œ ğ“° Ã— ğ•œ := match i with
  | Sum.inl H => âŸ¨Î¹UEA ğ•œ H, Î· HâŸ©
  | Sum.inr E => âŸ¨Î¹UEA ğ•œ E, 0âŸ©

/-- The Verma module of highest weight Î·. -/
def VermaHW (Î· : weight tri) :=
  VermaModule (weightHW Î·)

variable (Î· : weight tri)

/-- The highest weight vector of the Verma module of highest weight Î·. -/
noncomputable def VermaHW.hwVec (Î· : weight tri) : VermaHW Î· :=
  VermaModule.hwVec _

noncomputable instance (Î· : weight tri) : AddCommGroup (VermaHW Î·) :=
  instAddCommGroupVermaModule _

noncomputable instance (Î· : weight tri) :
    Module (ğ“¤ ğ•œ ğ“°) (VermaHW Î·) :=
  instModuleVermaModule _

noncomputable instance (Î· : weight tri) :
    Module ğ•œ (VermaHW Î·) :=
  moduleScalarOfModule ğ•œ (ğ“¤ ğ•œ ğ“°) (VermaHW Î·)

instance (Î· : weight tri) :
    IsScalarTower ğ•œ (ğ“¤ ğ•œ ğ“°) (VermaHW Î·) :=
  isScalarTowerModuleScalarOfModule ğ•œ (ğ“¤ ğ•œ ğ“°) (VermaHW Î·)

lemma VermaHW.smul_eq_algebraHom_smul {Î· : weight tri} (r : ğ•œ) (v : VermaHW Î·) :
    r â€¢ v = (algebraMap ğ•œ (ğ“¤ ğ•œ ğ“°) r) â€¢ v :=
  rfl

instance (Î· : weight tri) :
    SMulCommClass ğ•œ (ğ“¤ ğ•œ ğ“°) (VermaHW Î·) where
  smul_comm r a v := by
    simp_rw [VermaHW.smul_eq_algebraHom_smul]
    simp only [â† smul_assoc, smul_eq_mul, Algebra.commutes r a]

lemma VermaHW.hwVec_cyclic (Î· : weight tri) :
    Submodule.span (ğ“¤ ğ•œ ğ“°) {VermaHW.hwVec Î·} = âŠ¤ :=
  VermaModule.hwVec_cyclic _

lemma VermaHW.upper_smul_hwVec (Î· : weight tri) {E : ğ“°} (hE : E âˆˆ tri.upper) :
    Î¹UEA ğ•œ E â€¢ VermaHW.hwVec Î· = 0 := by
  simpa [weightHW] using VermaModule.apply_hwVec_eq (weightHW Î·) (Sum.inr âŸ¨E, hEâŸ©)

lemma VermaHW.cartan_smul_hwVec (Î· : weight tri) {H : ğ“°} (hH : H âˆˆ tri.cartan) :
    Î¹UEA ğ•œ H â€¢ VermaHW.hwVec Î· = (Î· âŸ¨H, hHâŸ©) â€¢ VermaHW.hwVec Î· := by
  simpa [weightHW] using VermaModule.apply_hwVec_eq (weightHW Î·) (Sum.inl âŸ¨H, hHâŸ©)

end TriangularDecomposition

end



section VirasoroVerma

--open VirasoroAlgebra

variable (ğ•œ : Type*) [Field ğ•œ] [CharZero ğ•œ]

def VirasoroAlgebra.indexTri (Îµ : SignType) : Set (Option â„¤) := match Îµ with
  | SignType.zero => {none, some 0}
  | SignType.pos => some '' {n : â„¤ | 0 < n}
  | SignType.neg => some '' {n : â„¤ | n < 0}

lemma VirasoroAlgebra.pairwise_disjoint_indexTri :
    Pairwise fun Îµâ‚ Îµâ‚‚ â†¦ Disjoint (indexTri Îµâ‚) (indexTri Îµâ‚‚) := by
  intro Îµâ‚ Îµâ‚‚ h
  simp only [indexTri]
  cases Îµâ‚
  Â· cases Îµâ‚‚ <;> aesop
  Â· cases Îµâ‚‚
    Â· aesop
    Â· aesop
    Â· apply Set.disjoint_image_image fun k hk l hl â†¦ ?_
      by_contra con
      simp only [Set.mem_setOf_eq, Option.some.injEq] at hk hl con
      linarith
  Â· cases Îµâ‚‚
    Â· aesop
    Â· apply Set.disjoint_image_image fun k hk l hl â†¦ ?_
      by_contra con
      simp only [Set.mem_setOf_eq, Option.some.injEq] at hk hl con
      linarith
    Â· aesop

lemma VirasoroAlgebra.iUnion_indexTri :
    â‹ƒ Îµ, indexTri Îµ = Set.univ := by
  Â· simp only [indexTri, Set.iUnion_eq_univ_iff]
    intro i
    match i with
    | none => refine âŸ¨0, by decideâŸ©
    | some n =>
      by_cases hn0 : n = 0
      Â· refine âŸ¨0, by simp [hn0]âŸ©
      by_cases n_pos : 0 < n
      Â· refine âŸ¨1, by simp [n_pos]âŸ©
      Â· have n_neg : n < 0 := by grind
        refine âŸ¨-1, by simp [n_neg]âŸ©

open VirasoroAlgebra in
/-- The triangular decomposition of the Virasoro algebra with upper and lower (essentially
nilpotent) parts spanned by the `Lâ‚™` with positive and negative `n`, respectively, and the
Cartan subalgebra spanned by `Lâ‚€` and the central element `C`. -/
noncomputable def virasoroTri :
    TriangularDecomposition ğ•œ (VirasoroAlgebra ğ•œ) :=
  TriangularDecomposition.ofBasis (basisLC ğ•œ) indexTri pairwise_disjoint_indexTri iUnion_indexTri

open VirasoroAlgebra in
lemma virasoroTri_cartan :
    (virasoroTri ğ•œ).cartan = Submodule.span ğ•œ {.cgen ğ•œ, .lgen ğ•œ 0} := by
  simp only [virasoroTri, indexTri, TriangularDecomposition.ofBasis, TriangularDecomposition.cartan]
  congr 1
  aesop

open VirasoroAlgebra in
lemma virasoroTri_upper :
    (virasoroTri ğ•œ).upper = Submodule.span ğ•œ ((fun n â†¦ lgen ğ•œ n) '' {n | 0 < n}) := by
  simp only [virasoroTri, indexTri, TriangularDecomposition.upper, TriangularDecomposition.ofBasis]
  congr 1
  aesop

open VirasoroAlgebra in
lemma virasoroTri_lower :
    (virasoroTri ğ•œ).lower = Submodule.span ğ•œ ((fun n â†¦ lgen ğ•œ n) '' {n | n < 0}) := by
  simp only [virasoroTri, indexTri, TriangularDecomposition.ofBasis, TriangularDecomposition.lower]
  congr 1
  aesop

open VirasoroAlgebra in
noncomputable def virasoroTri_cartan_basis :
    Basis ({none, some 0} : Set (Option â„¤)) ğ•œ (virasoroTri ğ•œ).cartan :=
  TriangularDecomposition.ofBasis.basis_part (VirasoroAlgebra.basisLC ğ•œ) indexTri
    pairwise_disjoint_indexTri iUnion_indexTri 0

/-- The highest weight of Virasoro algebra determined by the central charge `c` (`C`-eigenvalue)
and conformal weight `h` (`Lâ‚€`-eigenvalue). -/
noncomputable def VirasoroAlgebra.hw (c h : ğ•œ) :
    (virasoroTri ğ•œ).cartan â†’â‚—[ğ•œ] ğ•œ :=
  (virasoroTri_cartan_basis ğ•œ).constr (M' := ğ•œ) ğ•œ (fun i â†¦ if i.val = none then c else h)

/-- The Virasoro generator `C` as an element of the Cartan subalgebra. -/
noncomputable def virasoroTri_cgen : (virasoroTri ğ•œ).part 0 :=
  âŸ¨.cgen ğ•œ, Submodule.mem_span_of_mem (by simp [VirasoroAlgebra.indexTri])âŸ©

/-- The Virasoro generator `Lâ‚€` as an element of the Cartan subalgebra. -/
noncomputable def virasoroTri_lzero : (virasoroTri ğ•œ).part 0 :=
  âŸ¨.lgen ğ•œ 0, Submodule.mem_span_of_mem (by simp [VirasoroAlgebra.indexTri])âŸ©

@[simp] lemma virasoroTri_cgen_val :
    (virasoroTri_cgen ğ•œ).val = .cgen ğ•œ :=
  rfl

@[simp] lemma virasoroTri_lzero_val :
    (virasoroTri_lzero ğ•œ).val = .lgen ğ•œ 0 :=
  rfl

open VirasoroAlgebra in
lemma virasoroTri_cartan_basis_none_eq_cgen :
    (virasoroTri_cartan_basis ğ•œ) âŸ¨none, Set.mem_insert none {some 0}âŸ© = virasoroTri_cgen ğ•œ := by
  ext
  simp only [virasoroTri_cartan_basis, TriangularDecomposition.ofBasis.basis_part, indexTri,
             virasoroTri_cgen_val]
  convert (basisLC ğ•œ).basis_submodule_span_apply {none, some 0} âŸ¨none, Set.mem_insert none {some 0}âŸ©
  simp

open VirasoroAlgebra in
lemma virasoroTri_cartan_basis_some_eq_lzero :
    (virasoroTri_cartan_basis ğ•œ) âŸ¨some 0, by exact Set.mem_insert_of_mem none rflâŸ©
      = virasoroTri_lzero ğ•œ := by
  ext
  simp only [virasoroTri_cartan_basis, TriangularDecomposition.ofBasis.basis_part, indexTri,
             virasoroTri_lzero_val]
  convert (basisLC ğ•œ).basis_submodule_span_apply {none, some 0}
          âŸ¨some 0, Set.mem_insert_of_mem none rflâŸ©
  simp

lemma virasoroTri_cgen_mem_cartan :
    .cgen ğ•œ âˆˆ (virasoroTri ğ•œ).cartan := by
  simpa only [virasoroTri_cartan] using Submodule.mem_span_of_mem (by simp)

lemma virasoroTri_lgen_zero_mem_cartan :
    .lgen ğ•œ 0 âˆˆ (virasoroTri ğ•œ).cartan := by
  simpa only [virasoroTri_cartan] using Submodule.mem_span_of_mem (by simp)

lemma virasoroTri_lgen_pos_mem_upper {n : â„¤} (n_pos : 0 < n) :
    .lgen ğ•œ n âˆˆ (virasoroTri ğ•œ).upper := by
  simp only [virasoroTri_upper]
  apply Submodule.mem_span_of_mem
  exact (Set.mem_image ..).mpr âŸ¨n, âŸ¨n_pos, rflâŸ©âŸ©

lemma VirasoroAlgebra.hw_apply_cgen (c h : ğ•œ) :
    hw ğ•œ c h (virasoroTri_cgen ğ•œ) = c := by
  rw [â† virasoroTri_cartan_basis_none_eq_cgen]
  simp only [hw, Basis.constr_apply_fintype]
  simp only [Basis.equivFun_self, smul_eq_mul, mul_ite, ite_mul, one_mul, zero_mul]
  rw [Finset.sum_eq_single âŸ¨none, Set.mem_insert none {some 0}âŸ©]
  Â· simp
  Â· intro j _ hj
    simp [hj.symm, show Â¬ (j : Option â„¤) = none by aesop]
  Â· simp

lemma VirasoroAlgebra.hw_apply_lzero (c h : ğ•œ) :
    hw ğ•œ c h (virasoroTri_lzero ğ•œ) = h := by
  rw [â† virasoroTri_cartan_basis_some_eq_lzero]
  simp only [hw, Basis.constr_apply_fintype]
  simp only [Basis.equivFun_self, smul_eq_mul, mul_ite, ite_mul, one_mul, zero_mul]
  rw [Finset.sum_eq_single âŸ¨some 0, by exact Set.mem_insert_of_mem none rflâŸ©]
  Â· simp
  Â· intro j _ hj
    simp [hj.symm]
  Â· simp

/-- The Verma module for the Virasoso algebra with central charge `c` and conformal weight `h`. -/
abbrev VirasoroVerma (c h : ğ•œ) := (virasoroTri ğ•œ).VermaHW (VirasoroAlgebra.hw ğ•œ c h)

/-- The highest weight vector in the Verma module for the Virasoso algebra with central charge `c`
and conformal weight `h`. -/
noncomputable abbrev VirasoroVerma.hwVec (c h : ğ•œ) :=
   TriangularDecomposition.VermaHW.hwVec (VirasoroAlgebra.hw ğ•œ c h)

lemma VirasoroVerma.hwVec_cyclic (c h : ğ•œ) :
    Submodule.span (ğ“¤ ğ•œ (VirasoroAlgebra ğ•œ)) {VirasoroVerma.hwVec ğ•œ c h} = âŠ¤ :=
  VermaModule.hwVec_cyclic ..

lemma VirasoroVerma.lgen_pos_hwVec (c h : ğ•œ) {n : â„¤} (n_pos : 0 < n) :
    Î¹UEA ğ•œ (VirasoroAlgebra.lgen ğ•œ n) â€¢ hwVec ğ•œ c h = 0 :=
  TriangularDecomposition.VermaHW.upper_smul_hwVec
    (VirasoroAlgebra.hw ğ•œ c h) (E := .lgen ğ•œ n) (virasoroTri_lgen_pos_mem_upper ğ•œ n_pos)

lemma VirasoroVerma.lgen_zero_hwVec (c h : ğ•œ) :
    Î¹UEA ğ•œ (VirasoroAlgebra.lgen ğ•œ 0) â€¢ hwVec ğ•œ c h = h â€¢ hwVec ğ•œ c h := by
  convert TriangularDecomposition.VermaHW.cartan_smul_hwVec
          (VirasoroAlgebra.hw ğ•œ c h) (H := .lgen ğ•œ 0) (virasoroTri_lgen_zero_mem_cartan ğ•œ)
  exact (VirasoroAlgebra.hw_apply_lzero ğ•œ c h).symm

lemma VirasoroVerma.cgen_hwVec (c h : ğ•œ) :
    Î¹UEA ğ•œ (VirasoroAlgebra.cgen ğ•œ) â€¢ hwVec ğ•œ c h = c â€¢ hwVec ğ•œ c h := by
  convert TriangularDecomposition.VermaHW.cartan_smul_hwVec
          (VirasoroAlgebra.hw ğ•œ c h) (H := .cgen ğ•œ) (virasoroTri_cgen_mem_cartan ğ•œ)
  exact (VirasoroAlgebra.hw_apply_cgen ğ•œ c h).symm

lemma VirasoroVerma.cgen_smul (c h : ğ•œ) (v : VirasoroVerma ğ•œ c h) :
    Î¹UEA ğ•œ (VirasoroAlgebra.cgen ğ•œ) â€¢ v = c â€¢ v :=
  UniversalEnvelopingAlgebra.smul_eq_of_cyclic_of_forall_lie_eq_zero
    _ _ (fun _ â†¦ rfl) (hwVec_cyclic ğ•œ c h) (cgen_hwVec ğ•œ c h) v

end VirasoroVerma



end VirasoroProject
