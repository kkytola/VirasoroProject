/-
Copyright (c) 2024 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.LinearAlgebra.Basis.Bilinear
import VirasoroProject.IsCentralExtension
import VirasoroProject.ToMathlib.Algebra.Lie.Abelian
import VirasoroProject.ToMathlib.LinearAlgebra.Basis.Defs

/-!
# Heisenberg algebra

This file defines the Heisenberg algebra, as the one-dimensional central extension of a countably
infinite dimensional abelian Lie algebra associated to a nontrivial 2-cocycle.

(The construction is mathematically boring, but the interesting part is the relation with
Virasoro algebra: suitable positive energy representations of the Heisenberg algebra can be made
into representations of the Virasoro algebra by a Sugawara construction.)

## Main definitions

* `HeisenbergAlgebra`: The Heisenberg algebra.
* `HeisenbergAlgebra.jgen`: The (commonly used) elements Jâ‚–, k âˆˆ â„¤, of the Heisenberg algebra.
* `HeisenbergAlgebra.kgen`: The central element K of the Heisenberg algebra (commonly set
  to 1 in representations).

## Main statements

* `HeisenbergAlgebra.instLieAlgebra`: The Heisenberg algebra is a Lie algebra.

## Implementation notes

The Heisenberg algebra is defined as a central extension of an infinite-dimensional abelian Lie
algebra. (A more direct definition based on defining a Lie bracket on a countably infinite
dimensional vector space would also be possible.)

## Tags

Heisenberg algebra

-/

namespace VirasoroProject

section AbelianLieAlgebraOn

/-! ### Abelian Lie algebra with a given basis -/

variable (Î¹ : Type*)
variable (ğ•œ : Type*) [CommRing ğ•œ]

/-- An auxiliary construction of an abelian Lie algebra with a given index set for a basis. -/
def AbelianLieAlgebraOn := Î¹ â†’â‚€ ğ•œ

noncomputable instance : AddCommGroup (AbelianLieAlgebraOn Î¹ ğ•œ) := Finsupp.instAddCommGroup

noncomputable instance : Module ğ•œ (AbelianLieAlgebraOn Î¹ ğ•œ) := Finsupp.module ..

namespace AbelianLieAlgebraOn

variable {Î¹}

/-- The basis of `jáµ¢` generators of the abelian Lie algebra (indices `i : Î¹`). -/
noncomputable def jgen : Basis Î¹ ğ•œ (AbelianLieAlgebraOn Î¹ ğ•œ) := Finsupp.basisFun _ _

lemma jgen_eq_single (i : Î¹) : jgen ğ•œ i = Finsupp.single i 1 := rfl

/-- The Lie ring structure on the Witt algebra `WittAlgebra`. -/
noncomputable instance : LieRing (AbelianLieAlgebraOn Î¹ ğ•œ) where
  bracket X Y := 0
  add_lie Xâ‚ Xâ‚‚ Y := by simp
  lie_add X Yâ‚ Yâ‚‚ := by simp
  lie_self X := by simp
  leibniz_lie X Y Z := by simp

@[simp] lemma lie_def (X Y : AbelianLieAlgebraOn Î¹ ğ•œ) : â…X, Yâ† = 0 := rfl

/-- The Lie algebra structure on the Witt algebra `WittAlgebra`. -/
noncomputable instance : LieAlgebra ğ•œ (AbelianLieAlgebraOn Î¹ ğ•œ) where
  lie_smul c X Y := by simp

instance : IsLieAbelian (AbelianLieAlgebraOn Î¹ ğ•œ) where
  trivial _ _ := rfl

end AbelianLieAlgebraOn -- namespace

end AbelianLieAlgebraOn -- section

section HeisenbergCocycle

/-! ### The 2-cocycle defining the Heisenberg algebra as central extension -/

namespace AbelianLieAlgebraOn

variable (ğ•œ : Type*) [Field ğ•œ]

/-- A bilinear map version of the Heisenberg cocycle. -/
noncomputable def heisenbergCocycleBilin : (AbelianLieAlgebraOn â„¤ ğ•œ) â†’â‚—[ğ•œ] (AbelianLieAlgebraOn â„¤ ğ•œ) â†’â‚—[ğ•œ] ğ•œ :=
  (jgen ğ•œ).constr ğ•œ <| fun k â†¦ (jgen ğ•œ).constr ğ•œ <| fun l â†¦
      if k + l = 0 then k else 0

lemma heisenbergCocycleBilin_apply_jgen_jgen (k l : â„¤) :
    heisenbergCocycleBilin ğ•œ (jgen ğ•œ k) (jgen ğ•œ l)
      = if k + l = 0 then k else 0 := by
  simp [heisenbergCocycleBilin]

example (R U V W : Type) [Field R] [AddCommGroup U] [AddCommGroup V] [AddCommGroup W]
    [Module R U] [Module R V] [Module R W] (Î² : U â†’â‚—[R] V â†’â‚—[R] W) :
    V â†’â‚—[R] U â†’â‚—[R] W := by
  exact Î².flip

lemma heisenbergCocycleBilin_eq_neg_flip :
    heisenbergCocycleBilin ğ•œ = -(heisenbergCocycleBilin ğ•œ).flip := by
  apply LinearMap.ext_basis (jgen _) (jgen _)
  intro k l
  simp only [heisenbergCocycleBilin, Basis.constr_basis, LinearMap.neg_apply, LinearMap.flip_apply]
  by_cases opp : k + l = 0
  Â· simp [â†“reduceIte, show l = -k by linarith]
  Â· simp [opp, add_comm l k]

variable [CharZero ğ•œ]

/-- The Heisenberg cocycle. -/
noncomputable def heisenbergCocycle :
    LieTwoCocycle ğ•œ (AbelianLieAlgebraOn â„¤ ğ•œ) ğ•œ where
  toBilin := heisenbergCocycleBilin ğ•œ
  self' X := by
    apply (self_eq_neg ğ•œ ğ•œ).mp
    simpa only [LinearMap.neg_apply, LinearMap.coe_mk, AddHom.coe_mk]
      using LinearMap.congr_funâ‚‚ (heisenbergCocycleBilin_eq_neg_flip ğ•œ) X X
  leibniz' X Y Z := by
    simp only [lie_def, map_zero, LinearMap.zero_apply, (lie_skew X Z).symm, neg_zero, add_zero]

lemma heisenbergCocycle_apply_jgen_jgen (k l : â„¤) :
    heisenbergCocycle ğ•œ (jgen ğ•œ k) (jgen ğ•œ l) = if k + l = 0 then k else 0 :=
  heisenbergCocycleBilin_apply_jgen_jgen ğ•œ k l

lemma heisenbergCocycle_ne_zero :
    heisenbergCocycle ğ•œ â‰  0 := by
  have obs := heisenbergCocycle_apply_jgen_jgen ğ•œ 1 (-1)
  simp only [Int.reduceNeg, add_neg_cancel, â†“reduceIte, Int.cast_one] at obs
  intro maybe_zero
  simp [maybe_zero] at obs

/-- The Heisenberg cocycle is cohomologically nontrivial. -/
theorem cohomologyClass_heisenbergCocycle_ne_zero :
    (heisenbergCocycle ğ•œ).cohomologyClass â‰  0 := by
  change LieTwoCocycle.toLieTwoCohomologyEquiv ğ•œ
    (AbelianLieAlgebraOn â„¤ ğ•œ) ğ•œ (heisenbergCocycle ğ•œ) â‰  0
  exact (LinearEquiv.map_ne_zero_iff _).mpr <| heisenbergCocycle_ne_zero ğ•œ

/-- The abelian Lie algebra 2-cohomology `HÂ²(AbelianLieAlgebraOn â„¤ ğ•œ, ğ•œ)` is nontrivial. -/
theorem nontrivial_lieTwoCohomology :
    Nontrivial (LieTwoCohomology ğ•œ (AbelianLieAlgebraOn â„¤ ğ•œ) ğ•œ) :=
  nontrivial_of_ne _ _ (cohomologyClass_heisenbergCocycle_ne_zero ğ•œ)

end AbelianLieAlgebraOn -- namespace

end HeisenbergCocycle -- section

section HeisenbergAlgebra

/-! ### The Heisenberg (Lie) algebra -/

variable (ğ•œ : Type*) [Field ğ•œ]
variable [CharZero ğ•œ]

/-- The Heisenberg algebra. -/
def HeisenbergAlgebra := LieTwoCocycle.CentralExtension (AbelianLieAlgebraOn.heisenbergCocycle ğ•œ)

namespace HeisenbergAlgebra

lemma ext' {X Y : HeisenbergAlgebra ğ•œ} (hâ‚ : X.1 = Y.1) (hâ‚‚ : X.2 = Y.2) :
    X = Y :=
  LieTwoCocycle.CentralExtension.ext hâ‚ hâ‚‚

/-- The Heisenberg algebra is a Lie ring. -/
noncomputable instance : LieRing (HeisenbergAlgebra ğ•œ) :=
  LieTwoCocycle.CentralExtension.instLieRing _

/-- The Heisenberg algebra is a Lie algebra. -/
noncomputable instance : LieAlgebra ğ•œ (HeisenbergAlgebra ğ•œ) :=
  LieTwoCocycle.CentralExtension.instLieAlgebra _

variable {ğ•œ}

/-- The projection from Heisenberg algebra to the original abelian Lie algebra. -/
noncomputable def toAbelianLieAlgebraOn : HeisenbergAlgebra ğ•œ â†’â‚—â…ğ•œâ† AbelianLieAlgebraOn â„¤ ğ•œ :=
  LieTwoCocycle.CentralExtension.proj (AbelianLieAlgebraOn.heisenbergCocycle ğ•œ)

variable (ğ•œ)

/-- The embedding of central elements to Heisenberg algebra. -/
noncomputable def ofCentral : ğ•œ â†’â‚—â…ğ•œâ† HeisenbergAlgebra ğ•œ :=
  LieTwoCocycle.CentralExtension.emb (AbelianLieAlgebraOn.heisenbergCocycle ğ•œ)

lemma bracket_def' (X Y : HeisenbergAlgebra ğ•œ) :
    â…X, Yâ† = âŸ¨â…toAbelianLieAlgebraOn X, toAbelianLieAlgebraOn Yâ†,
              (AbelianLieAlgebraOn.heisenbergCocycle ğ•œ) (toAbelianLieAlgebraOn X) (toAbelianLieAlgebraOn Y)âŸ© := by
  rfl

@[simp] lemma bracket_fst (X Y : HeisenbergAlgebra ğ•œ) :
    â…X, Yâ†.1 = 0 := rfl

@[simp] lemma bracket_snd (X Y : HeisenbergAlgebra ğ•œ) :
    â…X, Yâ†.2 = (AbelianLieAlgebraOn.heisenbergCocycle ğ•œ) (toAbelianLieAlgebraOn X) (toAbelianLieAlgebraOn Y) := rfl

lemma add_def' (X Y : HeisenbergAlgebra ğ•œ) :
    X + Y = âŸ¨X.1 + Y.1, X.2 + Y.2âŸ© := rfl

lemma smul_def' (c : ğ•œ) (X : HeisenbergAlgebra ğ•œ) :
    c â€¢ X = âŸ¨c â€¢ X.1, c * X.2âŸ© := rfl

@[simp] lemma add_fst (X Y : HeisenbergAlgebra ğ•œ) :
    (X + Y).1 = X.1 + Y.1 := rfl

@[simp] lemma add_snd (X Y : HeisenbergAlgebra ğ•œ) :
    (X + Y).2 = X.2 + Y.2 := rfl

@[simp] lemma smul_fst (c : ğ•œ) (X : HeisenbergAlgebra ğ•œ) :
    (c â€¢ X).1 = c â€¢ X.1 := rfl

@[simp] lemma smul_snd (c : ğ•œ) (X : HeisenbergAlgebra ğ•œ) :
    (c â€¢ X).2 = c * X.2 := rfl

/-- The Heisenberg algebra is a central extension of the Witt algebra. -/
instance isCentralExtension : LieAlgebra.IsCentralExtension (ofCentral ğ•œ) toAbelianLieAlgebraOn :=
  LieTwoCocycle.CentralExtension.isCentralExtension _

/-- The (commonly used) `Jâ‚–` elements of the Heisenberg algebra, for `k âˆˆ â„¤`. -/
noncomputable def jgen (k : â„¤) : HeisenbergAlgebra ğ•œ := âŸ¨.jgen ğ•œ k, 0âŸ©

/-- The `K` central element of the Heisenberg algebra, which is commonly set to 1 (in
representations). -/
noncomputable def kgen : HeisenbergAlgebra ğ•œ := ofCentral ğ•œ 1

lemma kgen_eq_ofCentral_one : kgen ğ•œ = ofCentral ğ•œ 1 := rfl

lemma kgen_eq' : kgen ğ•œ = âŸ¨0, 1âŸ© := rfl

lemma jgen_eq' (k : â„¤) : jgen ğ•œ k = âŸ¨.jgen ğ•œ k, 0âŸ© := rfl

@[simp] lemma ofCentral_apply (a : ğ•œ) : ofCentral ğ•œ a = a â€¢ (kgen ğ•œ) := by
  change (âŸ¨0, aâŸ© : HeisenbergAlgebra ğ•œ) = a â€¢ âŸ¨0, 1âŸ©
  aesop

lemma toAbelianLieAlgebraOn_kgen :
  toAbelianLieAlgebraOn (kgen ğ•œ) = 0 := rfl

@[simp] lemma toAbelianLieAlgebraOn_jgen (n : â„¤) :
  toAbelianLieAlgebraOn (jgen ğ•œ n) = AbelianLieAlgebraOn.jgen ğ•œ n := rfl

@[simp] lemma kgen_bracket (Z : HeisenbergAlgebra ğ•œ) :
    â…kgen ğ•œ, Zâ† = 0 :=
  (isCentralExtension ğ•œ).central 1 Z

@[simp] lemma jgen_bracket (k l : â„¤) :
    â…jgen ğ•œ k, jgen ğ•œ lâ† = if k + l = 0 then (k : ğ•œ) â€¢ kgen ğ•œ else 0 := by
  simp_rw [bracket_def']
  by_cases h : k + l = 0
  Â· simp [h]
    apply ext'
    Â· simp [kgen_eq']
    Â· simp [AbelianLieAlgebraOn.heisenbergCocycle_apply_jgen_jgen, kgen_eq', h]
  Â· simp [h]
    apply ext' <;> simp [AbelianLieAlgebraOn.heisenbergCocycle_apply_jgen_jgen, h]

/-- A section of the standard projection from the Heisenberg algebra to the underlying
abelian Lie algebra. -/
noncomputable def jsection : AbelianLieAlgebraOn â„¤ ğ•œ â†’â‚—[ğ•œ] HeisenbergAlgebra ğ•œ :=
  LieTwoCocycle.CentralExtension.stdSection (AbelianLieAlgebraOn.heisenbergCocycle ğ•œ)

@[simp] lemma jsection_jgen (l : â„¤) :
    jsection ğ•œ (AbelianLieAlgebraOn.jgen ğ•œ l) = jgen ğ•œ l :=
  rfl

/-- The most commonly used basis of the Heisenberg algebra, consisting of `Jâ‚–` (`k âˆˆ â„¤`)
and the central element `K`. -/
noncomputable def basisJK : Basis (Option â„¤) ğ•œ (HeisenbergAlgebra ğ•œ) :=
  ((isCentralExtension ğ•œ).basis (jsection ğ•œ) rfl
        (Basis.singleton Unit ğ•œ) (AbelianLieAlgebraOn.jgen ğ•œ)).reindex
    { toFun uz := match uz with
        | Sum.inl _ => none
        | Sum.inr l => some l
      invFun oz := match oz with
        | none => Sum.inl âŸ¨âŸ©
        | some l => Sum.inr l
      left_inv uz := match uz with
        | Sum.inl _ => rfl
        | Sum.inr _ => rfl
      right_inv oz := match oz with
        | none => rfl
        | some _ => rfl }

@[simp] lemma basisJK_some (l : â„¤) :
    basisJK ğ•œ (some l) = jgen ğ•œ l := by
  simp [basisJK]

@[simp] lemma basisJK_none :
    basisJK ğ•œ none = kgen ğ•œ := by
  simp [basisJK]

end HeisenbergAlgebra -- namespace

end HeisenbergAlgebra

end VirasoroProject -- namespace
