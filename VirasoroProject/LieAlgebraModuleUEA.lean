/-
Copyright (c) 2025 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.Algebra.Lie.UniversalEnveloping
import Mathlib.GroupTheory.GroupAction.Ring
import Mathlib.Order.CompletePartialOrder
import VirasoroProject.LieAlgebraRepresentationOfBasis

/-!
# Modules over the universal enveloping algebra of a Lie algebra

This file contains some basics of the relationship between representations of a Lie algebra `ğ“°` and
modules over the universal enveloping algebra `ğ“¤ ğ•œ ğ“°`.

## Main definitions

As an auxiliary tool, we often need that any module `M` over a `ğ•œ`-algebra `A` is a module over `ğ•œ`.
We need to be able to talk separately about left `A`-multiplication and left `ğ•œ`-multiplication
on `M`, and more crucially yet, we need to be able to talk separately about `A`-module maps and
`ğ•œ`-linear maps.

* `ModuleOfModuleAlgebra`: This is a type synonym of the `A`-module `M` equipped with the instance
  of a `ğ•œ`-module.
* `ModuleOfModuleAlgebra.lsmul`: The left multiplication by an element `a âˆˆ A` defines a `ğ•œ`-linear
  map `M â†’ M`.
* `centralSMulHom`: The left multiplication by a central element `z âˆˆ A` defines an `A`-module
  map `M â†’ M`.
* `centralValueSubmodule`: Given a central element `z âˆˆ A` and a scalar `Î¶ âˆˆ ğ•œ`, this is the
  `A`-submodule of `M` consisting of vectors on which `z` acts as multiplication by `Î¶`.

The correspondence between representations of a Lie algebra `ğ“°` and modules over the
universal enveloping algebra `ğ“¤ ğ•œ ğ“°` consists of two directions:

* `UniversalEnvelopingAlgebra.representation`: Any module `V` over `ğ“¤ ğ•œ ğ“°` gives rise to a
  representation `ğ“° â†’ End(V)` of the Lie algebra `ğ“°` by restricting the module left multiplication
  to `ğ“° âŠ† ğ“¤ ğ•œ ğ“°`.
* `LieAlgebra.Representation.moduleUniversalEnvelopingAlgebra`: Any representation `ğ“° â†’ End(V)` of
  a Lie algebra `ğ“°` over `ğ•œ` gives rise to a structure of a `ğ“¤ ğ•œ ğ“°`-module on `V` in such a way that
  the representation defines the left multiplication by elements of `ğ“° âŠ† ğ“¤ ğ•œ ğ“°`.

## Main statements

* `UniversalEnvelopingAlgebra.induction`: Induction principle for the universal enveloping algebra:
  In order to prove a property for all elements of the universal enveloping algebra `ğ“¤ ğ•œ ğ“°` of a
  Lie algebra `ğ“°`, it is enough to prove that (1): the property holds for scalars `r âˆˆ ğ•œ âŠ† ğ“¤ ğ•œ ğ“°`,
  (2): the property holds for all Lie algebra elements `X âˆˆ ğ“° âŠ† ğ“¤ ğ•œ ğ“°`, (3): the property is
  preserved under multiplication, and (4): the property is preserved under addition.
* `UniversalEnvelopingAlgebra.central_of_forall_lie_eq_zero`: If `Z âˆˆ ğ“°` has vanishing Lie bracket
  with all other elements of `ğ“°`, then it is a central element in the universal enveloping
  algebra `ğ“¤ ğ•œ ğ“°`.

## Notation

* `ğ“¤` is introduced as a notation for `UniversalEnvelopingAlgebra`.
* `Î¹UEA` is introduced as a notation for the inclusion `ğ“° â†’ ğ“¤ ğ•œ ğ“°` (a Lie algebra homomorphism)
  of a Lie algebra `ğ“°` into its universal enveloping algebra `ğ“¤ ğ•œ ğ“°`.

## Tags

Lie algebra, universal enveloping algebra

-/


section modules_over_algebras_are_modules_over_scalars

variable (ğ•œ A : Type*) [CommRing ğ•œ] [Semiring A] [Algebra ğ•œ A]

lemma Algebra.scalar_smul_eq_smul_algebraMap_mul (c : ğ•œ) (a : A) :
    c â€¢ a = (c â€¢ (1 : A)) * a := by
  simp only [Algebra.smul_mul_assoc, one_mul]

lemma Algebra.smul_scalar_smul_eq_smul_algebraMap_mul (c : ğ•œ) (a : A) :
    c â€¢ a = a * (c â€¢ (1 : A)) := by
  simp only [Algebra.mul_smul_comm, mul_one]

variable (V : Type*) [AddCommGroup V] [Module A V]

/-- Any module over an algebra is a module over the scalars. -/
def moduleScalarOfModule : Module ğ•œ V :=
  Module.compHom _ (algebraMap ğ•œ A)

lemma moduleScalarOfModule.smul_def (r : ğ•œ) (v : V) :
    (moduleScalarOfModule ğ•œ A V).smul r v = algebraMap ğ•œ A r â€¢ v :=
  rfl

/-- When making any module over an algebra a module over the scalars, these form an
`IsScalarTower`.

(The reason this is needed as a separate lemma is that the actual meaning of the scalar
multiplication on representations of an algebra cannot be inferred by the type classes;
this must be hand-crafted by `moduleScalarOfModule` .) -/
lemma isScalarTowerModuleScalarOfModule :
    @IsScalarTower ğ•œ A V inferInstance inferInstance (moduleScalarOfModule ğ•œ A V).toSMul := by
  apply @IsScalarTower.mk ..
  intro c a v
  change (c â€¢ a) â€¢ v = algebraMap ğ•œ A c â€¢ a â€¢ v
  rw [Algebra.scalar_smul_eq_smul_algebraMap_mul]
  rw [mul_smul, Algebra.algebraMap_eq_smul_one c]

/-- Type synonym of a module over an algebra, when it is to be viewed as a module over
the scalars. -/
def _root_.ModuleOfModuleAlgebra (ğ•œ A V : Type*) [CommRing ğ•œ]
    [Semiring A] [Algebra ğ•œ A] [AddCommGroup V] [Module A V] :=
  V

instance : AddCommGroup (ModuleOfModuleAlgebra ğ•œ A V) :=
  â€¹AddCommGroup Vâ€º

instance : Module ğ•œ (ModuleOfModuleAlgebra ğ•œ A V) :=
  moduleScalarOfModule ğ•œ A V

/-- The map from `V` to its type synonym `ModuleOfModuleAlgebra ğ•œ A V`. -/
def _root_.ModuleOfModuleAlgebra.mk (v : V) :
    ModuleOfModuleAlgebra ğ•œ A V :=
  v

/-- The map from `V` to its type synonym `ModuleOfModuleAlgebra ğ•œ A V` as a
homomorphism of additive groups. -/
def _root_.ModuleOfModuleAlgebra.mkAddHom :
    V â†’+ ModuleOfModuleAlgebra ğ•œ A V where
  toFun := ModuleOfModuleAlgebra.mk ğ•œ A V
  map_zero' := rfl
  map_add' _ _ := rfl

/-- The map from the type synonym `ModuleOfModuleAlgebra ğ•œ A V` back to `V`. -/
def _root_.ModuleOfModuleAlgebra.unMk (v : ModuleOfModuleAlgebra ğ•œ A V) :
    V :=
  v

/-- The map from the type synonym `ModuleOfModuleAlgebra ğ•œ A V` back to `V` as a
homomorphism of additive groups. -/
def _root_.ModuleOfModuleAlgebra.unMkAddHom (ğ•œ A V : Type*) [CommRing ğ•œ]
    [Semiring A] [Algebra ğ•œ A] [AddCommGroup V] [Module A V] :
    ModuleOfModuleAlgebra ğ•œ A V â†’+ V where
  toFun := ModuleOfModuleAlgebra.unMk ğ•œ A V
  map_zero' := rfl
  map_add' _ _ := rfl

end modules_over_algebras_are_modules_over_scalars



section left_smul_linear_map

variable (ğ•œ : Type*) [CommRing ğ•œ]
variable {A : Type*} [Semiring A] [Algebra ğ•œ A]
variable (V : Type*) [AddCommGroup V] [Module A V]

/-- An element `a âˆˆ A` of a `ğ•œ`-algebra `A` defines a `ğ•œ`-linear map `V â†’ V` by left
multiplication. -/
def ModuleOfModuleAlgebra.lsmul (a : A) :
    ModuleOfModuleAlgebra ğ•œ A V â†’â‚—[ğ•œ] ModuleOfModuleAlgebra ğ•œ A V where
  toFun v := ModuleOfModuleAlgebra.mkAddHom ğ•œ A V (a â€¢ ModuleOfModuleAlgebra.unMkAddHom ğ•œ A V v)
  map_add' vâ‚ vâ‚‚ :=
    smul_add a (ModuleOfModuleAlgebra.unMkAddHom ğ•œ A V vâ‚) (ModuleOfModuleAlgebra.unMkAddHom ğ•œ A V vâ‚‚)
  map_smul' r v := by
    change a â€¢ (algebraMap ğ•œ A r â€¢ (ModuleOfModuleAlgebra.unMkAddHom ğ•œ A V v))
          = algebraMap ğ•œ A r â€¢ (a â€¢ (ModuleOfModuleAlgebra.unMkAddHom ğ•œ A V v))
    simp [â† smul_assoc]
    congr 1
    exact (Algebra.commutes r a).symm

lemma ModuleOfModuleAlgebra.lsmul_apply (a : A) (v : ModuleOfModuleAlgebra ğ•œ A V) :
    ModuleOfModuleAlgebra.lsmul ğ•œ V a v =
      ModuleOfModuleAlgebra.mkAddHom ğ•œ A V (a â€¢ ModuleOfModuleAlgebra.unMkAddHom ğ•œ A V v) := by
  rfl

end left_smul_linear_map



section central_element_action

variable {R : Type*} [Semiring R]

/-- In a module `M` over a ring `R`, left multiplication by a central element `z âˆˆ R` is an
`R`-linear map `M â†’ M`. -/
def centralSMulHom {z : R} (z_central : âˆ€ a, z * a = a * z)
    (M : Type*) [AddCommMonoid M] [Module R M] :
    M â†’â‚—[R] M where
  toFun := Module.toAddMonoidEnd R M z
  map_add' := map_add ((Module.toAddMonoidEnd R M) z)
  map_smul' a v := by
    change z â€¢ a â€¢ v = a â€¢ z â€¢ v
    rw [â† smul_assoc, â† mul_smul, â† z_central a, mul_smul, smul_assoc]

lemma centralSMulHom_apply {z : R} (z_central : âˆ€ a, Commute z a)
    (M : Type*) [AddCommMonoid M] [Module R M] (v : M) :
    centralSMulHom z_central M v = z â€¢ v :=
  rfl

variable {ğ•œ A : Type*} [CommRing ğ•œ] [Semiring A] [Algebra ğ•œ A]
variable (M : Type*) [AddCommGroup M] [Module ğ•œ M] [Module A M] [IsScalarTower ğ•œ A M]

/-- The set of vectors on which a given central element acts by a given scalar multiple as
a submodule. -/
def centralValueSubmodule {z : A} (z_central : âˆ€ a, Commute z a) (Î¶ : ğ•œ) :
    Submodule A M :=
  LinearMap.ker (centralSMulHom z_central M - Î¶ â€¢ (LinearMap.id ..))

/-- The defining property of `centralValueSubmodule` is the eigenvalue property for the central
element action. -/
lemma mem_centralValueSubmodule_iff {z : A} (z_central : âˆ€ a, Commute z a) (Î¶ : ğ•œ) (v : M) :
    v âˆˆ centralValueSubmodule M z_central Î¶ â†” z â€¢ v = Î¶ â€¢ v := by
  simp only [centralValueSubmodule, LinearMap.mem_ker, LinearMap.sub_apply, centralSMulHom_apply,
             LinearMap.smul_apply, LinearMap.id_coe, id_eq]
  grind

lemma smul_eq_scalar_smul_of_central_of_mem_span
    {z : A} (z_central : âˆ€ a, Commute z a) {u : M} {Î¶ : ğ•œ}
    (hzu : z â€¢ u = Î¶ â€¢ u) {v : M} (hv : v âˆˆ Submodule.span A {u}) :
    z â€¢ v = Î¶ â€¢ v := by
  rw [â† mem_centralValueSubmodule_iff]
  suffices Submodule.span A {u} â‰¤ centralValueSubmodule M z_central Î¶ from this hv
  apply (Submodule.span_singleton_le_iff_mem u _).mpr
  exact (mem_centralValueSubmodule_iff ..).mpr hzu
  -- *A shorter (and in some sense more elementary) proof:*
  --obtain âŸ¨a, hauvâŸ© := Submodule.mem_span_singleton.mp hv
  --rw [â† hauv, â† smul_assoc, smul_eq_mul, z_central a, â† smul_eq_mul, smul_assoc, hau]
  --exact smul_comm a Î¶ u

lemma smul_eq_scalar_smul_of_central
    {z : A} (z_central : âˆ€ a, Commute z a) {u : M} {Î¶ : ğ•œ}
    (hau : z â€¢ u = Î¶ â€¢ u) {v : M} (v_cyclic : Submodule.span A {u} = âŠ¤) :
    z â€¢ v = Î¶ â€¢ v := by
  apply smul_eq_scalar_smul_of_central_of_mem_span _ z_central hau
  simp [v_cyclic]

end central_element_action



section UniversalEnvelopingAlgebra

variable (ğ•œ : Type*) [CommRing ğ•œ]
variable (ğ“– : Type*) [LieRing ğ“–] [LieAlgebra ğ•œ ğ“–]

@[inherit_doc UniversalEnvelopingAlgebra]
abbrev ğ“¤ := UniversalEnvelopingAlgebra

@[inherit_doc UniversalEnvelopingAlgebra.Î¹]
abbrev Î¹UEA := UniversalEnvelopingAlgebra.Î¹

lemma UniversalEnvelopingAlgebra.mkAlgHom_range_eq_top :
    (UniversalEnvelopingAlgebra.mkAlgHom ğ•œ ğ“–).range = âŠ¤ := by
  simp only [UniversalEnvelopingAlgebra.mkAlgHom, RingQuot.mkAlgHom]
  rw [AlgHom.range_eq_top]
  exact RingQuot.mkRingHom_surjective (UniversalEnvelopingAlgebra.Rel ğ•œ ğ“–)

variable {ğ•œ ğ“–} in
lemma UniversalEnvelopingAlgebra.mkAlgHom_surjective :
    Function.Surjective (UniversalEnvelopingAlgebra.mkAlgHom ğ•œ ğ“–) := by
  simpa [â† AlgHom.range_eq_top] using mkAlgHom_range_eq_top ğ•œ ğ“–

lemma UniversalEnvelopingAlgebra.induction
    (C : ğ“¤ ğ•œ ğ“– â†’ Prop) (hAM : âˆ€ r, C (algebraMap ğ•œ (ğ“¤ ğ•œ ğ“–) r))
    (hÎ¹ : âˆ€ X, C (Î¹UEA ğ•œ X))
    (hMul : âˆ€ a b, C a â†’ C b â†’ C (a * b)) (hAdd : âˆ€ a b, C a â†’ C b â†’ C (a + b)) (a : ğ“¤ ğ•œ ğ“–) :
    C a := by
  let C' : TensorAlgebra ğ•œ ğ“– â†’ Prop := fun t â†¦ C (UniversalEnvelopingAlgebra.mkAlgHom _ _ t)
  suffices âˆ€ t, C' t by
    obtain âŸ¨t, htâŸ© := UniversalEnvelopingAlgebra.mkAlgHom_surjective a
    simpa only [â† ht] using this t
  apply TensorAlgebra.induction (C := C')
  Â· intro r
    simpa [C'] using hAM r
  Â· intro X
    exact hÎ¹ X
  Â· intro ta tb hta htb
    simpa only [C', map_mul] using hMul _ _ hta htb
  Â· intro ta tb hta htb
    simpa only [C', map_add] using hAdd _ _ hta htb

lemma UniversalEnvelopingAlgebra.central_of_forall_lie_eq_zero
    {Z : ğ“–} (hZ : âˆ€ (X : ğ“–), â…Z, Xâ† = 0) (a : ğ“¤ ğ•œ ğ“–) :
    Commute (Î¹UEA ğ•œ Z) a := by
  apply UniversalEnvelopingAlgebra.induction ğ•œ ğ“–
        (fun b â†¦ Commute (UniversalEnvelopingAlgebra.Î¹ ğ•œ Z) b)
  Â· intro r
    exact Algebra.commute_algebraMap_right r ((UniversalEnvelopingAlgebra.Î¹ ğ•œ) Z)
  Â· intro X
    apply commute_iff_lie_eq.mpr
    rw [â† LieHom.map_lie, hZ X, LieHom.map_zero]
  Â· intro a b ha hb
    exact Commute.mul_right ha hb
  Â· intro a b ha hb
    exact Commute.add_right ha hb

/-- If a central element of a Lie algebra acts as a scalar multiplication on a cyclic
vector in a representation, then it acts as the same scalar on the whole representation. -/
lemma UniversalEnvelopingAlgebra.smul_eq_of_cyclic_of_forall_lie_eq_zero
    {Z : ğ“–} {Î¶ : ğ•œ} (hZ : âˆ€ (X : ğ“–), â…Z, Xâ† = 0)
    {V : Type*} [AddCommGroup V] [Module (ğ“¤ ğ•œ ğ“–) V]
    {w : V} (w_cyclic : Submodule.span (ğ“¤ ğ•œ ğ“–) {w} = âŠ¤)
    (hw : Î¹UEA ğ•œ Z â€¢ w = algebraMap ğ•œ (ğ“¤ ğ•œ ğ“–) Î¶ â€¢ w) (v : V) :
    Î¹UEA ğ•œ Z â€¢ v = algebraMap ğ•œ (ğ“¤ ğ•œ ğ“–) Î¶ â€¢ v := by
  have z_central (a : ğ“¤ ğ•œ ğ“–) : Commute (Î¹UEA ğ•œ Z) a :=
    UniversalEnvelopingAlgebra.central_of_forall_lie_eq_zero _ _ hZ _
  have Î¶_central (a : ğ“¤ ğ•œ ğ“–) : Commute (algebraMap ğ•œ _ Î¶) a :=
    Algebra.commute_algebraMap_left Î¶ a
  let goodSubspace := LinearMap.ker (centralSMulHom z_central V - centralSMulHom Î¶_central V)
  have good_iff (u : V) :
      u âˆˆ goodSubspace â†” Î¹UEA ğ•œ Z â€¢ u = algebraMap ğ•œ (ğ“¤ ğ•œ ğ“–) Î¶ â€¢ u := by
    simp only [LinearMap.mem_ker, LinearMap.sub_apply, goodSubspace]
    simpa only [centralSMulHom_apply] using sub_eq_zero
  rw [â† good_iff]
  suffices âŠ¤ â‰¤ goodSubspace from this Submodule.mem_top
  rw [â† w_cyclic]
  apply (Submodule.span_singleton_le_iff_mem ..).mpr
  exact (good_iff w).mpr hw

open ModuleOfModuleAlgebra in
/-- Any module `V` over the universal enveloping algebra of a Lie algebra is a representation of the
Lie algebra.

This is recorded on the type synonym `ModuleOfModuleAlgebra ğ•œ (ğ“¤ ğ•œ ğ“–) V` of `V`, in order to
make the `V` a `ğ•œ`-module and talk about a representation of a `ğ•œ`-Lie algebra on it. -/
def UniversalEnvelopingAlgebra.representation
    {V : Type*} [AddCommGroup V] [Module (ğ“¤ ğ•œ ğ“–) V] :
    LieAlgebra.Representation ğ•œ ğ•œ ğ“– (ModuleOfModuleAlgebra ğ•œ (ğ“¤ ğ•œ ğ“–) V) where
  toFun X := lsmul ğ•œ V (Î¹UEA ğ•œ X)
  map_add' X Y := by
    ext v
    simp only [LinearMap.add_apply, LieHom.map_add]
    exact Module.add_smul _ _ ((unMkAddHom ğ•œ (ğ“¤ ğ•œ ğ“–) V) v)
  map_smul' r X := by
    ext v
    simp only [lsmul_apply, LinearMap.smul_apply]
    simp only [LieHom.map_smul, RingHom.id_apply]
    set v' := unMkAddHom ğ•œ (ğ“¤ ğ•œ ğ“–) V v with def_v'
    set a := Î¹UEA ğ•œ X with def_a
    have : ((algebraMap ğ•œ (ğ“¤ ğ•œ ğ“–) r) â€¢ a) â€¢ v' = algebraMap ğ•œ (ğ“¤ ğ•œ ğ“–) r â€¢ a â€¢ v' :=
      IsScalarTower.smul_assoc ((algebraMap ğ•œ (ğ“¤ ğ•œ ğ“–)) r) a v'
    convert this using 1
    simp only [mkAddHom, AddMonoidHom.coe_mk, ZeroHom.coe_mk, mk, smul_eq_mul]
    congr 1
    exact algebra_compatible_smul (ğ“¤ ğ•œ ğ“–) r a
  map_lie' := by
    intro X Y
    ext v
    change (lsmul ğ•œ V (Î¹UEA ğ•œ â…X, Yâ†)) v
          = lsmul ğ•œ V (Î¹UEA ğ•œ X) (lsmul ğ•œ V (Î¹UEA ğ•œ Y) v)
            - lsmul ğ•œ V (Î¹UEA ğ•œ Y) (lsmul ğ•œ V (Î¹UEA ğ•œ X) v)
    simp only [LieHom.map_lie]
    set v' := unMkAddHom ğ•œ (ğ“¤ ğ•œ ğ“–) V v with def_v'
    set a := (Î¹UEA ğ•œ X) with def_a
    set b := (Î¹UEA ğ•œ Y) with def_b
    change (a * b - b * a) â€¢ v' = a â€¢ (b â€¢ v') - b â€¢ (a â€¢ v')
    simp [sub_smul, mul_smul]

section moduleUEA

variable {ğ•œ ğ•‚ : Type*} [CommRing ğ•œ] [CommRing ğ•‚]
variable  {ğ“– : Type*} [LieRing ğ“–] [LieAlgebra ğ•œ ğ“–]
variable {V : Type*} [AddCommGroup V] [Module ğ•‚ V] [Module ğ•œ V]
variable [SMul ğ•œ ğ•‚] [IsScalarTower ğ•œ ğ•‚ V] [SMulCommClass ğ•‚ ğ•œ V]
variable (Ï : LieAlgebra.Representation ğ•œ ğ•‚ ğ“– V)

/-- A representation of a `ğ•œ`-Lie algebra `ğ“–` on a vector space `V` defines a `ğ“¤ ğ•œ ğ“–`-module
structure on `V`. -/
noncomputable def LieAlgebra.Representation.moduleUniversalEnvelopingAlgebra :
    Module (ğ“¤ ğ•œ ğ“–) V where
  smul a v := UniversalEnvelopingAlgebra.lift ğ•œ Ï a v
  one_smul v := by
    change UniversalEnvelopingAlgebra.lift ğ•œ Ï (1 : ğ“¤ ğ•œ ğ“–) v = v
    simp
  mul_smul a b v := by
    change UniversalEnvelopingAlgebra.lift ğ•œ Ï (a * b) v
            = UniversalEnvelopingAlgebra.lift ğ•œ Ï a (UniversalEnvelopingAlgebra.lift ğ•œ Ï b v)
    simp
  smul_zero a := by
    change UniversalEnvelopingAlgebra.lift ğ•œ Ï a 0 = 0
    simp
  smul_add a vâ‚ vâ‚‚ := by
    change UniversalEnvelopingAlgebra.lift ğ•œ Ï a (vâ‚ + vâ‚‚)
            = UniversalEnvelopingAlgebra.lift ğ•œ Ï a vâ‚ + UniversalEnvelopingAlgebra.lift ğ•œ Ï a vâ‚‚
    simp
  add_smul aâ‚ aâ‚‚ v := by
    change UniversalEnvelopingAlgebra.lift ğ•œ Ï (aâ‚ + aâ‚‚) v
            = UniversalEnvelopingAlgebra.lift ğ•œ Ï aâ‚ v + UniversalEnvelopingAlgebra.lift ğ•œ Ï aâ‚‚ v
    simp
  zero_smul v := by
    change UniversalEnvelopingAlgebra.lift ğ•œ Ï 0 v = 0
    simp

@[simp] lemma LieAlgebra.Representation.moduleUniversalEnvelopingAlgebra_smul_eq
    (a : ğ“¤ ğ•œ ğ“–) (v : V) :
    (LieAlgebra.Representation.moduleUniversalEnvelopingAlgebra Ï).smul a v
      = UniversalEnvelopingAlgebra.lift ğ•œ Ï a v :=
  rfl

/-- The defining property of the `ğ“¤ ğ•œ ğ“–`-module structure on a representation `V` of a
`ğ•œ`-Lie algebra `ğ“–`. -/
lemma LieAlgebra.Representation.moduleUniversalEnvelopingAlgebra_Î¹UEA_smul_eq
    (X : ğ“–) (v : V) :
    (LieAlgebra.Representation.moduleUniversalEnvelopingAlgebra Ï).smul (Î¹UEA ğ•œ X) v = Ï X v := by
  simp only [UniversalEnvelopingAlgebra.Î¹_apply, moduleUniversalEnvelopingAlgebra_smul_eq,
             UniversalEnvelopingAlgebra.lift_Î¹_apply']

end moduleUEA

end UniversalEnvelopingAlgebra
